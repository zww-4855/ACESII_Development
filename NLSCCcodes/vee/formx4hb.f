      SUBROUTINE FORMX4HB(R1,ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE CALCULATES THE X INTERMEDIATES USED IN THE
C R1L2Y2H CONTRACTIONS.  THESE ARE:
C
C X(BN,EJ) = W(MJ,NB)*R(ME)  => X(BJ,EN) LIST 424->424
C X(bn,ej) = W(mj,nb)*R(me)  => X(bj,en) LIST 425->425
C X(bN,Ej) = W(Mj,Nb)*R(ME)  => X(bj,EN) LIST 458->427
C X(Bn,eJ) = W(Jm,Bn)*R(me)  => X(BJ,en) LIST 459->426
C X(Bn,Ej) = W(Mj,Bn)*R(ME)  => X(Bj,En) LIST 428->428
C X(bN,eJ) = W(Jm,Nb)*R(me)  => X(bJ,eN) LIST 429->429
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      DIMENSION ICORE(MAXCOR),R1(*),I0R(2)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
      I0R(1)=1
      IF(IUHF.NE.0)THEN
       I0R(2)=I0R(1)+IINTFP*IRPDPD(IRREPX,9)
      ELSE
       I0R(2)=I0R(1)
      ENDIF
      I000=1
C
      IF(IUHF.NE.0)THEN
C
C DO AAAA AND BBBB SPIN CASES
C
C X(EN,BJ) = W(MJ,NB)*R(ME)  
C
       DO 10 ISPIN=1,2
        LISTW=6+ISPIN
        LISTX=423+ISPIN
        DO 20 IRREPR=1,NIRREP
         IRREPL=DIRPRD(IRREPR,IRREPX)
         DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
         NUMDSW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
         DISSYWX=IRPDPD(IRREPR,20+ISPIN)
         DISSYX=IRPDPD(IRREPR,ISYTYP(1,LISTX))
         NUMDSX=IRPDPD(IRREPL,ISYTYP(2,LISTX))
         MAXW=MAX(DISSYWX,NUMDSW,DISSYX,NUMDSX)
         MAXSIZ=MAX(DISSYWX*NUMDSW,DISSYX*NUMDSX)
         I010=I000+IINTFP*MAXSIZ
         I020=I010+IINTFP*MAXSIZ
         ITMP1=I020
         ITMP2=ITMP1+IINTFP*MAXW
         ITMP3=ITMP2+IINTFP*MAXW
C
C READ W(MJ,NB), REORDER TO W(BN,JM)
C
         CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREPR,LISTW)
         CALL SYMTR1(IRREPR,POP(1,ISPIN),VRT(1,ISPIN),DISSYW,
     &               ICORE(I010),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
         CALL VMINUS(ICORE(I010),NUMDSW*DISSYW)
         CALL TRANSP(ICORE(I010),ICORE(I000),NUMDSW,DISSYW)
         CALL SYMEXP(IRREPR,POP(1,ISPIN),NUMDSW,ICORE(I000))
C
C FORM PRODUCT X(BN,EJ) = W(BN,JM)*R(EM)
C
         DO 30 IRREPE=1,NIRREP
          IRREPM=DIRPRD(IRREPE,IRREPX)
          IRREPJ=DIRPRD(IRREPM,IRREPR)
          NUMM=POP(IRREPM,ISPIN)
          NUMJ=POP(IRREPJ,ISPIN)
          NUME=VRT(IRREPE,ISPIN)
          NROW=DISSYX*NUMJ
          NCOL=NUME
          NSUM=NUMM
          IOFFX=I010+IINTFP*(ISYMOFF(IRREPE,IRREPL,15+ISPIN)-1)*DISSYX
          IOFFW=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,20+ISPIN)-1)*NUMDSW
          IOFFR=I0R(ISPIN)+IINTFP*(ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1)
          CALL XGEMM('N','T',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &               R1(IOFFR),NCOL,ZILCH,ICORE(IOFFX),NROW)
30       CONTINUE
         CALL SYMTR1(IRREPL,POP(1,ISPIN),VRT(1,ISPIN),DISSYX,
     &               ICORE(I010),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
         CALL GETLST(ICORE(I000),1,NUMDSX,1,IRREPL,LISTX)
         CALL SAXPY (DISSYX*NUMDSX,ONEM,ICORE(I000),1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),1,NUMDSX,1,IRREPL,LISTX)
C
20      CONTINUE
C
C NOW RESORT X(BN,EJ) -> X(BJ,EN)
C
        ISIZE=IDSYMSZ(IRREPX,ISYTYP(1,LISTX),ISYTYP(2,LISTX))
        I010=I000+IINTFP*ISIZE
        I020=I010+IINTFP*ISIZE
        CALL GETALL(ICORE(I000),ISIZE,IRREPX,LISTX)
        CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,
     &              VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     &              ICORE(I020),IRREPX,'1432')
        CALL PUTALL(ICORE(I010),ISIZE,IRREPX,LISTX)
10     CONTINUE
C
      ENDIF
C
C X(bN,Ej) = W(Mj,Nb)*R(ME)  => X(bj,EN) LIST 438->456
C X(Bn,eJ) = W(Jm,Bn)*R(me)  => X(BJ,en) LIST 439->457
C
      DO 110 ISPIN=1,1+IUHF
       LISTW=11-ISPIN
       LISTX =457+ISPIN+(1-IUHF)
       LISTX0=428-ISPIN
       IF(IUHF.EQ.0)LISTX0=426
       DO 120 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
        NUMDSW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
        DISSYX=IRPDPD(IRREPR,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPL,ISYTYP(2,LISTX))
        MAXW=MAX(DISSYW,NUMDSW,DISSYX,NUMDSX)
        MAXSIZ=MAX(DISSYW*NUMDSW,DISSYX*NUMDSX)
        I010=I000+IINTFP*MAXSIZ
        I020=I010+IINTFP*MAXSIZ
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXW
        ITMP3=ITMP2+IINTFP*MAXW
C
C READ W(Mj,Nb) -> R(bN,jM) [ISPIN=1]
C READ W(Jm,Bn) -> R(Bn,Jm) [ISPIN=2]
C
        CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREPR,LISTW)
        IF(IUHF.EQ.0)THEN
         CALL SPINAD3(IRREPR,POP(1,1),DISSYW,NUMDSW,ICORE(I010),
     &                ICORE(ITMP1),ICORE(ITMP2))
        ENDIF
        IF(ISPIN.EQ.1)THEN 
         CALL SYMTR1(IRREPR,POP(1,1),VRT(1,2),DISSYW,ICORE(I010),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
         CALL SYMTR3(IRREPR,POP(1,1),POP(1,2),DISSYW,NUMDSW,ICORE(I010),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        ENDIF
        CALL TRANSP(ICORE(I010),ICORE(I000),NUMDSW,DISSYW)
C
C FORM PRODUCT 
C         X(bN,Ej) = W(bN,jM)*R(ME) [ISPIN=1]
C         X(Bn,eJ) = W(Bn,Jm)*R(me) [ISPIN=2]
C
        DO 130 IRREPE=1,NIRREP
         IRREPM=DIRPRD(IRREPE,IRREPX)
         IRREPJ=DIRPRD(IRREPM,IRREPR)
         NUMM=POP(IRREPM,ISPIN)
         NUMJ=POP(IRREPJ,3-ISPIN)
         NUME=VRT(IRREPE,ISPIN)
         NROW=DISSYX*NUMJ
         NCOL=NUME
         NSUM=NUMM
         IF(ISPIN.EQ.1)THEN
          IOFFX=I010+IINTFP*(ISYMOFF(IRREPE,IRREPL,25)-1)*DISSYX
          IOFFW=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,24)-1)*NUMDSW
         ELSE
          IOFFX=I010+IINTFP*(ISYMOFF(IRREPE,IRREPL,18)-1)*DISSYX
          IOFFW=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,14)-1)*NUMDSW
         ENDIF
         IOFFR=I0R(ISPIN)+IINTFP*(ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1)
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &              R1(IOFFR),NCOL,ZILCH,ICORE(IOFFX),NROW)
130     CONTINUE
        CALL SYMTR1(IRREPL,POP(1,3-ISPIN),VRT(1,ISPIN),DISSYX,
     &              ICORE(I010),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL GETLST(ICORE(I000),1,NUMDSX,1,IRREPL,LISTX)
        CALL SAXPY (DISSYX*NUMDSX,ONEM,ICORE(I000),1,ICORE(I010),1)
        CALL PUTLST(ICORE(I010),1,NUMDSX,1,IRREPL,LISTX)
C
120    CONTINUE
C
C NOW RESORT X(bn,ej) -> X(bj,en)
C
       ISIZE=IDSYMSZ(IRREPX,ISYTYP(1,LISTX),ISYTYP(2,LISTX))
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
       CALL GETALL(ICORE(I000),ISIZE,IRREPX,LISTX)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,
     &             VRT(1,3-ISPIN),POP(1,ISPIN),
     &             VRT(1,ISPIN),POP(1,3-ISPIN),
     &             ICORE(I020),IRREPX,'1432')
       CALL PUTALL(ICORE(I010),ISIZE,IRREPX,LISTX0)
110   CONTINUE
C
C X(Bn,Ej) = W(Mj,Bn)*R(ME)  => X(Bj,En) LIST 458->458
C X(bN,eJ) = W(Jm,Nb)*R(me)  => X(bJ,eN) LIST 459->459
C
      DO 210 ISPIN=2,2-IUHF,-1
       LISTW=8+ISPIN
       IF(IUHF.EQ.0)THEN
        LISTX=428
       ELSE
        LISTX=427+ISPIN
       ENDIF
       DO 220 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
        NUMDSW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
        DISSYX=IRPDPD(IRREPR,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPL,ISYTYP(2,LISTX))
        MAXW=MAX(DISSYW,NUMDSW,DISSYX,NUMDSX)
        MAXSIZ=MAX(DISSYW*NUMDSW,DISSYX*NUMDSX)
        I010=I000+IINTFP*MAXSIZ
        I020=I010+IINTFP*MAXSIZ
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXW
        ITMP3=ITMP2+IINTFP*MAXW
C
C READ W(Mj,Bn) -> W(Bn,jM) [ISPIN=1]
C READ W(Jm,Nb) -> W(bN,Jm) [ISPIN=2]
C
        CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREPR,LISTW)
        IF(ISPIN.EQ.1)THEN 
         CALL SYMTR3(IRREPR,POP(1,1),POP(1,2),DISSYW,NUMDSW,ICORE(I010),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        ELSE
         CALL SYMTR1(IRREPR,POP(1,1),VRT(1,2),DISSYW,ICORE(I010),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        ENDIF
        CALL TRANSP(ICORE(I010),ICORE(I000),NUMDSW,DISSYW)
C
C FORM PRODUCT 
C         X(Bn,Ej) = W(Bn,jM)*R(ME) [ISPIN=1]
C         X(bN,eJ) = W(bN,Jm)*R(me) [ISPIN=2]
C
        DO 230 IRREPE=1,NIRREP
         IRREPM=DIRPRD(IRREPE,IRREPX)
         IRREPJ=DIRPRD(IRREPM,IRREPR)
         NUMM=POP(IRREPM,ISPIN)
         NUMJ=POP(IRREPJ,3-ISPIN)
         NUME=VRT(IRREPE,ISPIN)
         NROW=DISSYX*NUMJ
         NCOL=NUME
         NSUM=NUMM
         IF(ISPIN.EQ.1)THEN
          IOFFX=I010+IINTFP*(ISYMOFF(IRREPE,IRREPL,25)-1)*DISSYX
          IOFFW=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,24)-1)*NUMDSW
         ELSE
          IOFFX=I010+IINTFP*(ISYMOFF(IRREPE,IRREPL,18)-1)*DISSYX
          IOFFW=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,14)-1)*NUMDSW
         ENDIF
         IOFFR=I0R(ISPIN)+IINTFP*(ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1)
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &              R1(IOFFR),NCOL,ZILCH,ICORE(IOFFX),NROW)
230     CONTINUE
        CALL SYMTR1(IRREPL,POP(1,3-ISPIN),VRT(1,ISPIN),DISSYX,
     &              ICORE(I010),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL GETLST(ICORE(I000),1,NUMDSX,1,IRREPL,LISTX)
        CALL SAXPY (DISSYX*NUMDSX,ONEM,ICORE(I000),1,ICORE(I010),1)
        CALL PUTLST(ICORE(I010),1,NUMDSX,1,IRREPL,LISTX)
C
220    CONTINUE
C
C NOW RESORT X(bn,ej) -> X(bj,en)
C
       ISIZE=IDSYMSZ(IRREPX,ISYTYP(1,LISTX),ISYTYP(2,LISTX))
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
       CALL GETALL(ICORE(I000),ISIZE,IRREPX,LISTX)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,
     &             VRT(1,ISPIN),POP(1,3-ISPIN),
     &             VRT(1,ISPIN),POP(1,3-ISPIN),
     &             ICORE(I020),IRREPX,'1432')
       CALL PUTALL(ICORE(I010),ISIZE,IRREPX,LISTX)
210   CONTINUE
C
      RETURN
      END
