      SUBROUTINE TPDIJKA2(ICORE,MAXCOR,IUHF)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL MBPT2,CC,CCD,RCCD,DRCCD,LCCD,LCCSD,CC2
      INTEGER DIRPRD,DISSYG,DISSYL,DISSYR,POP,VRT
      DIMENSION ICORE(MAXCOR)
      DIMENSION I0T(2),I0R(2)
C
C CALCULATION OF THE SECOND IJKA CONTRIBUTION TO
C THE EOM-CCSD TWO-PARTICLE DENSITY MATRIX
C
C G(IJ,KA) =  1/2  T(M,A) 1/2 { R(IJ,EF) + R(I,E) T(J,F) + T(I,E) R(J,F)
C
C                              -R(I,F) T(J,E) - T(I,F) R(J,E)} L(KM,EF)
C 
CEND 
C
C CODED JG SEPTEMBER/93
C
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /REFTYPE/ MBPT2,CC,CCD,RCCD,DRCCD,LCCD,LCCSD,CC2
C
      DATA AZERO,HALF,ONE /0.D0,0.5D0,1.0D0/

C READ IN T1, R1, AND L1 AMPLITUDES
C
      I0T(1)=1
      I0T(2)=I0T(1)+IRPDPD(1,9)*IINTFP*IUHF
      I0R(1)=I0T(2)+IRPDPD(1,10)*IINTFP
      I0R(2)=I0R(1)+IRPDPD(IRREPX,9)*IINTFP*IUHF
      ISTART=I0R(2)+IRPDPD(IRREPX,10)*IINTFP
C
      CALL GETLST(ICORE(I0R(1)),1,1,1,3,490)
      CALL GETLST(ICORE(I0T(1)),1,1,1,1,90)
      IF(IUHF.EQ.1) THEN
       CALL GETLST(ICORE(I0R(2)),1,1,1,4,490)
       CALL GETLST(ICORE(I0T(2)),1,1,1,2,90)
      ENDIF
C
C LOOP OVER SPIN CASES
C
      DO 1000 ISPIN=3-2*IUHF,3
C
C LOOP OVER IRREPS
C
       DO 100 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPR,IRREPX) 
C
C GET R(TWIDDLE)
C
        LISTR=460+ISPIN
        LISTL=443+ISPIN
C
        NUMSYR=IRPDPD(IRREPR,ISYTYP(2,LISTR))
        DISSYR=IRPDPD(IRREPL,ISYTYP(1,LISTR))
        NUMSYL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
C
C  ALLOCATE MEMORY
C
        I000=ISTART
        I010=I000+IINTFP*NUMSYR*NUMSYL
        I020=I010+IINTFP*NUMSYR*DISSYR
        IEND=I020+IINTFP*NUMSYL*DISSYL
        IF(IEND.GE.MAXCOR) CALL INSMEM('TPDIJKA2',IEND,MAXCOR)
C
C GET R AMPLITUDE
C
        IF (CC2) THEN
           CALL ZERO(ICORE(I010),NUMSYR*DISSYR)
        ELSE
           CALL GETLST(ICORE(I010),1,NUMSYR,1,IRREPR,LISTR)
        ENDIF
C
C FORM R(TWIDDLE)
C
        IF(ISPIN.LE.2) THEN
         CALL DTAU(IRREPL,IRREPR,IRREPX,1,ICORE(I010),
     &             ICORE(I0R(ISPIN)),ICORE(I0R(ISPIN)),
     &             ICORE(I0T(ISPIN)),ICORE(I0T(ISPIN)),
     &             ISPIN,ONE)
        ELSE
         CALL DTAU(IRREPL,IRREPR,IRREPX,1,ICORE(I010),
     &             ICORE(I0R(1)),ICORE(I0R(2)),
     &             ICORE(I0T(1)),ICORE(I0T(2)),
     &             3,ONE)
        ENDIF
C
C GET L AMPLITUDES
C
        CALL GETLST(ICORE(I020),1,NUMSYL,1,IRREPR,LISTL)
C
C PERFORM MULTIPLICATION
C
        CALL XGEMM('T','N',NUMSYR,NUMSYL,DISSYR,ONE,ICORE(I010),
     &             DISSYR,ICORE(I020),DISSYL,AZERO,ICORE(I000),
     &             NUMSYR)       
C
C MULTIPLY  R(TWIDDLE)*L WITH T1
C
        IF(ISPIN.LE.2) THEN
C
C SPIN CASES AAAA AND BBBB
C
         LISTG=106+ISPIN
C
         NUMSYG=IRPDPD(IRREPR,ISYTYP(2,LISTG))
         DISSYG=IRPDPD(IRREPR,ISYTYP(1,LISTG))
         NOCCSQ=IRPDPD(IRREPR,20+ISPIN)
         I010=I000+IINTFP*NUMSYR*NOCCSQ
         I015=I010+IINTFP*NUMSYG*DISSYG
         IEND=I015+IINTFP*NUMSYG*DISSYG
         IF(IEND.GE.MAXCOR) CALL INSMEM('TPDIJKA2',IEND,MAXCOR) 
C
C EXPAND FIRST RIGHT-HAND SIDE OF R*L PRODUCT
C
         CALL SYMEXP(IRREPR,POP(1,ISPIN),NUMSYR,ICORE(I000))
C
C PERFORM MULTIPLICATION
C 
         IOFFT=0
         IOFFG=0
         IOFFRL=0
C
         DO 150 IRREPRR=1,NIRREP
          IRREPRL=DIRPRD(IRREPR,IRREPRR)
          NVRTA=VRT(IRREPRR,ISPIN)
          NOCCM=POP(IRREPRR,ISPIN)
          NOCCK=POP(IRREPRL,ISPIN)
C
          CALL XGEMM('N','T',NUMSYR*NOCCK,NVRTA,NOCCM,HALF,
     &              ICORE(I000+IOFFRL),NUMSYR*NOCCK,
     &              ICORE(I0T(ISPIN)+IOFFT),NVRTA,AZERO,
     &              ICORE(I010+IOFFG),NUMSYR*NOCCK)
C
C UPDATE OFFSETS
C
          IOFFT=IOFFT+IINTFP*NVRTA*NOCCM
          IOFFRL=IOFFRL+IINTFP*NOCCK*NOCCM*NUMSYR
          IOFFG=IOFFG+IINTFP*NOCCK*NVRTA*NUMSYR
C
150      CONTINUE
C
C SAVE G(IJ,KA) ON DISK   (UPDATE ????)
C
          call checksum('tpdijka2',icore(i010),numsyg*dissyg,s)
         CALL GETLST(ICORE(I015),1,NUMSYG,1,IRREPR,LISTG)
         CALL SAXPY (NUMSYG*DISSYG,ONE,ICORE(I015),1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),1,NUMSYG,1,IRREPR,LISTG)
C
        ELSE
C
C SPIN CASES ABAB AND BABA
C
         DO 250 ISPIN2=1,1+IUHF
C
          LISTG=111-ISPIN2
C
          NUMSYG=IRPDPD(IRREPR,ISYTYP(2,LISTG))
          DISSYG=IRPDPD(IRREPR,ISYTYP(1,LISTG))
          I010=I000+IINTFP*MAX(NUMSYG*DISSYG,NUMSYR*NUMSYL)
          I015=I010+IINTFP*NUMSYG*DISSYG
          ITMP=I015+IINTFP*NUMSYG*DISSYG
          IEND=ITMP+3*MAX(DISSYG,NUMSYG,NUMSYR)
          IF(IEND.GE.MAXCOR) CALL INSMEM('TPDIJKA2',IEND,MAXCOR) 
    
          IF(ISPIN2.EQ.1) THEN
           IS1=1
           IS2=2
          ELSE
           IS1=2
           IS2=1
           CALL SYMTR1(IRREPR,POP(1,1),POP(1,2),NUMSYR,ICORE(I000),
     &                 ICORE(ITMP),ICORE(ITMP+IINTFP*NUMSYR),
     &                 ICORE(ITMP+2*IINTFP*NUMSYR))
          ENDIF
C
C PERFORM MULTIPLICATION
C
          IOFFT=0
          IOFFG=0
          IOFFRL=0
C
          DO 240 IRREPRR=1,NIRREP
           IRREPRL=DIRPRD(IRREPR,IRREPRR)
           NVRTA=VRT(IRREPRR,IS2)
           NOCCM=POP(IRREPRR,IS2)
           NOCCK=POP(IRREPRL,IS1)
C
           CALL XGEMM('N','T',NUMSYR*NOCCK,NVRTA,NOCCM,HALF,
     &                ICORE(I000+IOFFRL),NUMSYR*NOCCK,
     &                ICORE(I0T(IS2)+IOFFT),NVRTA,AZERO,
     &                ICORE(I010+IOFFG),NUMSYR*NOCCK)
C
C UPDATE OFFSETS
C
           IOFFT=IOFFT+IINTFP*NVRTA*NOCCM
           IOFFRL=IOFFRL+IINTFP*NOCCK*NOCCM*NUMSYR
           IOFFG=IOFFG+IINTFP*NOCCK*NVRTA*NUMSYR
C
240       CONTINUE
C
          IF(ISPIN2.EQ.2) THEN
           CALL SYMTR1(IRREPR,POP(1,2),VRT(1,1),NUMSYR,ICORE(I010),
     &                 ICORE(ITMP),ICORE(ITMP+IINTFP*NUMSYR),
     &                 ICORE(ITMP+2*IINTFP*NUMSYR))
          ENDIF
C
C SAVE G(IJ,KA) ON DISK  (UPDATE ????)
C
          call checksum('tpdijka2',icore(i010),numsyg*dissyg,s)
          CALL GETLST(ICORE(I015),1,NUMSYG,1,IRREPR,LISTG)
          CALL SAXPY (NUMSYG*DISSYG,ONE,ICORE(I015),1,ICORE(I010),1)
          CALL PUTLST(ICORE(I010),1,NUMSYG,1,IRREPR,LISTG)
C
250      CONTINUE 
C
        ENDIF
C
100    CONTINUE
C
1000  CONTINUE
C
C ALL DONE, RETURN
C
      TWO=0.D0
      if(iuhf.eq.0) then
       call checkgam1(icore,10,110,two,iuhf,2,pop) 
      endif
      IF(IUHF.EQ.1) THEN
       CALL CHECKGAM(ICORE,10,110,TWO)
       CALL CHECKGAM(ICORE,7,107,TWO)
       CALL CHECKGAM(ICORE,8,108,TWO)
       CALL CHECKGAM(ICORE,9,109,TWO)
      ENDIF
C
      RETURN
      END
