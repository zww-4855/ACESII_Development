      SUBROUTINE R1L2Y2I(F1,R1,ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = R(me)*F(ie)*L(ab,mj)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      DIMENSION ICORE(MAXCOR),R1(*),F1(*),I0X(2),I0F(2),I0R(2)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
C CALCULATE X(mi) = R(me)*F(ie) INTERMEDIATES
C
      I0X(1)=1
      I0F(1)=1
      I0R(1)=1
      IF(IUHF.NE.0)THEN
       I0X(2)=I0X(1)+IINTFP*IRPDPD(IRREPX,21)
       I0F(2)=I0F(1)+IINTFP*NT(1)
       I0R(2)=I0R(1)+IINTFP*IRPDPD(IRREPX,9)
      ELSE
       I0X(2)=I0X(1)
       I0F(2)=I0F(1)
       I0R(2)=I0R(1)
      ENDIF
      I000=I0X(2)+IINTFP*IRPDPD(IRREPX,22)
      DO 5 ISPIN=1,1+IUHF
       DO 6 IRREPI=1,NIRREP
        IRREPM=DIRPRD(IRREPI,IRREPX)
        IRREPE=IRREPI
        NUMM=POP(IRREPM,ISPIN)
        NUMI=POP(IRREPI,ISPIN)
        NUME=VRT(IRREPE,ISPIN)
        NROW=NUMM
        NCOL=NUMI
        NSUM=NUME
        IOFFR=I0R(ISPIN)+IINTFP*(ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1)
        IOFFF=I0F(ISPIN)+IINTFP*(ISYMOFF(IRREPI,1,8+ISPIN)-1)
        IOFFX=I0X(ISPIN)+IINTFP*(ISYMOFF(IRREPI,IRREPX,20+ISPIN)-1)
        CALL XGEMM('T','N',NROW,NCOL,NSUM,ONE,R1(IOFFR),NSUM,
     &             F1(IOFFF),NSUM,ZILCH,ICORE(IOFFX),NROW)
6      CONTINUE
5     CONTINUE
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE.
C
C   Y(Ab,Ij) = Q(Ab,Ij) + Q(Ba,Ji)
C
C WHERE
C
C   Q(Ab,Ij) = R(ME)*F(IE)*L(Ab,Mj)
C
C
C READ IN L(Ab,Mj) = L(Ba,Jm) AND FORM CONTRACTIONS
C
       DO 10 IRREPR=1,NIRREP
        LISTL=446
        LISTZ=63 
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        MAXT  =MAX(DISSYL,NUMDSL,DISSYZ,NUMDSZ)
        I010=I000+IINTFP*MAX(DISSYL*NUMDSL,3*MAXT,DISSYZ*NUMDSZ)
        I020=I010+IINTFP*DISSYZ*NUMDSZ
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXT
        ITMP3=ITMP2+IINTFP*MAXT
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
C
C  Q(Ba,Ji) = L(Ba,Jm) * X(MI)
C
        DO 20 IRREPI=1,NIRREP
         IRREPM=DIRPRD(IRREPX,IRREPI)
         IRREPJ=DIRPRD(IRREPM,IRREPR)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,1)
         NUMM=POP(IRREPM,1)
         NROW=DISSYL*NUMJ
         NCOL=NUMI
         NSUM=NUMM
         IOFFL=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,21)-1)*DISSYL
         IOFFX=I0X(1)+IINTFP*(ISYMOFF(IRREPI,IRREPX,21)-1)
         IOFFZ=I010+IINTFP*(ISYMOFF(IRREPI,IRREPL,21)-1)*DISSYZ
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),NROW,
     &              ICORE(IOFFX),NSUM,ZILCH,ICORE(IOFFZ),NROW)
20      CONTINUE
        CALL SYMRHF(IRREPL,VRT(1,1),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONEM,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
C
10     CONTINUE
C
      ELSE
C
C UHF
C
C AAAA AND BBBB
C
C       Y1(AB,IJ) = L(AB,MJ)*X(MI) + ANTISYMMETRIZATION
C

       DO 100 ISPIN=1,2
        DO 110 IRREPR=1,NIRREP
         LISTL=443+ISPIN
         LISTZ=60+ISPIN
         IRREPL=DIRPRD(IRREPR,IRREPX)
         DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
         NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
         NUMDSLX=IRPDPD(IRREPR,20+ISPIN)
         DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
         NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
         NUMDSZX=IRPDPD(IRREPL,20+ISPIN)
         MAXT  =MAX(DISSYL,NUMDSLX,DISSYZ,NUMDSZX)
         I010=I000+IINTFP*MAX(DISSYL*NUMDSLX,3*MAXT,DISSYZ*NUMDSZX)
         I020=I010+IINTFP*MAX(DISSYZ*NUMDSZX,DISSYL*NUMDSLX)
         ITMP1=I020
         ITMP2=ITMP1+IINTFP*MAXT
         ITMP3=ITMP2+IINTFP*MAXT
         CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
         CALL SYMEXP(IRREPR,POP(1,ISPIN),DISSYL,ICORE(I000))
C
C  -Y1(A<B,IJ) = Y1(A<B,JI) = L(A<B,JM)*X(MI)
C
         DO 120 IRREPI=1,NIRREP
          IRREPM=DIRPRD(IRREPX,IRREPI)
          IRREPJ=DIRPRD(IRREPM,IRREPR)
          NUMI=POP(IRREPI,ISPIN)
          NUMJ=POP(IRREPJ,ISPIN)
          NUMM=POP(IRREPM,ISPIN)
          NROW=DISSYL*NUMJ
          NCOL=NUMI
          NSUM=NUMM
          IOFFL=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,20+ISPIN)-1)*DISSYL
          IOFFX=I0X(ISPIN)+IINTFP*(ISYMOFF(IRREPI,IRREPX,20+ISPIN)-1)
          IOFFZ=I010+IINTFP*(ISYMOFF(IRREPI,IRREPL,20+ISPIN)-1)*DISSYZ
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),NROW,
     &               ICORE(IOFFX),NSUM,ZILCH,ICORE(IOFFZ),NROW)
120      CONTINUE
         CALL ASSYM2(IRREPL,POP(1,ISPIN),DISSYZ,ICORE(I010))
         CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
         CALL SAXPY (NUMDSZ*DISSYZ,ONEM,ICORE(I010),1,ICORE(I000),1)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
C
110     CONTINUE
C
100    CONTINUE
C
C ABAB : Y1(Ab,Ij) = L(Ab,Im)*X(mj) + L(Ab,Mj)*X(MI)
C
       DO 210 IRREPR=1,NIRREP
        LISTL=446
        LISTZ=63
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        MAXT  =MAX(DISSYL,NUMDSL,DISSYZ,NUMDSZ)
        I010=I000+IINTFP*MAX(DISSYL*NUMDSL,3*MAXT,DISSYZ*NUMDSZ)
        I020=I010+IINTFP*MAX(DISSYZ*NUMDSZ,DISSYL*NUMDSL)
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXT
        ITMP3=ITMP2+IINTFP*MAXT
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL SYMTR1(IRREPR,POP(1,1),POP(1,2),DISSYL,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C  Y1(Ab,jI) <= L(Ab,jM)*X(MI)
C
        DO 220 IRREPI=1,NIRREP
         IRREPM=DIRPRD(IRREPX,IRREPI)
         IRREPJ=DIRPRD(IRREPM,IRREPR)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
         NUMM=POP(IRREPM,1)
         NROW=DISSYL*NUMJ
         NCOL=NUMI
         NSUM=NUMM
         IOFFL=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,24)-1)*DISSYL
         IOFFX=I0X(1)+IINTFP*(ISYMOFF(IRREPI,IRREPX,21)-1)
         IOFFZ=I010+IINTFP*(ISYMOFF(IRREPI,IRREPL,24)-1)*DISSYZ
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),NROW,
     &              ICORE(IOFFX),NSUM,ZILCH,ICORE(IOFFZ),NROW)
220     CONTINUE
C
C Y1(Ab,jI) -> Y1(Ab,Ij)
C L (Ab,mI) -> L(Ab,Im)
C
        CALL SYMTR1(IRREPR,POP(1,2),POP(1,1),DISSYL,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL SYMTR1(IRREPL,POP(1,2),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C  Y1(Ab,Ij) <= L(Ab,Im)*X(mj)
C
        DO 230 IRREPJ=1,NIRREP
         IRREPM=DIRPRD(IRREPX,IRREPJ)
         IRREPI=DIRPRD(IRREPM,IRREPR)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
         NUMM=POP(IRREPM,2)
         NROW=DISSYL*NUMI
         NCOL=NUMJ
         NSUM=NUMM
         IOFFL=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,14)-1)*DISSYL
         IOFFX=I0X(2)+IINTFP*(ISYMOFF(IRREPJ,IRREPX,22)-1)
         IOFFZ=I010+IINTFP*(ISYMOFF(IRREPJ,IRREPL,14)-1)*DISSYZ
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),NROW,
     &              ICORE(IOFFX),NSUM,ONE,ICORE(IOFFZ),NROW)
230     CONTINUE
C
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
        CALL SAXPY (DISSYZ*NUMDSZ,ONEM,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
C
210    CONTINUE 
C
      ENDIF
C
      RETURN
      END
