      SUBROUTINE R2L2Y1HI(Y1,ICORE,MAXCOR,IUHF)
C
C Y2(ai) = R(mn,ef)*L(im,ge)*W(gn,af) - R(mn,ef)*L(mo,ea)*W(in,of)
C
C ONLY SPIN-ADAPTED RHF CONTRACTIONS ARE CODED THUS FAR, UHF VERSIONS
C CAN BE FOUND IN ROUTINES R2L2Y1H AND R2L2Y1I
C  
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,TWO,FACT,HALF,HALFM
      DOUBLE PRECISION THRHALFM,THRHALF
      DIMENSION ICORE(MAXCOR),Y1(*)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,TWO,ZILCH/1.0D0,-1.0D0,2.0D0,0.0D0/
      DATA THRHALF,THRHALFM,HALF,HALFM/1.5D0,-1.5D0,0.5D0,-0.5D0/
C
C SPIN ADAPTED RHF CODE.
C
C            _        _        _ 
C Y2(AI)=1/2 R(Mn,Ef)*L(Im,Ge)*W(Gn,Af) - 3/2 R(Nm,Ef)*L(Mi,Ge)*W(Gn,Fa)
C
C            _        _        _ 
C       -1/2 R(Mn,Ef)*L(Mo,Ea)*W(In,Of) + 3/2 R(Nm,Ef)*L(Om,Ea)*W(Ni,Of)
C
C WHERE 
C
       DO 10 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
        LISTR1=457
        LISTR2=459
        LISTSAV=137
        DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTR1))
        NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTR1))
        I000=1
C
C FIRST FORM 2*R(Mn,Ef) - R(Mn,Fe) AND PUT IN R(EM,fn) MATRIX
C
        DISSYR=IRPDPD(IRREPL,ISYTYP(1,LISTR1))
        NUMDSR=IRPDPD(IRREPR,ISYTYP(2,LISTR1))
        I010=I000+IINTFP*MAX(DISSYR*NUMDSR,DISSYZ*NUMDSZ)
        I020=I010+IINTFP*MAX(DISSYR*NUMDSR,DISSYZ*NUMDSZ)
        IF(I020.GE.MAXCOR) CALL INSMEM('R2L2Y1HI1',I020,MAXCOR)
        CALL GETLST(ICORE(I000),1,NUMDSR,1,IRREPR,LISTR1)
        CALL GETLST(ICORE(I010),1,NUMDSR,1,IRREPR,LISTR2)
        CALL SSCAL (NUMDSR*DISSYR,TWO,ICORE(I000),1)
        CALL SAXPY (NUMDSR*DISSYR,ONEM,ICORE(I010),1,ICORE(I000),1)
C
C NOW FORM 2*L(Im,Ae)-L(Im,Ea) AND PUT IN L(em,AI) MATRIX
C
        LISTL1=437
        LISTL2=439
        DISSYL=IRPDPD(IRREPR,ISYTYP(1,LISTL1))
        NUMDSL=IRPDPD(IRREPL,ISYTYP(2,LISTL1))
        I020=I010+IINTFP*MAX(DISSYL*NUMDSL,DISSYZ*NUMDSZ)
        I030=I020+IINTFP*MAX(DISSYL*NUMDSL,DISSYZ*NUMDSZ)
        IF(I030.GE.MAXCOR) CALL INSMEM('R2L2Y1HI2',I030,IEND)
        CALL GETLST(ICORE(I010),1,NUMDSL,1,IRREPL,LISTL1)
        CALL GETLST(ICORE(I020),1,NUMDSL,1,IRREPL,LISTL2)
        CALL SSCAL (NUMDSL*DISSYL,TWO,ICORE(I010),1)
        CALL SAXPY (NUMDSL*DISSYL,ONEM,ICORE(I020),1,ICORE(I010),1)
C           _        _
C CALCULATE L(ai,EM)*R(EM,fn) = Q(ai,fn) AND PUT ON RESORTED LAMBDA LISTS
C
        CALL XGEMM('N','N',DISSYL,NUMDSR,DISSYR,ONE,ICORE(I010),
     &             DISSYL,ICORE(I000),DISSYR,ZILCH,ICORE(I020),DISSYL)
        CALL PUTLST(ICORE(I020),1,NUMDSR,1,IRREPR,LISTSAV)
C
10     CONTINUE
C
C RESORT INTERMEDIATES
C
C          Q(gi,fn) -> Q(gn,fi)
C          Q(ao,fn) -> Q(an,fo)
C
       ISIZE=ISYMSZ(ISYTYP(1,137),ISYTYP(2,137))
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
       IF(I020.GE.MAXCOR) CALL INSMEM('R2L2Y1HI3',I020,IEND)
       CALL GETALL(ICORE(I000),ISIZE,1,LISTSAV)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,1),
     &             VRT(1,1),POP(1,1),ICORE(I020),1,'1432')
       CALL PUTALL(ICORE(I010),ISIZE,1,LISTSAV)
C
       LISTQ=137
       DO 20 IRREP=1,NIRREP
        DISSYQ=IRPDPD(IRREP,ISYTYP(1,LISTQ))
        NUMDSQ=IRPDPD(IRREP,ISYTYP(2,LISTQ))
        MAXQ=MAX(DISSYQ,NUMDSQ)
        I010=I000+IINTFP*NUMDSQ*DISSYQ
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXQ
        ITMP3=ITMP2+IINTFP*MAXQ
        IEND=ITMP3+IINTFP*MAXQ
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y1HI4',I020,IEND)
        CALL GETLST(ICORE(I000),1,NUMDSQ,1,IRREP,LISTQ)
        CALL SYMTR1(IRREP,VRT(1,1),POP(1,1),DISSYQ,ICORE(I000),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C FIRST SET OF CONTRACTIONS.  TREAT Q AS Q(gn,if)
C
        LISTW=30
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))  
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))  
        MAXW=MAX(NUMDSW,DISSYW)
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXW
        ITMP3=ITMP2+IINTFP*MAXW 
        ISTART=ITMP3+IINTFP*MAXW
        MAXDIS=(MAXCOR-ISTART)/(2*IINTFP*MAX(1,DISSYW))
        IF(MAXDIS.EQ.0) CALL INSMEM('R2L2Y1HI5',MAXDIS*DISSYW,DISSYW)
        I001=ISTART
        I002=I001+IINTFP*MAXDIS*DISSYW
C
        IOFFLIST=1
        NLEFT=NUMDSW
C
1       CONTINUE
C
         NREAD=MIN(NLEFT,MAXDIS)
         NLEFT=NLEFT-NREAD
C                                                      _
C READ W(Af,Gn) INTO CORE, SPIN-ADAPT AND TRANSPOSE TO W(Gn,Af)
C
         CALL GETLST(ICORE(I001),IOFFLIST,NREAD,1,IRREP,LISTW)
         CALL SPINAD3(IRREP,VRT(1,1),DISSYW,NREAD,ICORE(I001),
     &                ICORE(ITMP1),ICORE(ITMP2))
         CALL TRANSP(ICORE(I001),ICORE(I002),NREAD,DISSYW)
C                  _      +
C EVALUATE Y(AI) = W(Gn,Af) * Q(Gn,If)
C
         IOFFY=1
         FACT=HALF
         DO 21 IRREPI=1,NIRREP
          IRREPA=IRREPI
          IRREPF=DIRPRD(IRREPA,IRREP)
          NUMA=VRT(IRREPA,1)
          NUMF=VRT(IRREPF,1)
          NUMI=POP(IRREPI,1)
          NROW=NUMA
          NCOL=NUMI
          NSUM=NREAD
          IOFFQ=I000+IINTFP*(IOFFLIST-1+
     &                       DISSYQ*(ISYMOFF(IRREPF,IRREP,18)-1))
          IOFFW=I002+IINTFP*NREAD*(ISYMOFF(IRREPF,IRREP,13)-1)
C
          DO 19 F=1,NUMF
C
           CALL XGEMM('T','N',NROW,NCOL,NSUM,FACT,ICORE(IOFFW),NSUM,
     &                ICORE(IOFFQ),DISSYQ,ONE,Y1(IOFFY),NROW)
           IOFFW=IOFFW+IINTFP*NROW*NSUM
           IOFFQ=IOFFQ+IINTFP*NCOL*DISSYQ
C
19        CONTINUE
C
          IOFFY=IOFFY+IINTFP*NROW*NCOL
C
21       CONTINUE
C
         IOFFLIST=IOFFLIST+NREAD
C
        IF(NLEFT.NE.0) GO TO 1
C
C SECOND SET OF CONTRACTIONS.  RESORT Q(fo,an) -> Q(of,na)
C
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXQ
        ITMP3=ITMP2+IINTFP*MAXQ 
        IEND=ITMP3+IINTFP*MAXQ
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y1HI6',IEND,MAXCOR)
C 
        CALL SYMTR3(IRREP,VRT(1,1),POP(1,1),DISSYQ,NUMDSQ,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL MTRAN2(ICORE(I000),DISSYQ)
C       
        LISTW=10
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))  
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))  
        I020=I010+IINTFP*NUMDSW*DISSYW
        I030=I020+IINTFP*NUMDSW*DISSYW
        MAXW=MAX(NUMDSW,DISSYW)
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXW
        ITMP3=ITMP2+IINTFP*MAXW 
        IEND=ITMP3+IINTFP*MAXW 
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y1HI7',IEND,MAXCOR)
C                                                      _
C READ W(In,Of) INTO CORE, SPIN-ADAPT AND TRANSPOSE TO W(Of,nI)
C
        CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREP,LISTW)
        CALL SPINAD3(IRREP,POP(1,1),DISSYW,NUMDSW,ICORE(I010),
     &               ICORE(ITMP1),ICORE(ITMP2))
        CALL TRANSP (ICORE(I010),ICORE(I020),NUMDSW,DISSYW)
        CALL SCOPY  (NUMDSW*DISSYW,ICORE(I020),1,ICORE(I010),1)
        CALL SYMTR1 (IRREP,POP(1,1),POP(1,1),NUMDSW,ICORE(I010),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C                          + 
C EVALUATE Y(AI) = Q(of,na) * W(Of,nI)
C
        IOFFY=1
        IOFFQ=I000
        IOFFW=I010
        FACT=HALFM
        DO 22 IRREPI=1,NIRREP
         IRREPA=IRREPI
         IRREPN=DIRPRD(IRREPI,IRREP)
         NUMA=VRT(IRREPA,1)
         NUMN=POP(IRREPN,1)
         NUMI=POP(IRREPI,1)
         NROW=NUMA
         NCOL=NUMI
         NSUM=NUMDSW*NUMN
         CALL XGEMM('T','N',NROW,NCOL,NSUM,FACT,ICORE(IOFFQ),NSUM,
     &              ICORE(IOFFW),NSUM,ONE,Y1(IOFFY),NROW)
         IOFFW=IOFFW+IINTFP*NCOL*NSUM
         IOFFQ=IOFFQ+IINTFP*NROW*NSUM
         IOFFY=IOFFY+IINTFP*NROW*NCOL
22      CONTINUE
20     CONTINUE 
C 
C NOW WE NEED TO DEAL WITH THE EXCHANGE CONTRIBUTIONS.
C
C Y(AI) = -3/2 R(Mn,Fe)*L(Mo,Ae)*W(Ni,Of)
C
C         +3/2 R(Mn,Fe)*L(Im,Fe)*W(Gn,Fa)
C
       DO 30 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
        LISTSAV=139
        DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTR1))
        NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTR1))
        I000=1
C
        LISTR=459
        DISSYR=IRPDPD(IRREPL,ISYTYP(1,LISTR))
        NUMDSR=IRPDPD(IRREPR,ISYTYP(2,LISTR))
        I010=I000+IINTFP*MAX(DISSYR*NUMDSR,DISSYZ*NUMDSZ)
        IF(I010.GE.MAXCOR) CALL INSMEM('R2L2Y1HI8',I010,MAXCOR)
        CALL GETLST(ICORE(I000),1,NUMDSR,1,IRREPR,LISTR)
C
        LISTL=439
        DISSYL=IRPDPD(IRREPR,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPL,ISYTYP(2,LISTL))
        I020=I010+IINTFP*MAX(DISSYL*NUMDSL,DISSYZ*NUMDSZ)
        IF(I020.GE.MAXCOR) CALL INSMEM('R2L2Y1HI9',I020,MAXCOR)
        CALL GETLST(ICORE(I010),1,NUMDSL,1,IRREPL,LISTL)
C           
C CALCULATE L(Ao,eM)*R(eM,Fn) = Q(Ao,Fn) AND PUT ON RESORTED LAMBDA LISTS
C
        CALL XGEMM('N','N',DISSYL,NUMDSR,DISSYR,ONE,ICORE(I010),
     &             DISSYL,ICORE(I000),DISSYR,ZILCH,ICORE(I020),DISSYL)
        CALL PUTLST(ICORE(I020),1,NUMDSR,1,IRREPR,LISTSAV)
30     CONTINUE
C
C
C RESORT INTERMEDIATES 
C         Q(Ao,Fn) -> Q(An,Fo)
C         Q(Gi,Fn) -> Q(Gn,Fi)
C
       ISIZE=ISYMSZ(ISYTYP(1,139),ISYTYP(2,139))
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
       IF(I020.GE.MAXCOR) CALL INSMEM('R2L2Y1HI10',I020,MAXCOR)
       CALL GETALL(ICORE(I000),ISIZE,1,LISTSAV)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,1),
     &             VRT(1,1),POP(1,1),ICORE(I020),1,'1432')
       CALL PUTALL(ICORE(I010),ISIZE,1,LISTSAV)
C
       LISTQ=139
       DO 40 IRREP=1,NIRREP
        DISSYQ=IRPDPD(IRREP,ISYTYP(1,LISTQ))
        NUMDSQ=IRPDPD(IRREP,ISYTYP(2,LISTQ))
        MAXQ=MAX(DISSYQ,NUMDSQ)
        I010=I000+IINTFP*NUMDSQ*DISSYQ
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXQ
        ITMP3=ITMP2+IINTFP*MAXQ
        IEND=ITMP3+IINTFP*MAXQ
        CALL GETLST(ICORE(I000),1,NUMDSQ,1,IRREP,LISTQ)
        CALL SYMTR1(IRREP,VRT(1,1),POP(1,1),DISSYQ,ICORE(I000),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C FIRST SET OF CONTRACTIONS.  TREAT Q AS Q(gn,if)
C
        LISTW=30
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))  
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        MAXW=MAX(NUMDSW,DISSYW)
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXW  
        ITMP3=ITMP2+IINTFP*MAXW  
        ISTART=ITMP3+IINTFP*MAXW
        MAXDIS=(MAXCOR-ISTART)/(2*IINTFP*MAX(1,DISSYW))  
        IF(MAXDIS.EQ.0) CALL INSMEM('R2L2Y1HI11',MAXDIS*DISSYW,DISSYW)
        I001=ISTART
        I002=I001+IINTFP*MAXDIS*DISSYW
C        
        IOFFLIST=1
        NLEFT=NUMDSW
C
2       CONTINUE
C
         NREAD=MIN(NLEFT,MAXDIS)
         NLEFT=NLEFT-NREAD
C                                              _
C READ W(Fa,Gn) INTO CORE AND TRANSPOSE TO W(Gn,Fa)
C 
         CALL GETLST(ICORE(I001),IOFFLIST,NREAD,1,IRREP,LISTW)
         CALL TRANSP (ICORE(I001),ICORE(I002),NREAD,DISSYW)
         CALL SYMTR1(IRREP,VRT(1,1),VRT(1,1),NREAD,ICORE(I002),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C                         +
C EVALUATE Y(AI) = W(Gn,aF) * Q(Gn,iF)
C
         IOFFY=1
         FACT=THRHALFM
         DO 41 IRREPI=1,NIRREP
          IRREPA=IRREPI
          IRREPF=DIRPRD(IRREPA,IRREP)
          NUMA=VRT(IRREPA,1)
          NUMF=VRT(IRREPF,1)
          NUMI=POP(IRREPI,1)
          NROW=NUMA
          NCOL=NUMI
          NSUM=NREAD
          IOFFQ=I000+IINTFP*(IOFFLIST-1+
     &                       DISSYQ*(ISYMOFF(IRREPF,IRREP,18)-1))
          IOFFW=I002+IINTFP*NREAD*(ISYMOFF(IRREPF,IRREP,20)-1)
C
          DO 39 F=1,NUMF
C
           CALL XGEMM('T','N',NROW,NCOL,NSUM,FACT,ICORE(IOFFW),NSUM,
     &                ICORE(IOFFQ),DISSYQ,ONE,Y1(IOFFY),NROW)
           IOFFW=IOFFW+IINTFP*NROW*NSUM
           IOFFQ=IOFFQ+IINTFP*NCOL*DISSYQ
C
39        CONTINUE
C
          IOFFY=IOFFY+IINTFP*NROW*NCOL
C
41       CONTINUE
C 
         IOFFLIST=IOFFLIST+NREAD
C
        IF(NLEFT.NE.0) GO TO 2
C
C SECOND SET OF CONTRACTIONS.  RESORT Q(Fo,An) -> Q(oF,nA)
C
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXQ
        ITMP3=ITMP2+IINTFP*MAXQ 
        IEND=ITMP3+IINTFP*MAXQ
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y1HI12',IEND,MAXCOR)
        CALL SYMTR3(IRREP,VRT(1,1),POP(1,1),DISSYQ,NUMDSQ,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL MTRAN2(ICORE(I000),DISSYQ)
C       
        LISTW=10
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))  
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))  
        I020=I010+IINTFP*NUMDSW*DISSYW
        I030=I020+IINTFP*NUMDSW*DISSYW
        IF(I030.GE.MAXCOR) CALL INSMEM('R2L2Y1HI13',I030,MAXCOR)
C                                   
C READ W(Ni,Of) INTO CORE AND TRANSPOSE TO W(Of,Ni)
C
        CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREP,LISTW)
        CALL TRANSP (ICORE(I010),ICORE(I020),NUMDSW,DISSYW)
        CALL SCOPY  (NUMDSW*DISSYW,ICORE(I020),1,ICORE(I010),1)
C                          + 
C EVALUATE Y(AI) = Q(of,na) * W(Of,nI)
C
        IOFFY=1
        IOFFQ=I000
        IOFFW=I010
        FACT=THRHALF
        DO 42 IRREPI=1,NIRREP
         IRREPA=IRREPI
         IRREPN=DIRPRD(IRREPI,IRREP)
         NUMA=VRT(IRREPA,1)
         NUMN=POP(IRREPN,1)
         NUMI=POP(IRREPI,1)
         NROW=NUMA
         NCOL=NUMI
         NSUM=NUMDSW*NUMN
         CALL XGEMM('T','N',NROW,NCOL,NSUM,FACT,ICORE(IOFFQ),NSUM,
     &              ICORE(IOFFW),NSUM,ONE,Y1(IOFFY),NROW)
         IOFFW=IOFFW+IINTFP*NCOL*NSUM
         IOFFQ=IOFFQ+IINTFP*NROW*NSUM
         IOFFY=IOFFY+IINTFP*NROW*NCOL
42      CONTINUE
C
40     CONTINUE
C
      RETURN
      END
