      SUBROUTINE R2L2Y1A(Y1,F1,ICORE,MAXCOR,IUHF)
C
C Y1(ai) = L(im,ae)*R(mn,ef)*F(nf)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,TWO
      DIMENSION ICORE(MAXCOR),Y1(*),F1(*),IAI(2)
      COMMON/STATSYM/IRREPX
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,ONEM,TWO,ZILCH/1.0D0,-1.0D0,2.0D0,0.0D0/
C
      IF(IUHF.EQ.0)THEN
       I0Q=1
       I000=I0Q+IINTFP*IRPDPD(IRREPX,9)
C
C PLACE RESORTED R VECTOR ON LISTS 434-439
C
C
C SPIN ADAPTED RHF CODE.
C
C  Y1(AI) = F(fn)*[2R(Mn,Ef)-R(Nm,Ef)]*[2L(Im,Ae)-L(Mi,Ae)]
C
C FIRST FORM 2*R(Mn,Ef) - R(Nm,Ef) AND PUT IN R(EM,fn) MATRIX
C
       LISTR1=457
       LISTR2=459
       DISSYR=IRPDPD(IRREPX,ISYTYP(1,LISTR1))
       NUMDSR=IRPDPD(1,ISYTYP(2,LISTR1))
       I010=I000+IINTFP*DISSYR*NUMDSR
       I020=I010+IINTFP*DISSYR*NUMDSR
       CALL GETLST(ICORE(I000),1,NUMDSR,1,1,LISTR1)
       CALL GETLST(ICORE(I010),1,NUMDSR,1,1,LISTR2)
       CALL SSCAL (NUMDSR*DISSYR,TWO,ICORE(I000),1)
       CALL SAXPY (NUMDSR*DISSYR,ONEM,ICORE(I010),1,ICORE(I000),1)
C
C                 _
C CALCULATE F(fn)*R(EM,fn) = Q(EM)
C
       CALL XGEMM('N','T',1,DISSYR,NUMDSR,ONE,F1,
     &            1,ICORE(I000),DISSYR,ZILCH,ICORE(I0Q),1)
C
C PLACE RESORTED L VECTOR ON LISTS 434-439
C
C
C NOW FORM 2*L(Im,Ae)-L(Mi,Ae) AND PUT IN L(AI,em) MATRIX
C
       LISTL1=437
       LISTL2=439
       DISSYL=IRPDPD(IRREPX,ISYTYP(1,LISTL1))
       NUMDSL=IRPDPD(1,ISYTYP(2,LISTL1))
       I010=I000+IINTFP*DISSYL*NUMDSL
       I020=I010+IINTFP*DISSYL*NUMDSL
       CALL GETLST(ICORE(I000),1,NUMDSL,1,1,LISTL1)
       CALL GETLST(ICORE(I010),1,NUMDSL,1,1,LISTL2)
       CALL SSCAL (NUMDSL*DISSYL,TWO,ICORE(I000),1)
       CALL SAXPY (NUMDSL*DISSYL,ONEM,ICORE(I010),1,ICORE(I000),1)
C
C                           _
C CALCULATE Z(AI) = Q(EM) * L(EM,ai)
C
       CALL XGEMM('N','N',1,NUMDSL,DISSYL,ONE,ICORE(I0Q),
     &            1,ICORE(I000),DISSYL,ONE,Y1,1)
C
      ELSE
C
C Y1(AI) = L(IM,AE)*R(MN,EF)*F(NF) + L(IM,AE)*R(Mn,Ef)*F(nf)
C        + L(Im,Ae)*R(mn,ef)*F(nf) + L(Im,Ae)*R(mN,eF)*F(NF)
C
C        = L(IM,AE)*Q(EM) + L(Im,Ae)*Q(em)
C
       I0Q=1
       I000=I0Q+IINTFP*(IRPDPD(IRREPX,9)+IRPDPD(IRREPX,10))
C
       IAI(1)=1
       IAI(2)=1+NT(1)*IINTFP
       IOFFQ1=I0Q
       DO 10 ISPIN=1,2
C                         + 
C CALCULATE F(FN)*R(EM,FN) => Q(EM)
C
        LISTR1=453+ISPIN
        DISSYR=IRPDPD(IRREPX,ISYTYP(1,LISTR1))
        NUMDSR=IRPDPD(1,ISYTYP(2,LISTR1))
        I010=I000+IINTFP*DISSYR*NUMDSR
        CALL GETLST(ICORE(I000),1,NUMDSR,1,1,LISTR1)
        CALL XGEMM('N','T',1,DISSYR,NUMDSR,ONEM,F1(IAI(ISPIN)),
     &             1,ICORE(I000),DISSYR,ZILCH,ICORE(IOFFQ1),1)
C                         +
C CALCULATE F(fn)*R(EM,fn) => Q(EM)
C
        LISTR2=458-ISPIN
        DISSYR=IRPDPD(IRREPX,ISYTYP(1,LISTR2))
        NUMDSR=IRPDPD(1,ISYTYP(2,LISTR2))
        I010=I000+IINTFP*DISSYR*NUMDSR
        CALL GETLST(ICORE(I000),1,NUMDSR,1,1,LISTR2)
        CALL XGEMM('N','T',1,DISSYR,NUMDSR,ONE,F1(IAI(3-ISPIN)),
     &             1,ICORE(I000),DISSYR,ONE,ICORE(IOFFQ1),1)
C
        IOFFQ1=IOFFQ1+IINTFP*IRPDPD(IRREPX,9)
C
10     CONTINUE
C
       IOFFY=1
       IAI(1)=I0Q
       IAI(2)=IAI(1)+IINTFP*IRPDPD(IRREPX,9)
       DO 11 ISPIN=1,2
C
C CALCULATE XI(AI) = Q(EM)*L(EM,AI)
C
        LISTL1=433+ISPIN
        DISSYL=IRPDPD(IRREPX,ISYTYP(1,LISTL1))
        NUMDSL=IRPDPD(1,ISYTYP(2,LISTL1))
        I010=I000+IINTFP*DISSYL*NUMDSL
        CALL GETLST(ICORE(I000),1,NUMDSL,1,1,LISTL1)
        CALL XGEMM('N','N',1,NUMDSL,DISSYL,ONEM,ICORE(IAI(ISPIN)),
     &             1,ICORE(I000),DISSYL,ONE,Y1(IOFFY),1)
C
C CALCULATE XI(AI) = Q(em)*L(em,AI)
C
        LISTL2=435+ISPIN
        DISSYL=IRPDPD(IRREPX,ISYTYP(1,LISTL2))
        NUMDSL=IRPDPD(1,ISYTYP(2,LISTL2))
        I010=I000+IINTFP*DISSYL*NUMDSL
        CALL GETLST(ICORE(I000),1,NUMDSL,1,1,LISTL2)
        CALL XGEMM('N','N',1,NUMDSL,DISSYL,ONE,ICORE(IAI(3-ISPIN)),
     &             1,ICORE(I000),DISSYL,ONE,Y1(IOFFY),1)
C
        IOFFY =IOFFY +IINTFP*NT(ISPIN)
C
11     CONTINUE
C
      ENDIF
C
      RETURN
      END
