      SUBROUTINE R1L1Y2C(R1,L1,ICORE,MAXCOR,IUHF)
C
C Y2A(ab,ij) = - P(ab) [R(em) * L(am) * W(ij,eb)]
C
CEND
      IMPLICIT INTEGER (A-Z)
      LOGICAL TRANS,EXP
      DOUBLE PRECISION ONE,ONEM,ZILCH,L1,R1
      DIMENSION ICORE(MAXCOR),R1(*),L1(*),ILR(2)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
      IF(IUHF.EQ.0)THEN
C
       LISTW=16
       LISTY=63
C
C SPIN ADAPTED RHF CODE.
C
C  Z(Ab,Ij) = L(am) * R(em) * W(Eb,Ij) 
C
C  Y2B(Ab,Ij) = Z(Ab,Ij) + Z(Ba,Ji)
C
C CALCULATE Q(ae) =  - L(am)*R(em)
C
       I000=1
       I010=I000+IINTFP*NFEA(1)
       IOFFQ=I000
       DO 10 IRREPE=1,NIRREP
        IRREPA=IRREPE
        IRREPM=DIRPRD(IRREPA,IRREPX)
        NROW=VRT(IRREPA,1)
        NCOL=VRT(IRREPE,1)
        NSUM=POP(IRREPM,1)
        IOFFL=1+(ISYMOFF(IRREPM,IRREPX,9)-1)
        IOFFR=IOFFL
        CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,L1(IOFFL),
     &             NROW,R1(IOFFR),NCOL,ZILCH,ICORE(IOFFQ),NROW)
        IOFFQ=IOFFQ+NROW*NCOL*IINTFP
10     CONTINUE
C
C READ IN W(Eb,Ij) = W(Be,Ji) INTEGRALS
C
       DO 20 IRREP=1,NIRREP
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        MAXW  =MAX(NUMDSW,DISSYW)
        I020=I010+IINTFP*DISSYW*NUMDSW
        I030=I020+IINTFP*MAX(3*MAXW,DISSYW*NUMDSW)
        ITMP1=I020
        ITMP2=ITMP1+MAXW*IINTFP
        ITMP3=ITMP2+MAXW*IINTFP
        IEND=ITMP3+MAXW*IINTFP
        IF(IEND.GE.MAXCOR) CALL INSMEM('R1L1Y2C1',IEND,MAXCOR)
        CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREP,LISTW)
C
C TRANSPOSE TO W(Ji,Be)
C
        CALL TRANSP(ICORE(I010),ICORE(I020),NUMDSW,DISSYW)
C
C CALCULATE Z(Ji,Ba) = W(Ji,Be) * Q(ae)
C
        IOFFW=I020
        IOFFQ=I000
        IOFFZ=I010
        DO 30 IRREPE=1,NIRREP
         IRREPA=IRREPE
         IRREPB=DIRPRD(IRREPE,IRREP)
         NUMA=VRT(IRREPA,1)
         NUMB=VRT(IRREPB,1)
         NUME=VRT(IRREPE,1)
         NROW=NUMDSW*NUMB
         NCOL=NUMA
         NSUM=NUME
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &              ICORE(IOFFQ),NCOL,ZILCH,ICORE(IOFFZ),NROW)
         IOFFZ=IOFFZ+NROW*NCOL*IINTFP
         IOFFQ=IOFFQ+NUME*NUMA*IINTFP
         IOFFW=IOFFW+NROW*NCOL*IINTFP
30      CONTINUE
C
        CALL TRANSP(ICORE(I010),ICORE(I020),DISSYW,NUMDSW)
        CALL SCOPY (DISSYW*NUMDSW,ICORE(I020),1,ICORE(I010),1)
C
C SYMMETRIZE AND AUGMENT LIST
C
        CALL SYMRHF(IRREP,VRT(1,1),POP(1,1),DISSYW,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL GETLST(ICORE(I020),1,NUMDSW,1,IRREP,LISTY)
        CALL SAXPY (NUMDSW*DISSYW,ONE,ICORE(I020),1,ICORE(I010),1)
        CALL PUTLST(ICORE(I010),1,NUMDSW,1,IRREP,LISTY)
C
20     CONTINUE
C
      ELSE
C
       ILR(1)=1
       ILR(2)=1+IINTFP*NFEA(1)
       ISTART=ILR(2)+IINTFP*NFEA(2)
C
C CALCULATE Q(ae) = - L(am)*R(em)
C
       DO 1000 ISPIN=1,2
C
        I000=ILR(ISPIN)
        I010=ISTART
        IOFFQ=I000
C
        DO 110 IRREPE=1,NIRREP
C
         IRREPA=IRREPE
         IRREPM=DIRPRD(IRREPA,IRREPX)
         NROW=VRT(IRREPA,ISPIN)
         NCOL=VRT(IRREPE,ISPIN)
         NSUM=POP(IRREPM,ISPIN)
         IOFFR=1+(IRPDPD(IRREPX,9)*(ISPIN-1)
     &          +(ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1))
         IOFFL=IOFFR
C
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,L1(IOFFL),
     &              NROW,R1(IOFFR),NCOL,ZILCH,ICORE(IOFFQ),NROW)
C
         IOFFQ=IOFFQ+NROW*NCOL*IINTFP
C
110     CONTINUE
C
1000   CONTINUE
C
C READ IN W(EB,IJ) INTEGRALS
C
       DO 1100 ISPIN=1,4
C
        LISTW=13+ISPIN
        LISTY=60+ISPIN
        ISPIN1=ISPIN
        ISPIN2=ISPIN
        TRANS=.FALSE.
        EXP=.TRUE.
        IF(ISPIN.EQ.3) THEN
         ISPIN1=2
         ISPIN2=1
         TRANS=.TRUE.
         EXP=.FALSE.
        ELSE IF(ISPIN.EQ.4) THEN
         LISTW=16
         LISTY=63
         ISPIN1=1
         ISPIN2=2
         TRANS=.FALSE.
         EXP=.FALSE.
        ENDIF
C
        DO 120 IRREP=1,NIRREP
         DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
         NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
         DISDSF=DISSYW
         IF(ISPIN.LE.2) DISDSF=IRPDPD(IRREP,18+ISPIN)
         MAXW=MAX(NUMDSW,DISDSF)
         I010=ISTART
         I020=I010+IINTFP*DISDSF*NUMDSW
         I030=I020+IINTFP*MAX(3*MAXW,DISDSF*NUMDSW)
         ITMP1=I030
         ITMP2=ITMP1+MAXW*IINTFP
         ITMP3=ITMP2+MAXW*IINTFP
         IEND=ITMP3+MAXW*IINTFP
         IF(IEND.GE.MAXCOR) CALL INSMEM('R1L1Y2C1',IEND,MAXCOR)
         CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREP,LISTW)
         CALL TRANSP(ICORE(I010),ICORE(I020),NUMDSW,DISSYW)
         IF(EXP) CALL SYMEXP(IRREP,VRT(1,ISPIN),NUMDSW,ICORE(I020))
         IF(TRANS) CALL SYMTR1(IRREP,VRT(1,ISPIN2),VRT(1,ISPIN1),
     &                        NUMDSW,ICORE(I020),ICORE(ITMP1),
     &                        ICORE(ITMP2),ICORE(ITMP3))
C
C CALCULATE Z(JI,BA) = W(JI,BE) * Q(AE)
C
         IOFFW=I020
         IOFFQ=ILR(ISPIN2)
         IOFFZ=I010
         DO 130 IRREPE=1,NIRREP
          IRREPA=IRREPE
          IRREPB=DIRPRD(IRREPE,IRREP)
          NUMA=VRT(IRREPA,ISPIN2)
          NUMB=VRT(IRREPB,ISPIN1)
          NUME=VRT(IRREPE,ISPIN2)
          NROW=NUMDSW*NUMB
          NCOL=NUMA
          NSUM=NUME
          CALL XGEMM('N','T',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &               ICORE(IOFFQ),NCOL,ZILCH,ICORE(IOFFZ),NROW)
          IOFFZ=IOFFZ+NROW*NCOL*IINTFP
          IOFFQ=IOFFQ+NUME*NUMA*IINTFP
          IOFFW=IOFFW+NROW*NCOL*IINTFP
130      CONTINUE
C
         IF(TRANS) CALL SYMTR1(IRREP,VRT(1,ISPIN1),VRT(1,ISPIN2),
     &                         NUMDSW,ICORE(I010),ICORE(ITMP1),
     &                         ICORE(ITMP2),ICORE(ITMP3))
C
C ANTI-SYMMETRIZE(IN CASE OF ISPIN=1,2) AND AUGMENT LIST
C
         IF(EXP) CALL ASSYM2(IRREP,VRT(1,ISPIN),NUMDSW,ICORE(I010))
C
         CALL TRANSP(ICORE(I010),ICORE(I020),DISSYW,NUMDSW)
         CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREP,LISTY)
         CALL SAXPY (NUMDSW*DISSYW,ONE,ICORE(I010),1,ICORE(I020),1)
         CALL PUTLST(ICORE(I020),1,NUMDSW,1,IRREP,LISTY)
C
120     CONTINUE
C
1100   CONTINUE
C
      ENDIF 
C
      RETURN
      END
