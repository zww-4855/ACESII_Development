      SUBROUTINE R1L2Y2C(R1,ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = - L(ef,ij)*R(me)*W(fm,ba)
C
C           = - L(ef,ij) R(mf) W*(ab,em)
C
C RHF : SPIN ADAPTED CODE
C UHF : ALL SPIN CASES ARE EXPLICITELY CONSIDERED
C  
CEND
C
C CODED SEPTEMBER/93  JFS AND JG
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,FACT
C
      DIMENSION ICORE(MAXCOR),R1(*)
      DIMENSION IOFFRR(2)
C
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE.
C
C  Y1(Ab,Ij) = Q(Ab,Ij) + Q(Ba,Ji)
C
C WHERE
C
C  Q(Ab,Ij) = [R(me) * L(Fe,Ij)] * W(Ab,Fm)
C
       LISTL=446
       LISTZ=63
       LISTW=30
       DO 10 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTW))
        DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
        NUMDSW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
        DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
        MAXL  =MAX(DISSYL,NUMDSL,DISSYX,NUMDSX,DISSYZ,NUMDSZ)
C    
        I000=1
        I010=I000+IINTFP*MAX(DISSYX*NUMDSX,DISSYZ*NUMDSZ)
        I020=I010+IINTFP*MAX(DISSYL*NUMDSL,DISSYZ*NUMDSZ)
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXL
        ITMP3=ITMP2+IINTFP*MAXL
        MAXDIS=(MAXCOR-I020)/(IINTFP*MAX(1,DISSYW))
        IF(MAXDIS.LE.0) CALL INSMEM('R1L2Y2C1',DISSYW,MAXDIS*DISSYW)
C
        CALL GETTRN(ICORE(I010),ICORE(ITMP1),DISSYL,NUMDSL,1,
     &              IRREPR,LISTL)
C
C FORM X(Ij,Fm) = L(Ij,Fe) * R(em)
C
        DO 20 IRREPM=1,NIRREP
         IRREPE=DIRPRD(IRREPM,IRREPX)
         IRREPF=DIRPRD(IRREPE,IRREPL)
         NUMM=POP(IRREPM,1)
         NUME=VRT(IRREPE,1)
         NUMF=VRT(IRREPF,1)
         NROW=NUMDSL*NUMF
         NCOL=NUMM
         NSUM=NUME
         IOFFL=I010+IINTFP*(ISYMOFF(IRREPE,IRREPL,19)-1)*NUMDSL
         IOFFQ=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,9)-1)*DISSYX
         IOFFR=1+IINTFP*(ISYMOFF(IRREPM,IRREPX,9)-1)
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),
     &              NROW,R1(IOFFR),NSUM,ZILCH,ICORE(IOFFQ),
     &              NROW)
20      CONTINUE    
C
C NOW READ IN W(Ab,Fm) AND FORM PRODUCT 
C
C       Q(Ab,Ij) = W(Ab,Fm) * X(Ij,Fm)
C
        IOFFSET=I000
        NLEFT=NUMDSW
        IOFFLIST=1
        FACT=ZILCH
1       CONTINUE
C
         NREAD=MIN(NLEFT,MAXDIS)
         NLEFT=NLEFT-NREAD
         CALL GETLST(ICORE(I020),IOFFLIST,NREAD,1,IRREPR,LISTW)
C
         IOFFLIST=IOFFLIST+NREAD
C
         CALL XGEMM('N','T',DISSYW,DISSYX,NREAD,ONEM,ICORE(I020),
     &              DISSYW,ICORE(IOFFSET),DISSYX,FACT,ICORE(I010),
     &              DISSYW)
C
         FACT=ONE
         IOFFSET=IOFFSET+IINTFP*NREAD*DISSYX
C
        IF(NLEFT.NE.0) GO TO 1
C
        CALL SYMRHF(IRREPR,VRT(1,1),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
c        call checksum('r1l2y2c r',icore(i000),numdsz*dissyz,S)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
10     CONTINUE
C
       ELSE  
C
C GET OFFSETS FOR R1:
C
        IOFFRR(1)=1
        IOFFRR(2)=1+IINTFP*IRPDPD(IRREPX,9)
C
C DO FIRST SPIN CASES AAAA AND BBBB
C
        DO 1000 ISPIN=1,2
C
         LISTL=443+ISPIN
         LISTZ=60+ISPIN
         LISTW=26+ISPIN
C
         DO 1010 IRREPR=1,NIRREP
C
          IRREPL=DIRPRD(IRREPR,IRREPX)
C
          DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
          NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
          DISSYX=IRPDPD(IRREPR,ISYTYP(2,LISTL))
          NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTW))
          DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
          NUMDSW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
          DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
          NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
          NVRTSQL=IRPDPD(IRREPL,18+ISPIN)
C
          I000=1
          I010=I000+IINTFP*MAX(NUMDSX*DISSYX,NUMDSZ*DISSYZ)
          I020=I010+IINTFP*MAX(NUMDSL*NVRTSQL,NUMDSZ*DISSYZ)
          ITMP=I020
          IEND=ITMP+IINTFP*MAX(NUMDSL,NVRTSQL)
          MAXDIS=(MAXCOR-I020)/(IINTFP*MAX(1,DISSYW))
C
          IF(MAXDIS.LE.0) CALL INSMEM('R1L2Y2C',DISSYW,MAXDIS*DISSYW)
C
          CALL GETTRN(ICORE(I010),ICORE(ITMP),DISSYL,NUMDSL,1,
     &                IRREPR,LISTL)
C
          CALL SYMEXP(IRREPL,VRT(1,ISPIN),NUMDSL,ICORE(I010))
C
C FORM X(IJ,EM) = L(IJ,EF) * R(FM)
C
          DO 1020 IRREPM=1,NIRREP
           IRREPF=DIRPRD(IRREPM,IRREPX)
           IRREPE=DIRPRD(IRREPF,IRREPL)
           NUMM=POP(IRREPM,ISPIN)
           NUME=VRT(IRREPE,ISPIN)
           NUMF=VRT(IRREPF,ISPIN)
           NROW=NUMDSL*NUME
           NCOL=NUMM
           NSUM=NUMF
           IOFFL=I010+IINTFP*(ISYMOFF(IRREPF,IRREPL,18+ISPIN)-1)*NUMDSL
           IOFFQ=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,8+ISPIN)-1)*DISSYX
           IOFFR=IOFFRR(ISPIN)+IINTFP*(ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1)
           CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),
     &                NROW,R1(IOFFR),NSUM,ZILCH,ICORE(IOFFQ),
     &                NROW)
1020      CONTINUE
C
          IOFFSET=I000
          NLEFT=NUMDSW
          IOFFLIST=1
          FACT=ZILCH
C
2         CONTINUE
C
           NREAD=MIN(NLEFT,MAXDIS)
           NLEFT=NLEFT-NREAD
           CALL GETLST(ICORE(I020),IOFFLIST,NREAD,1,IRREPR,LISTW)
           IOFFLIST=IOFFLIST+NREAD
C
           CALL XGEMM('N','T',DISSYW,DISSYX,NREAD,ONEM,ICORE(I020),
     &               DISSYW,ICORE(IOFFSET),DISSYX,FACT,ICORE(I010),
     &               DISSYW)
C
           FACT=ONE
           IOFFSET=IOFFSET+IINTFP*NREAD*DISSYX
C
          IF(NLEFT.NE.0) GO TO 2
C
          CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
          CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
c        call checksum('2c 1',icore(i000),numdsz*dissyz,s)
          CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
1010     CONTINUE
C
1000    CONTINUE
C
C DO ABAB SPIN CASE 
C
        LISTL=446
        LISTZ=63
        LISTW1=30
        LISTW2=29
C 
        DO 2010 IRREPR=1,NIRREP
C 
         IRREPL=DIRPRD(IRREPR,IRREPX)
C 
         DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
         NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
         DISSYX=IRPDPD(IRREPR,ISYTYP(2,LISTL))
         NUMDSX1=IRPDPD(IRREPR,ISYTYP(2,LISTW1))
         NUMDSX2=IRPDPD(IRREPR,ISYTYP(2,LISTW2))
         DISSYW1=IRPDPD(IRREPR,ISYTYP(1,LISTW1))
         NUMDSW1=IRPDPD(IRREPR,ISYTYP(2,LISTW1))
         DISSYW2=IRPDPD(IRREPR,ISYTYP(1,LISTW2))
         NUMDSW2=IRPDPD(IRREPR,ISYTYP(2,LISTW2))
         DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
         NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
C
         I000=1
         I010=I000+IINTFP*MAX(NUMDSZ*DISSYZ,NUMDSX1*DISSYX)
         I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSZ*DISSYZ)
         ITMP=I020
         IEND=ITMP+3*IINTFP*MAX(NUMDSL,DISSYL,NUMDSX1,DISSYX)
         MAXDIS=(MAXCOR-I020)/(IINTFP*MAX(1,DISSYW1))
         IF(MAXDIS.LE.0) CALL INSMEM('R1L2Y2C3',DISSYW1,MAXDIS*DISSYW1)
C
         CALL GETTRN(ICORE(I010),ICORE(ITMP),DISSYL,NUMDSL,1,
     &               IRREPR,LISTL)
C
C FORM X(Ij,Em) = L(Ij,Ef) * R(fm)
C
         DO 2020 IRREPM=1,NIRREP
          IRREPF=DIRPRD(IRREPM,IRREPX)
          IRREPE=DIRPRD(IRREPF,IRREPL)
          NUMM=POP(IRREPM,2)
          NUME=VRT(IRREPE,1)
          NUMF=VRT(IRREPF,2)
          NROW=NUMDSL*NUME
          NCOL=NUMM
          NSUM=NUMF
          IOFFL=I010+IINTFP*(ISYMOFF(IRREPF,IRREPL,13)-1)*NUMDSL
          IOFFQ=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,11)-1)*DISSYX
          IOFFR=IOFFRR(2)+IINTFP*(ISYMOFF(IRREPM,IRREPX,10)-1)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),
     &               NROW,R1(IOFFR),NSUM,ZILCH,ICORE(IOFFQ),
     &               NROW)
2020     CONTINUE
C
         IOFFSET=I000
         NLEFT=NUMDSW1
         IOFFLIST=1
         FACT=ZILCH
C
3        CONTINUE
C
          NREAD=MIN(NLEFT,MAXDIS)
          NLEFT=NLEFT-NREAD
          CALL GETLST(ICORE(I020),IOFFLIST,NREAD,1,IRREPR,LISTW1)
          IOFFLIST=IOFFLIST+NREAD
C
          CALL XGEMM('N','T',DISSYW1,DISSYX,NREAD,ONEM,ICORE(I020),
     &               DISSYW1,ICORE(IOFFSET),DISSYX,FACT,ICORE(I010),
     &               DISSYW1)
C
          FACT=ONE
          IOFFSET=IOFFSET+IINTFP*NREAD*DISSYX
C
         IF(NLEFT.NE.0) GO TO 3 
C
         CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
c        call checksum('2c 2',icore(i000),numdsz*dissyz,s)
         CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
c        call checksum('2c 2add',icore(i000),numdsz*dissyz,s)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
         I000=1
         I010=I000+IINTFP*MAX(NUMDSZ*DISSYZ,NUMDSX2*DISSYX)
         I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSZ*DISSYZ)
         ITMP=I020
         IEND=ITMP+3*IINTFP*MAX(NUMDSL,DISSYL,NUMDSX2,DISSYX)
         MAXDIS=(MAXCOR-I020)/(IINTFP*MAX(1,DISSYW2))
         IF(MAXDIS.LE.0) CALL INSMEM('R1L2Y2C4',DISSYW2,MAXDIS*DISSYW2)
C
         CALL GETTRN(ICORE(I010),ICORE(ITMP),DISSYL,NUMDSL,1,
     &               IRREPR,LISTL)
C
         CALL SYMTR1(IRREPL,VRT(1,1),VRT(1,2),NUMDSL,ICORE(I010),
     &               ICORE(ITMP),ICORE(ITMP+IINTFP*NUMDSL),
     &               ICORE(ITMP+2*IINTFP*NUMDSL))
C
C FORM X(Ij,Mf) = L(Ij,Ef) * R(EM)
C
         DO 2030 IRREPM=1,NIRREP
          IRREPE=DIRPRD(IRREPM,IRREPX)
          IRREPF=DIRPRD(IRREPE,IRREPL)
          NUMM=POP(IRREPM,1)
          NUME=VRT(IRREPE,1)
          NUMF=VRT(IRREPF,2)
          NROW=NUMDSL*NUMF
          NCOL=NUMM
          NSUM=NUME
          IOFFL=I010+IINTFP*(ISYMOFF(IRREPE,IRREPL,23)-1)*NUMDSL
          IOFFQ=I000+IINTFP*(ISYMOFF(IRREPM,IRREPR,12)-1)*DISSYX
          IOFFR=IOFFRR(1)+IINTFP*(ISYMOFF(IRREPM,IRREPX,9)-1)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),
     &               NROW,R1(IOFFR),NSUM,ZILCH,ICORE(IOFFQ),
     &               NROW)
2030     CONTINUE
C
         CALL SYMTR1(IRREPR,VRT(1,2),POP(1,1),DISSYX,ICORE(I000),
     &               ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYX),
     &               ICORE(ITMP+2*IINTFP*DISSYX))
C
         NLEFT=NUMDSW2
         IOFFLIST=1
         IOFFSET=I000
         FACT=ZILCH
C
4        CONTINUE
C
          NREAD=MIN(NLEFT,MAXDIS)
          NLEFT=NLEFT-NREAD
          CALL GETLST(ICORE(I020),IOFFLIST,NREAD,1,IRREPR,LISTW2)
          IOFFLIST=IOFFLIST+NREAD
C
          CALL XGEMM('N','T',DISSYW2,DISSYX,NREAD,ONEM,ICORE(I020),
     &               DISSYW2,ICORE(IOFFSET),DISSYX,FACT,ICORE(I010),
     &               DISSYW2)
C
          FACT=ONE
          IOFFSET=IOFFSET+IINTFP*NREAD*DISSYX
C
         IF(NLEFT.NE.0) GO TO 4
C
         CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
c        call checksum('2c 3',icore(i000),numdsz*dissyz,s)
         CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
c        call checksum('2c 3add',icore(i000),numdsz*dissyz,s)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
2010    CONTINUE
C
       ENDIF
C
C ALL DONE, RETURN
C
       RETURN
       END 
