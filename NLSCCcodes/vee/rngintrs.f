      SUBROUTINE RNGINTRS(ICORE,MAXCOR,IUHF,IRREPX,ISET)
C
C THIS ROUTINE RESORTS THE Q RING INTERMEDIATES FOR R2L2Y1
C CONTRACTIONS
C
C  Q(AI,BJ) -> Q(AJ,IB) [LIST 134]
C  Q(AI,bj) -> Q(Aj,Ib) [LIST 137]
C  Q(Ai,Bj) -> Q(Aj,iB) [LIST 125]
C  Q(ai,bj) -> Q(aj,ib) [LIST 135]
C  Q(ai,BJ) -> Q(aJ,iB) [LIST 136]
C  Q(aI,bJ) -> Q(aJ,Ib) [LIST 126]
C
CEND
      IMPLICIT INTEGER (A-Z)
      DIMENSION ICORE(MAXCOR)
      DIMENSION ISP1(6),ISP2(6),ISP3(6),ISP4(6)
      DIMENSION TYPEL(6),TYPER(6),LSTNUM(6)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
C
      DATA LSTNUM/134,137,125,135,136,126/
      DATA ISP1/1,1,1,2,2,2/
      DATA ISP2/1,1,2,2,2,1/
      DATA ISP3/1,2,1,2,1,2/
      DATA ISP4/1,2,2,2,1,1/
      DATA TYPEL/9,11,11,10,12,12/
      DATA TYPER/9,12,11,10,11,12/
C
      IF(ISET.EQ.0.AND.IUHF.NE.0)THEN
       CALL NEWTYP(125,11,11,.TRUE.)
       CALL NEWTYP(126,12,12,.TRUE.)
       CALL NEWTYP(134,9,9,.TRUE.)
       CALL NEWTYP(135,10,10,.TRUE.)
       CALL NEWTYP(136,10,9,.TRUE.)
       CALL NEWTYP(137,9,10,.TRUE.)
      ELSEIF(ISET.EQ.1)THEN
       DO 100 ICOUNT=1,3+3*IUHF
        ILIST=LSTNUM(ICOUNT)
        ISIZE=ISYMSZ(ISYTYP(1,ILIST),ISYTYP(2,ILIST))
        I000=1
        I010=I000+IINTFP*ISIZE
        I020=I010+IINTFP*ISIZE
        CALL GETALL(ICORE(I000),ISIZE,1,ILIST)
        CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,
     &              VRT(1,ISP1(ICOUNT)),POP(1,ISP2(ICOUNT)),
     &              VRT(1,ISP3(ICOUNT)),POP(1,ISP4(ICOUNT)),
     &              ICORE(I020),1,'1423')
        CALL NEWTYP(ILIST,TYPEL(ICOUNT),TYPER(ICOUNT),.TRUE.)
        CALL PUTALL(ICORE(I010),ISIZE,1,ILIST)
100    CONTINUE
C
      ENDIF
C
      RETURN
      END
