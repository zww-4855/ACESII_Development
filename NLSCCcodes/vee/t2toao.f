      SUBROUTINE T2TOAO(ICORE,MAXCOR,IUHF,TAU,TAU2,LISTT2,LSTAO,IRREPX)
C
C THIS ROUTINE DRIVES THE TRANSFORMATION OF THE T2 VECTOR
C FROM THE T2(AB,IJ) REPRESENTATION TO THE T2(XX,IJ) REPRESENTATION
C AND WRITES IT TO DISK.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE
      LOGICAL TAU,TAU2
      DIMENSION ICORE(MAXCOR)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/AOSYM/IAOPOP(8),IOFFAO(8),IOFFV(8,2),IOFFO(8,2),
     &             IRPDPDAO(8),IRPDPDAOS(8),ISTART(8,8),ISTARTMO(8,3)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/INFO/NOCCO(2),NVRTO(2)
C
      DATA ONE /1.0D0/
C
      NMO=NOCCO(1)+NVRTO(1)
      IONE=1
      CALL GETREC(20,'JOBARC','NBASTOT ',IONE,NAO)
      CALL GETAOINF(IUHF,IRREPX)
C
C LOOP OVER SYMMETRIES AND FORM T2 VECTOR IN THE XX,IJ REPRESENTATION
C
      DO 3 ISPIN=3,3-2*IUHF,-1
       DO 4 IRREPIJ=1,NIRREP  
        IRREPAB=DIRPRD(IRREPIJ,IRREPX)
        NUMAB=IRPDPD(IRREPAB,ISYTYP(1,43+ISPIN))
        NUMIJ=IRPDPD(IRREPIJ,ISYTYP(2,43+ISPIN))
        NUMAO=IRPDPDAO(IRREPAB)
        IF(ISPIN.LE.2)THEN
         NUMABX=IRPDPD(IRREPAB,18+ISPIN)
        ELSE
         NUMABX=NUMAB
        ENDIF
        I000=1
        I010=I000+NUMABX*NUMIJ*IINTFP
        I020=I010+NUMAO*NUMIJ*IINTFP
        I030=I020+MAX(NAO*NMO,NT(1))*IINTFP
        I040=I030+MAX(NAO*NMO,NT(2))*IINTFP
        IF(I040.GE.MAXCOR) CALL INSMEM('T2TOAO',I040,MAXCOR)
        CALL GETLST(ICORE(I000),1,NUMIJ,1,IRREPIJ,LISTT2+ISPIN)
CSSS        call checksum("@T2TOAO1:",Icore(I000),NUMIJ*NUMAB,s)
C
        IF(TAU)THEN
         IF(ISPIN.LE.2)THEN
          CALL GETLST(ICORE(I020),1,1,1,ISPIN,90)
          CALL FTAU  (ICORE(I000),ICORE(I020),ICORE(I020),NUMAB,
     &                NUMIJ,POP(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),
     &                VRT(1,ISPIN),IRREPAB,ISPIN,ONE)
         ELSE 
C
          CALL GETLST(ICORE(I020),1,1,1,1,90)
          CALL GETLST(ICORE(I030),1,1,1,1+IUHF,90)
          CALL FTAU  (ICORE(I000),ICORE(I020),ICORE(I030),NUMAB,
     &                NUMIJ,POP(1,1),POP(1,2),VRT(1,1),
     &                VRT(1,2),IRREPAB,3,ONE)
         ENDIF
        ENDIF
C
        IF(TAU2)THEN
C
         I050=I040+IINTFP*IRPDPD(IRREPX,9)
         IEND=I050+IINTFP*IRPDPD(IRREPX,10)
         IF(IEND.GE.MAXCOR) CALL INSMEM('T2TOAO',IEND,MAXCOR)
C
         IF(ISPIN.LE.2) THEN
          CALL GETLST(ICORE(I020),1,1,1,ISPIN,90)
          CALL GETLST(ICORE(I040),1,1,1,ISPIN+2,490)
          CALL DTAU(IRREPAB,IRREPIJ,1,IRREPX,ICORE(I000),
     &              ICORE(I020),ICORE(I020),ICORE(I040),
     &              ICORE(I040),ISPIN,ONE)
C
         ELSE
C
          CALL GETLST(ICORE(I020),1,1,1,1,90)
          CALL GETLST(ICORE(I040),1,1,1,3,490)
          CALL GETLST(ICORE(I030),1,1,1,1+IUHF,90)
          CALL GETLST(ICORE(I050),1,1,1,3+IUHF,490)
          CALL DTAU(IRREPAB,IRREPIJ,1,IRREPX,ICORE(I000),
     &              ICORE(I020),ICORE(I030),ICORE(I040),
     &              ICORE(I050),3,ONE)
C
         ENDIF
C
        ENDIF
C
C EXPAND TO FULL T2(AB,I<J) FOR AA AND BB SPIN CASES
C
        IF(ISPIN.LE.2)THEN
         CALL SYMEXP2(IRREPAB,VRT(1,ISPIN),NUMABX,NUMAB,
     &                NUMIJ,ICORE(I000),ICORE(I000))
        ENDIF
C
C TRANSFORM THIS IRREP OF T2 VECTOR TO MIXED REPRESENTATION AND
C WRITE TO DISK.
C
C          T2(AB,*) -> T2(XX,*)
C
        CALL T2TRAN(1,ICORE(I000),ICORE(I010),ICORE(I020),
     &                ICORE(I030),ICORE(I040),NAO,NMO,ISPIN,IUHF,
     &                IRREPIJ,IRREPAB)
        CALL PUTLST(ICORE(I010),1,NUMIJ,1,IRREPIJ,LSTAO+ISPIN)
CSSS        call checksum("@T2TOAO2:",Icore(I010),NUMIJ*NUMAO,s)
4      CONTINUE
3     CONTINUE
C
      RETURN
      END
