      SUBROUTINE CALCXI(ICORE,MAXCOR,IUHF)
C
C DRIVER FOR EVALUATION OF THE  XI AMPLITUDES INVOLVED IN EOM-CC GRADIENT
C THEORY  XI = <0|L Hbar|Q><Q|R|P> [SEE J.F. STANTON, JCP XX, XXXX (1993)] 
C
C
C PROGRAMMED BY J.F. STANTON, AUSTIN, 1993
C
CEND
      IMPLICIT INTEGER(A-Z)
      DOUBLE PRECISION ONE,ZILCH,XINORM,ZETANORM,SNRM2,SQRT
      LOGICAL MBPT2,CC,CCD,RCCD,DRCCD,LCCD,LCCSD,CC2
      DIMENSION ICORE(MAXCOR)
      COMMON/STATSYM/IRREPX
      COMMON/REFTYPE/MBPT2,CC,CCD,RCCD,DRCCD,LCCD,LCCSD,CC2
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      LOGICAL SS, SD, DS, DD
      COMMON/DRVHBAR/SS, SD, DS, DD
C
      DATA ONE,ZILCH/1.0D0,0.0D0/
C 
C ZERO OUT XI AMPLITUDE LISTS AND CALCULATE TOTAL LENGTH OF VECTOR
C
      LENTOT=0
      DO 10 ISPIN=3,3-2*IUHF,-1
       LIST=60+ISPIN
       CALL ZEROLIST(ICORE,MAXCOR,LIST)
       LENTOT=LENTOT+ISYMSZ(ISYTYP(1,LIST),ISYTYP(2,LIST))
10    CONTINUE
      DO 11 ISPIN=1,1+IUHF
       LENTOT=LENTOT+NT(ISPIN)
       CALL ZERO(ICORE,NT(ISPIN))
       CALL PUTLST(ICORE,1,1,1,ISPIN+2,90)
11    CONTINUE
C
      CALL UPDMOI(1,LENTOT,1,474,0,0)
      CALL UPDMOI(1,LENTOT,1, 88,0,0)
C   
C EVALUATE XI AMPLITUDES
C   
      CALL R1L1Y2(ICORE,MAXCOR,IUHF)
      IF (.NOT. CC2) CALL R1L2Y2(ICORE,MAXCOR,IUHF)
      IF (CC) CALL R2L1Y1(ICORE,MAXCOR,IUHF)
      IF (CC) CALL R2L2Y1(ICORE,MAXCOR,IUHF)
C SG 2/96 r2l2y2 is not needed for P-EOM-MBPT(2)
      IF ((CC .OR. DD) .AND. .NOT. CC2) CALL R2L2Y2(ICORE,MAXCOR,IUHF)
C
      XINORM = ZILCH
      ZETANORM = ZILCH
      IOFF=1
      DO 12 ISPIN=1,1+IUHF
       IF(CC .OR. CC2)THEN
        CALL GETLST(ICORE(IOFF),1,1,1,2+ISPIN,90)
        XINORM = XINORM + SNRM2(NT(ISPIN),ICORE(IOFF),1)**2
       ELSE 
        CALL ZERO(ICORE(IOFF),NT(ISPIN))
        CALL PUTLST(ICORE(IOFF),1,1,1,ISPIN,190)
       ENDIF
       IOFF=IOFF+NT(ISPIN)*IINTFP
12    CONTINUE
      DO 13 ISPIN=3,3-2*IUHF,-1
       LIST=60+ISPIN
       LENGTH=ISYMSZ(ISYTYP(1,LIST),ISYTYP(2,LIST))
       CALL GETALL(ICORE(IOFF),LENGTH,1,LIST)
       XINORM = XINORM + SNRM2(LENGTH,ICORE(IOFF),1)**2
clower case code for eomee-mbpt(2)
       IF(MBPT2 .OR. LCCD .OR. LCCSD .OR. RCCD .OR. DRCCD)THEN
        iscratch=ioff+length*iintfp
        call getall(icore(iscratch),length,1,47+ispin)
        call vecprd(icore(ioff),icore(iscratch),icore(ioff),length)
        zetanorm = zetanorm + snrm2(length,icore(ioff),1)**2
        call putall(icore(ioff),length,1,60+ispin)
        call sscal(length,0.d0,icore(ioff),1)
        call putall(icore(ioff),length,1,143+ispin)
       ENDIF
       IOFF=IOFF+LENGTH*IINTFP
13    CONTINUE
      CALL PUTLST(ICORE,1,1,1,1,474)
      WRITE(6,3000)SQRT(XINORM)
C
      CALL NEWLST(1,ICORE,MAXCOR,IUHF)
c      CALL HBARDIG2(ICORE,MAXCOR/2,IUHF,LENTOT)
C
C SOLVE ZETA EQUATIONS
C
      IF(CC)THEN
       CALL HBARDIG2(ICORE,MAXCOR/2,IUHF,LENTOT)
       CALL DOZETA(ICORE,MAXCOR,IUHF,LENTOT)
       CALL GETLST  (ICORE,1,1,1,1,474)
       ZETANORM=SNRM2(LENTOT,ICORE,1)**2
      ENDIF
      WRITE(6,3001)sqrt(zetanorm)
C
C PUT RESORTED L VECTOR AND G INTERMEDIATES BACK IN PLACE
C
      CALL RESORT(ICORE,MAXCOR,IUHF,1,144,134)
      CALL GFORMG(1,1,144,44,100,ICORE,MAXCOR,0,ONE,ONE,IUHF)
      CALL NEWLST(IRREPX,ICORE,MAXCOR,IUHF)
      CALL GETLST(ICORE,3,1,1,1,472)
      CALL UPDATES(IRREPX,ICORE,444,0,490,IUHF)
      CALL GETLST (ICORE,2,1,1,1,472)
      CALL UPDATES(IRREPX,ICORE,461,2,490,IUHF)
      CALL RESORT(ICORE,MAXCOR,IUHF,IRREPX,444,434)
      CALL RESORT(ICORE,MAXCOR,IUHF,IRREPX,461,454)
C
      RETURN
3000  FORMAT(T3,'@CALCXI-I, Norm of xi amplitudes: ',F15.10,'.')
 3001 FORMAT(T3,'@CALCXI-I, Norm of zeta amplitudes: ',F15.10,'.')
      END
