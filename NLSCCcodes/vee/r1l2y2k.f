      SUBROUTINE R1L2Y2K(Q1,F1,R1,ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = P(ij)P(ab) [R(me)*F(ia)*L(be,jm)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ZILCH
      CHARACTER*6 SPTYPE(2)
      DIMENSION ICORE(MAXCOR),R1(*),F1(*),Q1(*),IOFFAI(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,ZILCH/1.0D0,0.0D0/
      DATA SPTYPE/'AAAA','BBBB'/
C
      IOFFAI(1)=1
      IF(IUHF.NE.0)THEN
       IOFFAI(2)=IOFFAI(1)+IINTFP*NT(1)
      ELSE
       IOFFAI(2)=IOFFAI(1)
      ENDIF
C
      I000=1
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE.
C
C   Y(Ab,Ij) = Q(Ab,Ij) + Q(Ba,Ji)
C
C WHERE
C
C   Q(Ab,Ij) = R(ME)*[2L(Be,Jm)-L(Eb,Jm)]*F(IA) = X(bj)*F(AI)
C
       LISTZ=42
       DISSYZ=IRPDPD(1,ISYTYP(1,LISTZ))
       NUMDSZ=IRPDPD(1,ISYTYP(2,LISTZ))
       I010=I000+IINTFP*DISSYZ*NUMDSZ
C
C FORM Z(AI,bj) = F(AI)*Q(bj)
C
       CALL XGEMM('N','N',DISSYZ,NUMDSZ,1,ONE,F1,DISSYZ,
     &            Q1,1,ZILCH,ICORE(I000),DISSYZ)
       CALL MPMT (ICORE(I000),DISSYZ)
       CALL PUTLST(ICORE(I000),1,NUMDSZ,1,1,LISTZ)
C
C NOW RESORT TO Z(Ab,Ij)
C
       ISIZE=ISYMSZ(ISYTYP(1,63),ISYTYP(2,63))
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
       CALL ZERO(ICORE(I000),ISIZE)
       CALL GETLST(ICORE(I000),1,NUMDSZ,1,1,LISTZ)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,1),
     &             VRT(1,1),POP(1,1),ICORE(I020),1,'1324')
       CALL GETALL(ICORE(I000),ISIZE,1,63)
       CALL SAXPY (ISIZE,ONE,ICORE(I010),1,ICORE(I000),1)
       CALL PUTALL(ICORE(I000),ISIZE,1,63)
C
      ELSE
C
C UHF : Y(Ab,Ij) = X(bj)*F(AI) + X(AI)*F(bj)
C
C AAAA : Y(AB,IJ) = X(BJ)*F(AI) + ANTISYMMETRIZATION
C
       DO 100 ISPIN=1,1+IUHF
        LSTTMP=39+ISPIN
        LISTZ =60+ISPIN
        DISSYZ=IRPDPD(1,ISYTYP(1,LSTTMP))
        NUMDSZ=IRPDPD(1,ISYTYP(2,LSTTMP))
        I010=I000+IINTFP*DISSYZ*NUMDSZ
C
C FORM Z(AI,BJ) = F(AI)*Q(BJ)
C
        CALL XGEMM('N','N',DISSYZ,NUMDSZ,1,ONE,F1(IOFFAI(ISPIN)),DISSYZ,
     &             Q1(IOFFAI(ISPIN)),1,ZILCH,ICORE(I000),DISSYZ)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,1,LSTTMP)
C
C NOW ANTISYMMETRIZE AND RESORT TO Z(A<B,I<J)
C
        ISIZE=ISYMSZ(ISYTYP(1,LSTTMP),ISYTYP(2,LSTTMP))
        ISIZ2=ISYMSZ(ISYTYP(1,LISTZ),ISYTYP(2,LISTZ))
        I010=I000+IINTFP*ISIZE
        I020=I010+IINTFP*ISIZE
        CALL ZERO(ICORE(I000),ISIZE)
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,1,LSTTMP)
        CALL DSST03I(ICORE(I000),ICORE(I010),ISIZE,ISIZE,ICORE(I020),
     &               SPTYPE(ISPIN),1)
        CALL GETALL(ICORE(I000),ISIZ2,1,LISTZ)
        CALL SAXPY (ISIZE,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTALL(ICORE(I000),ISIZ2,1,LISTZ)
100    CONTINUE
C
C ABAB
C
       LISTZ=42
       DISSYZ=IRPDPD(1,ISYTYP(1,LISTZ))
       NUMDSZ=IRPDPD(1,ISYTYP(2,LISTZ))
       I010=I000+IINTFP*DISSYZ*NUMDSZ
C
C FORM Z(AI,bj) = F(AI)*Q(bj) + F(bj)*Q(AI)
C
       CALL XGEMM('N','N',DISSYZ,NUMDSZ,1,ONE,F1(IOFFAI(1)),DISSYZ,
     &            Q1(IOFFAI(2)),1,ZILCH,ICORE(I000),DISSYZ)
       CALL XGEMM('N','N',DISSYZ,NUMDSZ,1,ONE,Q1(IOFFAI(1)),DISSYZ,
     &            F1(IOFFAI(2)),1,ONE,ICORE(I000),DISSYZ)
       CALL PUTLST(ICORE(I000),1,NUMDSZ,1,1,LISTZ)
C
C NOW RESORT TO Z(Ab,Ij)
C
       ISIZE=ISYMSZ(ISYTYP(1,63),ISYTYP(2,63))
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
       CALL ZERO(ICORE(I000),ISIZE)
       CALL GETLST(ICORE(I000),1,NUMDSZ,1,1,LISTZ)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,1),
     &             VRT(1,2),POP(1,2),ICORE(I020),1,'1324')
       CALL GETALL(ICORE(I000),ISIZE,1,63)
       CALL SAXPY (ISIZE,ONE,ICORE(I010),1,ICORE(I000),1)
       CALL PUTALL(ICORE(I000),ISIZE,1,63)
C
      ENDIF
C
      RETURN
      END 
