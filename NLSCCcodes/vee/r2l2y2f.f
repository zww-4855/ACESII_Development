      SUBROUTINE R2L2Y2F(ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = -1/2 P(ab) R(mn,ef)*L(mn,af)*W(eb,ij)
C 
C  RHF : SPIN ADAPTED CODE
C  UHF : ALL SPIN CASES ARE EXPLICITELY CONSIDERED
C
CEND
C
C CODED SEPTEMBER/93 JFS AND JG
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ZILCH
C
      DIMENSION ICORE(MAXCOR)
      DIMENSION I00G(2)
C
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ZILCH/1.0D0,0.0D0/
C
      IF(IUHF.EQ.0)THEN
       LISTW=16
       LISTZ=63
       I0G=1
       I000=I0G+IINTFP*IRPDPD(1,19)
C
C SPIN ADAPTED RHF CODE.
C
C  Y1(Ab,Ij) = Z(Ab,Ij) + Z(Ba,Ji)
C
C WHERE
C
C   Z(Ab,Ij) = [2 R(Mn,Ef)-R(Nm,Ef)]*L(Mn,Af)*W(Eb,Ij)
C
C
C FIRST PICK UP G(EA) = -L(Mn,Af)*[2*R(Mn,Ef)-R(Nm,Ef)] FROM LIST 192
C
       CALL GETLST(ICORE(I0G),1,1,1,1,192)
C
C FORM PRODUCT Z(Ab,Ij)=W(Eb,Ij)*G(EA)
C
       DO 10 IRREP=1,NIRREP
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ))
        MAXSIZ=MAX(DISSYZ*NUMDSZ,DISSYW*NUMDSW)
        MAXT  =MAX(DISSYZ,NUMDSZ,DISSYW,NUMDSW)
        I010=I000+IINTFP*MAXSIZ
        I020=I010+IINTFP*MAXSIZ
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXT
        ITMP3=ITMP2+IINTFP*MAXT
        ITMP4=ITMP3+IINTFP*MAXT
        CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREP,LISTW)
        CALL TRANSP(ICORE(I010),ICORE(I000),NUMDSW,DISSYW)
        CALL SYMTR1(IRREP,VRT(1,1),VRT(1,1),NUMDSW,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C NOW FORM Z(Ij,bA) = W(Ij,bE)*G(EA)
C
        DO 20 IRREPE=1,NIRREP
         IRREPB=DIRPRD(IRREPE,IRREP)
         IRREPA=IRREPE
         NUME=VRT(IRREPE,1)
         NUMB=VRT(IRREPB,1)
         NUMA=VRT(IRREPA,1)
         NROW=NUMDSW*NUMB
         NCOL=NUMA
         NSUM=NUME
         IOFFW=I000+(ISYMOFF(IRREPE,IRREP,19)-1)*NUMDSW*IINTFP
         IOFFZ=I010+(ISYMOFF(IRREPA,IRREP,19)-1)*NUMDSZ*IINTFP
         IOFFG=I0G+(ISYMOFF(IRREPA,1,19)-1)*IINTFP
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &              ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),NROW)
20      CONTINUE
        CALL SYMTR1(IRREP,VRT(1,1),VRT(1,1),NUMDSW,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL TRANSP(ICORE(I010),ICORE(I000),DISSYZ,NUMDSZ)
       
C
        CALL SYMRHF(IRREP,VRT(1,1),POP(1,1),DISSYZ,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL GETLST(ICORE(I010),1,NUMDSZ,1,IRREP,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1) 
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
10     CONTINUE
C
      ELSE
C
       I00G(1)=1
       I00G(2)=I00G(1)+IINTFP*IRPDPD(1,19)
       ISTART=I00G(2)+IINTFP*IRPDPD(1,20)
       CALL GETLST(ICORE(I00G(1)),1,1,1,1,192)
       CALL GETLST(ICORE(I00G(2)),1,1,1,2,192)
C
C DO FIRST SPIN CASES AAAA AND BBBB
C
       DO 1000 ISPIN=1,2
C
        LISTW=13+ISPIN
        LISTZ=60+ISPIN
C
C LOOP OVER IRREPS
C
        DO 100 IRREP=1,NIRREP
C
         NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
         DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
         NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ))
         DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
         NVRTSQ=IRPDPD(IRREP,18+ISPIN)
C
         I000=ISTART
         I010=I000+IINTFP*NUMDSW*NVRTSQ
         ITMP=I010+IINTFP*NUMDSZ*NVRTSQ
         IEND=ITMP+IINTFP*3*MAX(NUMDSZ,NUMDSW,NVRTSQ)
         IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y2F',IEND,MAXCOR)
C
         CALL GETTRN(ICORE(I000),ICORE(ITMP),DISSYW,NUMDSW,1,
     &               IRREP,LISTW)
C
         CALL SYMEXP(IRREP,VRT(1,ISPIN),NUMDSW,ICORE(I000))
C
         DO 120 IRREPE=1,NIRREP
C
          IRREPA=DIRPRD(IRREPE,IRREP)
          IRREPB=IRREPE
          NUME=VRT(IRREPE,ISPIN)
          NUMA=VRT(IRREPA,ISPIN)
          NUMB=VRT(IRREPB,ISPIN)
C
C
          NROW=NUMDSW*NUMA
          NCOL=NUMB
          NSUM=NUME
C
          IOFFW=I000+IINTFP*NUMDSW*
     &               (ISYMOFF(IRREPE,IRREP,18+ISPIN)-1)
          IOFFZ=I010+IINTFP*NUMDSZ*
     &               (ISYMOFF(IRREPB,IRREP,18+ISPIN)-1)
          IOFFG=I00G(ISPIN)+IINTFP*
     &               (ISYMOFF(IRREPB,1,18+ISPIN)-1)
C
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),
     &               NROW,ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),
     &               NROW)
C
120      CONTINUE
C
         CALL ASSYM(IRREP,VRT(1,ISPIN),NUMDSZ,NUMDSZ,
     &              ICORE(I000),ICORE(I010))
C
         CALL TRANSP(ICORE(I000),ICORE(I010),DISSYZ,NUMDSZ)
C

         CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
         CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
100     CONTINUE
C
1000   CONTINUE
C
C DO ABAB SPIN CASE
C
       LISTW=16
       LISTZ=63
C
C LOOP OVER IRREPS
C
       DO 1100 IRREP=1,NIRREP
C
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ))
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
C
        I000=ISTART
        I010=I000+IINTFP*NUMDSW*DISSYW
        ITMP=I010+IINTFP*NUMDSZ*DISSYZ
        IEND=ITMP+IINTFP*3*MAX(NUMDSZ,DISSYZ)
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y2F',IEND,MAXCOR)
C
        CALL GETTRN(ICORE(I000),ICORE(ITMP),DISSYW,NUMDSW,1,
     &              IRREP,LISTW)
C
        DO 1120 IRREPE=1,NIRREP
C
         IRREPA=DIRPRD(IRREPE,IRREP)
         IRREPB=IRREPE
         NUME=VRT(IRREPE,2)
         NUMA=VRT(IRREPA,1)
         NUMB=VRT(IRREPB,2)
C
         NROW=NUMDSW*NUMA
         NCOL=NUMB
         NSUM=NUME
C
C
         IOFFW=I000+IINTFP*NUMDSW*
     &              (ISYMOFF(IRREPE,IRREP,13)-1)
         IOFFZ=I010+IINTFP*NUMDSZ*
     &              (ISYMOFF(IRREPB,IRREP,13)-1)
         IOFFG=I00G(2)+IINTFP*
     &              (ISYMOFF(IRREPB,1,20)-1)
C
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),
     &              NROW,ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),
     &              NROW)
C
1120    CONTINUE
C
        CALL SYMTR1(IRREP,VRT(1,1),VRT(1,2),NUMDSW,ICORE(I000),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*NUMDSW),
     &              ICORE(ITMP+2*IINTFP*NUMDSW))
        CALL SYMTR1(IRREP,VRT(1,1),VRT(1,2),NUMDSZ,ICORE(I010),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*NUMDSZ),
     &              ICORE(ITMP+2*IINTFP*NUMDSZ))

        DO 1130 IRREPE=1,NIRREP
C
         IRREPB=DIRPRD(IRREPE,IRREP)
         IRREPA=IRREPE
         NUME=VRT(IRREPE,1)
         NUMA=VRT(IRREPA,1)
         NUMB=VRT(IRREPB,2)
C
         NROW=NUMDSW*NUMB
         NCOL=NUMA
         NSUM=NUME
C
         IOFFW=I000+IINTFP*NUMDSW*
     &              (ISYMOFF(IRREPE,IRREP,23)-1)
         IOFFZ=I010+IINTFP*NUMDSZ*
     &              (ISYMOFF(IRREPA,IRREP,23)-1)
         IOFFG=I00G(1)+IINTFP*
     &              (ISYMOFF(IRREPA,1,19)-1)
C
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),
     &              NROW,ICORE(IOFFG),NSUM,ONE,ICORE(IOFFZ),
     &              NROW)
C
1130    CONTINUE
C
        CALL SYMTR1(IRREP,VRT(1,2),VRT(1,1),NUMDSZ,ICORE(I010),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*NUMDSZ),
     &              ICORE(ITMP+2*IINTFP*NUMDSZ))
        CALL TRANSP(ICORE(I010),ICORE(I000),DISSYZ,NUMDSZ)
C
        CALL GETLST(ICORE(I010),1,NUMDSZ,1,IRREP,LISTZ)
        CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I000),1,ICORE(I010),1)
        CALL PUTLST(ICORE(I010),1,NUMDSZ,1,IRREP,LISTZ)
C
1100   CONTINUE
C
      ENDIF
C
C ALL DONE, RETURN
C
      RETURN
      END
