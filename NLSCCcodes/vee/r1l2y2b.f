      SUBROUTINE R1L2Y2B(Q1,R1,ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = - P(ab) R(me) L(mn,ea) W(ij,nb)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      DIMENSION ICORE(MAXCOR),R1(*),Q1(*),I0Q(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
      I0Q(1)=1
      IF(IUHF.NE.0)THEN
       I0Q(2)=I0Q(1)+IINTFP*NT(1)
      ELSE
       I0Q(2)=I0Q(1)
      ENDIF
      I000=1
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE.
C
C  Y1(Ab,Ij) = Y(Ab,Ij) + Y(Ba,Ji)
C
C
C FORM PRODUCT Y(Ij,Ab) = W(Ij,Nb) * Q(An)
C
       DO 10 IRREP=1,NIRREP
        LISTW=10
        LISTZ=63
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ)) 
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ)) 
        I010=I000+IINTFP*MAX(DISSYW*NUMDSW,DISSYZ*NUMDSZ)
        I020=I010+IINTFP*DISSYZ*NUMDSZ
        MAXW=MAX(DISSYW,NUMDSW,DISSYZ,NUMDSZ)
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXW 
        ITMP3=ITMP2+IINTFP*MAXW
        IEND=ITMP3+IINTFP*MAXW
        IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2B1',IEND,MAXCOR) 
        CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREP,LISTW)
        CALL SYMTR1(IRREP,POP(1,1),VRT(1,1),DISSYW,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        IOFFQ=I0Q(1)
        IOFFW=I000
        IOFFZ=I010  
C
C                             +
C  Y(Ij,bA) = W(Ij,bN) * Q(AN)
C
        DO 20 IRREPA=1,NIRREP
         IRREPB=DIRPRD(IRREPA,IRREP)
         IRREPN=IRREPA
         NUMA=VRT(IRREPA,1)
         NUMB=VRT(IRREPB,1)
         NUMN=POP(IRREPN,1)
         NROW=DISSYW*NUMB
         NCOL=NUMA
         NSUM=NUMN
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),NROW,
     &              Q1(IOFFQ),NCOL,ZILCH,ICORE(IOFFZ),NROW)
         IOFFW=IOFFW+IINTFP*NROW*NSUM
         IOFFQ=IOFFQ+IINTFP*NCOL*NSUM
         IOFFZ=IOFFZ+IINTFP*NROW*NCOL 
20      CONTINUE
        CALL SYMTR1(IRREP,VRT(1,1),VRT(1,1),NUMDSZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL TRANSP(ICORE(I010),ICORE(I000),DISSYZ,NUMDSZ)
        CALL SYMRHF(IRREP,VRT(1,1),POP(1,1),DISSYZ,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL GETLST(ICORE(I010),1,NUMDSZ,1,IRREP,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
10     CONTINUE
C
      ELSE
C
C UHF
C
C  Y1(AB,IJ) = Q(AN)*W(IJ,NB) + ANTISYMMETRIZATION
C  
C  Y1(Ab,Ij) = Q(AN)*W(Ij,Nb) + Q(an)*W(Ij,Bn)
C
C AAAA AND BBBB
C
       DO 100 ISPIN=1,2
        LISTW=6+ISPIN
        LISTZ=60+ISPIN
        DO 110 IRREP=1,NIRREP
         DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
         NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
         DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ)) 
         NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ)) 
         DISSYZX=IRPDPD(IRREP,18+ISPIN)
         MAXTMP=MAX(DISSYW,NUMDSW,DISSYZ,DISSYZX,NUMDSZ)
         I010=I000+IINTFP*MAX(DISSYW*NUMDSW,DISSYZX*NUMDSZ)
         I020=I010+IINTFP*DISSYZX*NUMDSZ
         ITMP1=I020
         ITMP2=ITMP1+IINTFP*MAXTMP
         ITMP3=ITMP2+IINTFP*MAXTMP
         IEND=ITMP3+IINTFP*MAXTMP
         IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2B2',IEND,MAXCOR) 
         CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREP,LISTW)
         CALL SYMTR1(IRREP,POP(1,ISPIN),VRT(1,ISPIN),DISSYW,ICORE(I000),
     &               ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
         IOFFQ=I0Q(ISPIN)
         IOFFW=I000
         IOFFZ=I010
C
C Y(IJ,AB) = -Y(IJ,BA) = W(IJ,BN)*Q(AN)
C
         DO 120 IRREPA=1,NIRREP
          IRREPB=DIRPRD(IRREPA,IRREP)
          IRREPN=IRREPA
          NUMA=VRT(IRREPA,ISPIN)
          NUMB=VRT(IRREPB,ISPIN)
          NUMN=POP(IRREPN,ISPIN)
          NROW=DISSYW*NUMB
          NCOL=NUMA
          NSUM=NUMN
          CALL XGEMM('N','T',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &               Q1(IOFFQ),NCOL,ZILCH,ICORE(IOFFZ),NROW)
          IOFFW=IOFFW+IINTFP*NROW*NSUM
          IOFFQ=IOFFQ+IINTFP*NCOL*NSUM
          IOFFZ=IOFFZ+IINTFP*NROW*NCOL 
120      CONTINUE
         CALL ASSYM2(IRREP,VRT(1,ISPIN),NUMDSZ,ICORE(I010))
         CALL TRANSP(ICORE(I010),ICORE(I000),DISSYZ,NUMDSZ)
         CALL GETLST(ICORE(I010),1,NUMDSZ,1,IRREP,LISTZ)
         CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
110     CONTINUE
100    CONTINUE
C
C  ABAB : Y1(Ab,Ij) = Q(AN)*W(Ij,Nb) + Q(bn)*W(Ij,An)
C
       LISTZ=63
       DO 210 IRREP=1,NIRREP
C
C Y1(Ab,Ij) <= W(Ij,Nb)*Q(AN)
C
        LISTW=10
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ)) 
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ)) 
        MAXTMP=MAX(DISSYW,NUMDSW,DISSYZ,NUMDSZ)
        I0Z=I000
        I001=I0Z+IINTFP*DISSYZ*NUMDSZ
        I010=I001+IINTFP*MAX(DISSYW*NUMDSW,DISSYZ*NUMDSZ)
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXTMP
        ITMP3=ITMP2+IINTFP*MAXTMP
        IEND=ITMP3+IINTFP*MAXTMP
        IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2B3',IEND,MAXCOR) 
C
        CALL GETLST(ICORE(I001),1,NUMDSW,1,IRREP,LISTW)
C
C W(Ij,Nb) -> W(Ij,bN)
C
        CALL SYMTR1(IRREP,POP(1,1),VRT(1,2),DISSYW,ICORE(I001),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        IOFFQ=I0Q(1)
        IOFFW=I001
        IOFFZ=I0Z
C
C Y(Ij,bA) <= W(Ij,bN)*Q(AN)
C
        DO 220 IRREPA=1,NIRREP
         IRREPB=DIRPRD(IRREPA,IRREP)
         IRREPN=IRREPA
         NUMA=VRT(IRREPA,1)
         NUMB=VRT(IRREPB,2)
         NUMN=POP(IRREPN,1)
         NROW=DISSYW*NUMB
         NCOL=NUMA
         NSUM=NUMN
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),NROW,
     &              Q1(IOFFQ),NCOL,ZILCH,ICORE(IOFFZ),NROW)
         IOFFW=IOFFW+IINTFP*NROW*NSUM
         IOFFQ=IOFFQ+IINTFP*NCOL*NSUM
         IOFFZ=IOFFZ+IINTFP*NROW*NCOL 
220     CONTINUE
C
        CALL SYMTR1(IRREP,VRT(1,2),VRT(1,1),NUMDSZ,ICORE(I0Z),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C Y1(Ab,Ij) <= Q(bn)*W(Ij,An)
C
        LISTW=9
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ)) 
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ)) 
        MAXTMP=MAX(DISSYW,NUMDSW,DISSYZ,NUMDSZ)
        I010=I001+IINTFP*MAX(DISSYW*NUMDSW,DISSYZ*NUMDSZ)
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXTMP
        ITMP3=ITMP2+IINTFP*MAXTMP
        IEND=ITMP3+IINTFP*MAXTMP
        IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2B4',IEND,MAXCOR) 
C
        CALL GETLST(ICORE(I001),1,NUMDSW,1,IRREP,LISTW)
C
        IOFFQ=I0Q(2)
        IOFFW=I001
        IOFFZ=I0Z
C
C Y(Ij,Ab) <= W(Ij,An)*Q(bn)
C
        DO 230 IRREPB=1,NIRREP
         IRREPA=DIRPRD(IRREPB,IRREP)
         IRREPN=IRREPB
         NUMA=VRT(IRREPA,1)
         NUMB=VRT(IRREPB,2)
         NUMN=POP(IRREPN,2)
         NROW=DISSYW*NUMA
         NCOL=NUMB
         NSUM=NUMN
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),NROW,
     &              Q1(IOFFQ),NCOL,ONE,ICORE(IOFFZ),NROW)
         IOFFW=IOFFW+IINTFP*NROW*NSUM
         IOFFQ=IOFFQ+IINTFP*NCOL*NSUM
         IOFFZ=IOFFZ+IINTFP*NROW*NCOL 
230     CONTINUE
C
        CALL TRANSP(ICORE(I0Z),ICORE(I001),DISSYZ,NUMDSZ)
        CALL GETLST(ICORE(I0Z),1,NUMDSZ,1,IRREP,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I0Z),1,ICORE(I001),1)
        CALL PUTLST(ICORE(I001),1,NUMDSZ,1,IRREP,LISTZ)
C
210    CONTINUE
C
      ENDIF
C
      RETURN
      END
