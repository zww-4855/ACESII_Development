      SUBROUTINE GINC1L(IRREPX,ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE FORMS THE THREE-BODY CONTRIBUTION TO THE SINGLES
C LEFT-HAND EIGENVECTOR
C
C       C(ai) = G(gf) W(gi,fa) - G(no) W(ni,oa)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DIMENSION ICORE(MAXCOR)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
C ALLOCATE MEMORY FOR SINGLES (C) ARRAY AND G ARRAYS
C
      LENCA=IRPDPD(IRREPX,9)
      LENCB=IRPDPD(IRREPX,10)
      LENGABA=IRPDPD(IRREPX,19)
      LENGABB=IRPDPD(IRREPX,20)
      LENGIJA=IRPDPD(IRREPX,21)
      LENGIJB=IRPDPD(IRREPX,22)
      I0CA=1
      I0GABA=I0CA+IINTFP*(LENCA+IUHF*LENCB)
      I0GIJA=I0GABA+IINTFP*(LENGABA+IUHF*LENGABB)
      I000 =I0GIJA+IINTFP*(LENGIJA+IUHF*LENGIJB)
      MXCOR=MAXCOR-I000+1
      CALL GETLST(ICORE(I0CA),1,1,1,3,490)
      CALL GETLST(ICORE(I0GIJA),1,1,1,1,491)
      CALL GETLST(ICORE(I0GABA),1,1,1,1,492)
      IF(IUHF.NE.0)THEN
       I0CB=I0CA+IINTFP*LENCA
       I0GABB=I0GABA+IINTFP*LENGABA
       I0GIJB=I0GIJA+IINTFP*LENGIJA
       CALL GETLST(ICORE(I0CB),1,1,1,4,490)
       CALL GETLST(ICORE(I0GIJB),1,1,1,2,491)
       CALL GETLST(ICORE(I0GABB),1,1,1,2,492)
      ENDIF
C
C TRANSPOSE C(ai) COEFFICIENTS
C
      CALL SYMTRA(IRREPX,VRT(1,1),POP(1,1),1,ICORE(I0CA),ICORE(I000))
      CALL SCOPY (LENCA,ICORE(I000),1,ICORE(I0CA),1)
      IF(IUHF.NE.0)THEN
       CALL SYMTRA(IRREPX,VRT(1,2),POP(1,2),1,ICORE(I0CB),ICORE(I000))
       CALL SCOPY (LENCB,ICORE(I000),1,ICORE(I0CB),1)
      ENDIF
C
C NEGATE CURRENT C VECTOR IN PREPARATION FOR CONTRACTIONS
C
      CALL VMINUS(ICORE(I0CA),LENCA+IUHF*LENCB)
C
      CALL DOOINVO(IRREPX,ICORE(I0CA),ICORE(I0GIJA),ICORE(I000),
     &             MXCOR,IUHF,7)
C
C TRANSPOSE C(ai) AND G(fg) COEFFICIENTS
C
      CALL SYMTRA(IRREPX,POP(1,1),VRT(1,1),1,ICORE(I0CA),ICORE(I000))
      CALL SCOPY (LENCA,ICORE(I000),1,ICORE(I0CA),1)
      IF(IUHF.NE.0)THEN
       CALL SYMTRA(IRREPX,POP(1,2),VRT(1,2),1,ICORE(I0CB),ICORE(I000))
       CALL SCOPY (LENCB,ICORE(I000),1,ICORE(I0CB),1)
      ENDIF
C
      CALL DVVINVO(IRREPX,ICORE(I0CA),ICORE(I0GABA),ICORE(I000),
     &             MXCOR,IUHF,27)
C
      CALL VMINUS(ICORE(I0CA),LENCA+IUHF*LENCB)
      CALL PUTLST(ICORE(I0CA),1,1,1,3,490)
      IF(IUHF.NE.0)THEN
       CALL PUTLST(ICORE(I0CB),1,1,1,4,490)
      ENDIF
C
      RETURN
      END
