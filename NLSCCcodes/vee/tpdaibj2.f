      SUBROUTINE TPDAIBJ2(ICORE,MAXCOR,IUHF)
C
C CALCULATION OF THE SECOND AIBJ CONTRIBUTION TO
C THE EOM-CCSD TWO-PARTICLE DENSITY MATRIX
C
C G(IB,AJ) <= L(IM,AE)*[R(JE)*T(MB)+T(JE)*R(MB)]
C
C SPIN CASES:
C
C AAAA : L(IM,AE)*X(JE)*Y(MB)
C BBBB : L(im,ae)*X(je)*Y(mb)
C ABAB : L(Im,Ae)*X(je)*Y(mb)
C BABA : L(iM,aE)*X(JE)*Y(MB)
C ABBA : L(Im,aE)*X(JE)*Y(mb)
C BAAB : L(iM,Ae)*X(je)*Y(MB)
C
CEND 
C
C CODED JG SEPTEMBER/93
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ZILCH,ONE,ONEM
      DIMENSION ICORE(MAXCOR),I0T(2),I0R(2)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ZILCH,ONE,ONEM /0.D0,1.0D0,-1.0D0/
C
C READ IN R1 AND T1 AMPLITUDES
C
      I0T(1)=1
      I0T(2)=I0T(1)+IRPDPD(1,9)*IINTFP*IUHF
      I0R(1)=I0T(2)+IRPDPD(1,10)*IINTFP
      I0R(2)=I0R(1)+IRPDPD(IRREPX,9)*IINTFP*IUHF
      ISTART=I0R(2)+IRPDPD(IRREPX,10)*IINTFP
C
      CALL GETLST(ICORE(I0T(1)),1,1,1,1,90)
      CALL GETLST(ICORE(I0R(1)),1,1,1,3,490)
      IF(IUHF.EQ.1) THEN
       CALL GETLST(ICORE(I0T(2)),1,1,1,2,90)
       CALL GETLST(ICORE(I0R(2)),1,1,1,4,490)
      ENDIF
C
C DEAL WITH SPIN CASES AAAA AND BBBB
C
      DO 10 ISPIN=1,1+IUHF
       LISTL=433+ISPIN
       LISTG=122+ISPIN
       DO 20 IRREPR=1,NIRREP 
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYG=IRPDPD(IRREPL,ISYTYP(1,LISTG))
        NUMDSG=IRPDPD(IRREPL,ISYTYP(2,LISTG))
        DISSYX=DISSYL
        NUMDSX=IRPDPD(IRREPL,20+ISPIN)
        MAXSIZ=MAX(NUMDSL*DISSYL,DISSYG*NUMDSG,DISSYX*NUMDSX)
        MAXT=MAX(NUMDSL,DISSYL,DISSYG,NUMDSG,DISSYX,NUMDSX)
        I000=ISTART
        I010=I000+IINTFP*MAXSIZ
        I020=I010+IINTFP*MAXSIZ
        ITMP1=I020
        ITMP2=ITMP1+MAXT*IINTFP
        ITMP3=ITMP2+MAXT*IINTFP
C
C READ L(AI,EM) AND TRANSPOSE TO L(AI,ME)
C
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL SYMTR1(IRREPR,VRT(1,ISPIN),POP(1,ISPIN),DISSYL,
     &              ICORE(I000),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C FORM L(AI,ME)*R(EJ) => X(AI,MJ)
C
        DO 100 IRREPE=1,NIRREP
         IRREPJ=DIRPRD(IRREPE,IRREPX)
         IRREPM=DIRPRD(IRREPE,IRREPR)
         NUME=VRT(IRREPE,ISPIN)
         NUMJ=POP(IRREPJ,ISPIN)
         NUMM=POP(IRREPM,ISPIN)
         NROW=DISSYL*NUMM
         NCOL=NUMJ
         NSUM=NUME
         IOFFL=I000+(ISYMOFF(IRREPE,IRREPR,15+ISPIN)-1)*DISSYL*IINTFP
         IOFFR=I0R(ISPIN)+(ISYMOFF(IRREPJ,IRREPX,8+ISPIN)-1)*IINTFP
         IOFFX=I010+(ISYMOFF(IRREPJ,IRREPL,20+ISPIN)-1)*DISSYL*IINTFP
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),NROW,
     &              ICORE(IOFFR),NSUM,ZILCH,ICORE(IOFFX),NROW)
100     CONTINUE
C
C NOW TRANSPOSE X(AI,MJ) -> X(AI,JM)
C
        CALL SYMTR1(IRREPL,POP(1,ISPIN),POP(1,ISPIN),DISSYL,
     &              ICORE(I010),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C FORM X(AI,JM)*T(BM) => G(AI,JB)
C
        DO 200 IRREPM=1,NIRREP
         IRREPB=IRREPM
         IRREPJ=DIRPRD(IRREPM,IRREPL)
         NUMB=VRT(IRREPB,ISPIN)
         NUMJ=POP(IRREPJ,ISPIN)
         NUMM=POP(IRREPM,ISPIN)
         NROW=DISSYL*NUMJ
         NCOL=NUMB
         NSUM=NUMM
         IOFFG=I000+(ISYMOFF(IRREPB,IRREPL,15+ISPIN)-1)*DISSYG*IINTFP
         IOFFT=I0T(ISPIN)+(ISYMOFF(IRREPM,1,8+ISPIN)-1)*IINTFP
         IOFFX=I010+(ISYMOFF(IRREPM,IRREPL,20+ISPIN)-1)*DISSYL*IINTFP
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONE,ICORE(IOFFX),NROW,
     &              ICORE(IOFFT),NCOL,ZILCH,ICORE(IOFFG),NROW)
200     CONTINUE
C
C NOW TRANSPOSE G(AI,JB) -> G(AI,BJ)
C
        CALL SYMTR1(IRREPL,POP(1,ISPIN),VRT(1,ISPIN),DISSYG,
     &              ICORE(I000),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL GETLST(ICORE(I010),1,NUMDSG,1,IRREPL,LISTG)
        CALL SAXPY (NUMDSG*DISSYG,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSG,1,IRREPL,LISTG)
20     CONTINUE
C
10    CONTINUE
C
C DO ABAB AND BABA SPIN CASES
C
      DO 30 ISPIN=1,1+IUHF
       LISTL=438-ISPIN
       LISTG=119-ISPIN
       DO 40 IRREPR=1,NIRREP 
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYG=IRPDPD(IRREPL,ISYTYP(1,LISTG))
        NUMDSG=IRPDPD(IRREPL,ISYTYP(2,LISTG))
        DISSYX=DISSYL
        NUMDSX=IRPDPD(IRREPL,23-ISPIN)
        MAXSIZ=MAX(NUMDSL*DISSYL,DISSYG*NUMDSG,DISSYX*NUMDSX)
        MAXT=MAX(NUMDSL,DISSYL,DISSYG,NUMDSG,DISSYX,NUMDSX)
        I000=ISTART
        I010=I000+IINTFP*MAXSIZ
        I020=I010+IINTFP*MAXSIZ
        ITMP1=I020
        ITMP2=ITMP1+MAXT*IINTFP
        ITMP3=ITMP2+MAXT*IINTFP
C
C READ L(AI,em) AND TRANSPOSE TO L(AI,me)
C
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL SYMTR1(IRREPR,VRT(1,3-ISPIN),POP(1,3-ISPIN),DISSYL,
     &              ICORE(I000),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C FORM L(AI,me)*R(ej) => X(AI,mj)
C
        DO 300 IRREPE=1,NIRREP
         IRREPJ=DIRPRD(IRREPE,IRREPX)
         IRREPM=DIRPRD(IRREPE,IRREPR)
         NUME=VRT(IRREPE,3-ISPIN)
         NUMJ=POP(IRREPJ,3-ISPIN)
         NUMM=POP(IRREPM,3-ISPIN)
         NROW=DISSYL*NUMM
         NCOL=NUMJ
         NSUM=NUME
         IOFFL=I000+(ISYMOFF(IRREPE,IRREPR,18-ISPIN)-1)*DISSYL*IINTFP
         IOFFR=I0R(3-ISPIN)+(ISYMOFF(IRREPJ,IRREPX,11-ISPIN)-1)*IINTFP
         IOFFX=I010+(ISYMOFF(IRREPJ,IRREPL,23-ISPIN)-1)*DISSYL*IINTFP
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),NROW,
     &              ICORE(IOFFR),NSUM,ZILCH,ICORE(IOFFX),NROW)
300     CONTINUE
C
C NOW TRANSPOSE X(AI,mj) -> X(AI,jm)
C
        CALL SYMTR1(IRREPL,POP(1,3-ISPIN),POP(1,3-ISPIN),DISSYL,
     &              ICORE(I010),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C FORM X(AI,jm)*T(bm) => G(AI,jb)
C
        DO 400 IRREPM=1,NIRREP
         IRREPB=IRREPM
         IRREPJ=DIRPRD(IRREPM,IRREPL)
         NUMB=VRT(IRREPB,3-ISPIN)
         NUMJ=POP(IRREPJ,3-ISPIN)
         NUMM=POP(IRREPM,3-ISPIN)
         NROW=DISSYL*NUMJ
         NCOL=NUMB
         NSUM=NUMM
         IOFFG=I000+(ISYMOFF(IRREPB,IRREPL,18-ISPIN)-1)*DISSYG*IINTFP
         IOFFT=I0T(3-ISPIN)+(ISYMOFF(IRREPM,1,11-ISPIN)-1)*IINTFP
         IOFFX=I010+(ISYMOFF(IRREPM,IRREPL,23-ISPIN)-1)*DISSYL*IINTFP
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFX),NROW,
     &              ICORE(IOFFT),NCOL,ZILCH,ICORE(IOFFG),NROW)
400     CONTINUE
C
C NOW TRANSPOSE G(AI,jb) -> G(AI,bj)
C
        CALL SYMTR1(IRREPL,POP(1,3-ISPIN),VRT(1,3-ISPIN),DISSYG,
     &              ICORE(I000),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL GETLST(ICORE(I010),1,NUMDSG,1,IRREPL,LISTG)
        CALL SAXPY (NUMDSG*DISSYG,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSG,1,IRREPL,LISTG)
40     CONTINUE
C
30    CONTINUE
C
C DO SPIN CASES ABBA AND BAAB
C
      DO 50 ISPIN=1,1+IUHF
       LISTL=440-ISPIN
       LISTG=124+ISPIN
       DO 60 IRREPR=1,NIRREP 
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYG=IRPDPD(IRREPL,ISYTYP(1,LISTG))
        NUMDSG=IRPDPD(IRREPL,ISYTYP(2,LISTG))
        DISSYX=DISSYL
        NUMDSX=IRPDPD(IRREPL,14)
        MAXSIZ=MAX(NUMDSL*DISSYL,DISSYG*NUMDSG,DISSYX*NUMDSX)
        MAXT=MAX(NUMDSL,DISSYL,DISSYG,NUMDSG,DISSYX,NUMDSX)
        I000=ISTART
        I010=I000+IINTFP*MAXSIZ
        I020=I010+IINTFP*MAXSIZ
        ITMP1=I020
        ITMP2=ITMP1+MAXT*IINTFP
        ITMP3=ITMP2+MAXT*IINTFP
C
C READ 
C        L(Ai,eM) AND TRANSPOSE TO L(Ai,Me)
C        L(aI,Em) AND TRANSPOSE TO L(aI,mE)
C
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL SYMTR1(IRREPR,VRT(1,3-ISPIN),POP(1,ISPIN),DISSYL,
     &              ICORE(I000),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C FORM 
C          L(Ai,Me)*R(ej) => X(Ai,Mj)
C          L(aI,mE)*R(EJ) => X(aI,mJ)
C
        DO 500 IRREPE=1,NIRREP
         IRREPJ=DIRPRD(IRREPE,IRREPX)
         IRREPM=DIRPRD(IRREPE,IRREPR)
         NUME=VRT(IRREPE,3-ISPIN)
         NUMJ=POP(IRREPJ,3-ISPIN)
         NUMM=POP(IRREPM,ISPIN)
         NROW=DISSYL*NUMM
         NCOL=NUMJ
         NSUM=NUME
         ITL=18+7*(ISPIN-1)
         ITX=14+10*(ISPIN-1)
         IOFFL=I000+(ISYMOFF(IRREPE,IRREPR,ITL)-1)*DISSYL*IINTFP
         IOFFR=I0R(3-ISPIN)+(ISYMOFF(IRREPJ,IRREPX,11-ISPIN)-1)*IINTFP
         IOFFX=I010+(ISYMOFF(IRREPJ,IRREPL,ITX)-1)*DISSYL*IINTFP
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFL),NROW,
     &              ICORE(IOFFR),NSUM,ZILCH,ICORE(IOFFX),NROW)
500     CONTINUE
C
C NOW TRANSPOSE 
C
C             X(Ai,Mj) -> X(Ai,jM)
C             X(aI,mJ) -> X(aI,Jm)
C
        CALL SYMTR1(IRREPL,POP(1,ISPIN),POP(1,3-ISPIN),DISSYL,
     &              ICORE(I010),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C FORM 
C
C             X(Ai,jM)*T(BM) => G(Ai,jB)
C             X(aI,Jm)*T(bm) => G(aI,Jb)
C
        DO 600 IRREPM=1,NIRREP
         IRREPB=IRREPM
         IRREPJ=DIRPRD(IRREPM,IRREPL)
         NUMB=VRT(IRREPB,ISPIN)
         NUMJ=POP(IRREPJ,3-ISPIN)
         NUMM=POP(IRREPM,ISPIN)
         NROW=DISSYL*NUMJ
         NCOL=NUMB
         NSUM=NUMM
         ITG=25-7*(ISPIN-1)
         ITX=24-10*(ISPIN-1)
         IOFFG=I000+(ISYMOFF(IRREPB,IRREPL,ITG)-1)*DISSYG*IINTFP
         IOFFT=I0T(ISPIN)+(ISYMOFF(IRREPM,1,8+ISPIN)-1)*IINTFP
         IOFFX=I010+(ISYMOFF(IRREPM,IRREPL,ITX)-1)*DISSYL*IINTFP
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFX),NROW,
     &              ICORE(IOFFT),NCOL,ZILCH,ICORE(IOFFG),NROW)
600     CONTINUE
C
C NOW TRANSPOSE 
C 
C              G(Ai,jB) -> G(Ai,Bj)
C              G(aI,Jb) -> G(aI,bJ)
C
        CALL SYMTR1(IRREPL,POP(1,3-ISPIN),VRT(1,ISPIN),DISSYG,
     &              ICORE(I000),ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL GETLST(ICORE(I010),1,NUMDSG,1,IRREPL,LISTG)
        CALL SAXPY (NUMDSG*DISSYG,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSG,1,IRREPL,LISTG)
60     CONTINUE
C
50    CONTINUE
C
      RETURN
      END
