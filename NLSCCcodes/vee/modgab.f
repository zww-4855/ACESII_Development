      SUBROUTINE MODGAB(IRREPG,IRREPTA,IRREPTB,GP,G,TA,TB,ISPIN,FACT)
C
C THIS ROUTINE ACCEPTS THE INPUT ARRAYS G(be), TA(ai) AND TB(ai) 
C AND MODIFIES G ACCORDING TO:
C
C       GP(be) = G(be) + FACT * TA(bm) * TB(em)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ZILCH,FACT,G,TA,TB,GP
      DIMENSION GP(*),G(*),TA(*),TB(*)
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ZILCH /1.0D0,0.0D0/
C
C CARRY OUT MULTIPLICATION
C                                                  +
C           GP(be) = G(be) + FACT * TA(bm) * TB(em)
C
      DO 10 IRREPE=1,NIRREP
       IRREPB=DIRPRD(IRREPE,IRREPG)
       IRREPM=DIRPRD(IRREPB,IRREPTA)
       NUME=VRT(IRREPE,ISPIN)
       NUMB=VRT(IRREPB,ISPIN)
       NUMM=POP(IRREPM,ISPIN)
       IOFFTA=ISYMOFF(IRREPM,IRREPTA,8+ISPIN) 
       IOFFTB=ISYMOFF(IRREPM,IRREPTB,8+ISPIN) 
       IOFFG =ISYMOFF(IRREPE,IRREPG ,18+ISPIN)
       CALL XGEMM('N','T',NUMB,NUME,NUMM,FACT,TA(IOFFTA),NUMB,
     &            TB(IOFFTB),NUME,ZILCH,GP(IOFFG),NUMB)
       CALL SAXPY(NUMB*NUME,ONE,G(IOFFG),1,GP(IOFFG),1)
10    CONTINUE
C
      RETURN
      END
