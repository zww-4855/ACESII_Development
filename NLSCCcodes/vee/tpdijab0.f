      SUBROUTINE TPDIJAB0(ICORE,MAXCOR,IUHF)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL MBPT2,CC,CCD,RCCD,DRCCD,LCCD,LCCSD,CC2
      INTEGER DIRPRD,DISSYG,POP,VRT
      DIMENSION ICORE(MAXCOR)
      DIMENSION I0T(2)
C
C CALCULATION OF THE `ZEROTH' IJAB CONTRIBUTION TO
C THE EOM-CCSD TWO-PARTICLE DENSITY MATRIX
C
C  1/4 P(IJ) P(AB) R(MN,EF) L(MN,EF) TAU(IJ,AB)
C
C   WHICH REDUCES TO 1/4 P(IJ) P(AB) TAU(IJ,AB)
C   DUE TO THE BIORTHOGONALITY CONDITIONS FOR R
C   AND L  (NOTE R(I)* L(J) = DELTA(I,J))
C  
C NOTE THAT 4*G(IJ,AB) IS CALCULATED
C
CEND 
C
C CODED JG OCTOBER/93
C
      COMMON/REFTYPE/MBPT2,CC,CCD,RCCD,DRCCD,LCCD,LCCSD,CC2
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,TWO /1.0D0,2.D0/
C
C READ IN T1 AMPLITUDES
C
      I0T(1)=1
      I0T(2)=I0T(1)+IRPDPD(1,9)*IINTFP*IUHF
      ISTART=I0T(2)+IRPDPD(1,10)*IINTFP
C
      CALL GETLST(ICORE(I0T(1)),1,1,1,1,90)
C
      IF(IUHF.EQ.1) THEN
       CALL GETLST(ICORE(I0T(2)),1,1,1,2,90)
      ENDIF
C
      DO 1000 ISPIN=3-2*IUHF,3
C
       DO 200 IRREP=1,NIRREP
C
        LISTG =113+ISPIN
        LISTT =43 +ISPIN 
        NUMSYG=IRPDPD(IRREP,ISYTYP(2,LISTG))
        DISSYG=IRPDPD(IRREP,ISYTYP(1,LISTG))
C
        I000=ISTART
        IEND=I000+IINTFP*NUMSYG*DISSYG
        IF(IEND.GE.MAXCOR) CALL INSMEM('TPDABIJ1',IEND,MAXCOR)
C
        CALL GETLST(ICORE(I000),1,NUMSYG,1,IRREP,LISTT)
C
C FORM TAU AMPLITUDES
C
        IF(ISPIN.LT.3) THEN
         CALL FTAU(ICORE(I000),ICORE(I0T(ISPIN)),ICORE(I0T(ISPIN)),
     &             DISSYG,NUMSYG,POP(1,ISPIN),POP(1,ISPIN),
     &             VRT(1,ISPIN),VRT(1,ISPIN),IRREP,ISPIN,ONE)
        ELSE
         CALL FTAU(ICORE(I000),ICORE(I0T(1)),ICORE(I0T(2)),
     &             DISSYG,NUMSYG,POP(1,1),POP(1,2),VRT(1,1),
     &             VRT(1,2),IRREP,ISPIN,ONE)
        ENDIF
C
C CCD is a iterative CC method and list 43, 44, 45 are created in vcc.
C Ajith Perera, 07/2013
C
        IF(MBPT2 .OR. RCCD .OR. DRCCD)THEN
         CALL GETLST(ICORE(IEND),1,NUMSYG,1,IRREP,60+ISPIN)
         CALL SAXPY (NUMSYG*DISSYG,ONE,ICORE(IEND),1,ICORE(I000),1)
        ENDIF
C
        CALL PUTLST(ICORE(I000),1,NUMSYG,1,IRREP,LISTG)
C
200    CONTINUE
C
1000  CONTINUE
C
      TWO=0.D0
      IF(IUHF.EQ.0)
     &   CALL CHECKGAM1(ICORE,16,116,TWO,IUHF,2,VRT)
      IF(IUHF.EQ.1) THEN
       CALL CHECKGAM(ICORE,16,116,TWO)
       CALL CHECKGAM(ICORE,14,114,TWO)
       CALL CHECKGAM(ICORE,15,115,TWO)
      ENDIF
C
      RETURN
      END
