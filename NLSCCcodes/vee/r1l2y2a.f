      SUBROUTINE R1L2Y2A(Q1,R1,ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = P(ij) R(me) L(im,fe) W(fj,ab)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ZILCH
      DIMENSION ICORE(MAXCOR),R1(*),Q1(*),I0Q(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ZILCH/1.0D0,0.0D0/
C
      I0Q(1)=1
      IF(IUHF.NE.0)THEN
       I0Q(2)=I0Q(1)+IINTFP*NT(1)
      ELSE
       I0Q(2)=I0Q(1)
      ENDIF
      I000=1
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE.
C
C  Y1(Ab,Ij) = Y(Ab,Ij) + Y(Ba,Ji)
C
C FORM PRODUCT Y(Ab,Ij) = W(Ab,Fj) * Q(fi)
C
       MAXVRT=0
       DO 5 IRREP=1,NIRREP
        MAXVRT=MAX(MAXVRT,VRT(IRREP,1))
5      CONTINUE
C
       DO 10 IRREP=1,NIRREP
        LISTW=30
        LISTZ=63
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ)) 
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ)) 
        I010=I000+IINTFP*MAX(DISSYW*MAXVRT,DISSYZ*NUMDSZ)
        I020=I010+IINTFP*DISSYZ*NUMDSZ
        MAXW=MAX(DISSYW,NUMDSW,DISSYZ,NUMDSZ)
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXW 
        ITMP3=ITMP2+IINTFP*MAXW
        IEND=ITMP3+IINTFP*MAXW 
        IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2A1',IEND,MAXCOR) 
C
C Y(Ab,jI) = W(Ab,jF) * Q(FI)
C
        IOFFLIST=1
        IOFFZ=I010
        IOFFW=I000
C
        DO 20 IRREPJ=1,NIRREP
         IRREPI=DIRPRD(IRREPJ,IRREP)
         IRREPF=IRREPI
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,1)
         NUMF=VRT(IRREPF,1)
         NROW=DISSYW
         NCOL=NUMI
         NSUM=NUMF
         IOFFQ=I0Q(1)+IINTFP*(ISYMOFF(IRREPI,1,9)-1)
C
         DO 15 J=1,NUMJ
C       
          CALL GETLST(ICORE(IOFFW),IOFFLIST,NUMF,1,IRREP,LISTW)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &               Q1(IOFFQ),NSUM,ZILCH,ICORE(IOFFZ),NROW)
C
          IOFFLIST=IOFFLIST+NUMF
          IOFFZ=IOFFZ+IINTFP*NROW*NCOL
C
15       CONTINUE
C 
20      CONTINUE
C
        CALL SYMRHF(IRREP,VRT(1,1),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
10     CONTINUE
C
      ELSE
C
C UHF
C
C  Y1(AB,IJ) = Q(FI)*W(AB,FJ) + ANTISYMMETRIZATION
C  
C  Y1(Ab,Ij) = Q(FI)*W(Ab,Fj) + Q(fj)*W(Ab,If)
C
C AAAA AND BBBB
C
       DO 100 ISPIN=1,2
C
        MAXVRT=0
        DO 105 IRREP=1,NIRREP
         MAXVRT=MAX(MAXVRT,VRT(IRREP,ISPIN))
105     CONTINUE
C
        LISTW=26+ISPIN
        LISTZ=60+ISPIN
        DO 110 IRREP=1,NIRREP
         DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
         NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
         DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ)) 
         NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ)) 
         NUMDSZX=IRPDPD(IRREP,20+ISPIN)
         MAXTMP=MAX(NUMDSZX,NUMDSW,DISSYW,DISSYZ)
         I010=I000+IINTFP*MAX(DISSYW*MAXVRT,DISSYZ*NUMDSZX)
         I020=I010+IINTFP*DISSYZ*NUMDSZX
         ITMP1=I020
         ITMP2=ITMP1+IINTFP*MAXTMP
         ITMP3=ITMP2+IINTFP*MAXTMP
         IEND=ITMP3+IINTFP*MAXTMP
         IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2A2',IEND,MAXCOR)
C
C Y(AB,IJ) = W(AB,FJ)*Q(FI)
C
         IOFFLIST=1
         IOFFZ=I010
         IOFFW=I000
C
         DO 120 IRREPJ=1,NIRREP
          IRREPI=DIRPRD(IRREPJ,IRREP)
          IRREPF=IRREPI
          NUMI=POP(IRREPI,ISPIN)
          NUMJ=POP(IRREPJ,ISPIN)
          NUMF=VRT(IRREPF,ISPIN)
          NROW=DISSYW
          NCOL=NUMI
          NSUM=NUMF
          IOFFQ=I0Q(ISPIN)+IINTFP*(ISYMOFF(IRREPI,1,8+ISPIN)-1)
C
          DO 115 J=1,NUMJ
C
           CALL GETLST(ICORE(IOFFW),IOFFLIST,NUMF,1,IRREP,LISTW)
           CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &               Q1(IOFFQ),NSUM,ZILCH,ICORE(IOFFZ),NROW)
C
           IOFFLIST=IOFFLIST+NUMF
           IOFFZ=IOFFZ+IINTFP*NROW*NCOL
115       CONTINUE
C 
120      CONTINUE
C
         CALL ASSYM2(IRREP,POP(1,ISPIN),DISSYZ,ICORE(I010))
         CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
         CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
c         call checksum('aaaa',icore(i000),numdsz*dissyz,S)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
110     CONTINUE
100    CONTINUE
C
C  ABAB : Y1(Ab,Ij) = Q(FI)*W(Ab,Fj) + Q(fj)*W(Ab,If)
C
       LISTZ=63
C
       MAXVRT=0
       MAXPOP=0
       DO 205 IRREP=1,NIRREP
        MAXVRT=MAX(MAXVRT,VRT(IRREP,1))
        MAXPOP=MAX(MAXPOP,POP(IRREP,1))
205    CONTINUE
C
       DO 210 IRREP=1,NIRREP
C
C W(Ab,Fj)*Q(FI)
C
        LISTW=30
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ)) 
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ)) 
        MAXTMP=MAX(DISSYW,NUMDSW,DISSYZ,NUMDSZ)
        I010=I000+IINTFP*MAX(DISSYW*MAX(MAXVRT,MAXPOP),DISSYZ*NUMDSZ)
        I020=I010+IINTFP*DISSYZ*NUMDSZ
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXTMP
        ITMP3=ITMP2+IINTFP*MAXTMP
        IEND=ITMP3+IINTFP*MAXTMP
        IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2A3',IEND,MAXCOR)
C
C Y(Ab,jI) = W(Ab,jF)* Q(FI)
C
        IOFFZ=I010
        IOFFW=I000
        IOFFLIST=1
C
        DO 220 IRREPJ=1,NIRREP
         IRREPI=DIRPRD(IRREPJ,IRREP)
         IRREPF=IRREPI
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
         NUMF=VRT(IRREPF,1)
         NROW=DISSYW
         NCOL=NUMI
         NSUM=NUMF
         IOFFQ=I0Q(1)+IINTFP*(ISYMOFF(IRREPI,1,9)-1)
C
         DO 215 J=1,NUMJ
C
          CALL GETLST(ICORE(IOFFW),IOFFLIST,NUMF,1,IRREP,LISTW)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &               Q1(IOFFQ),NSUM,ZILCH,ICORE(IOFFZ),NROW)
C
          IOFFLIST=IOFFLIST+NUMF
          IOFFZ=IOFFZ+IINTFP*NROW*NCOL 
C
215      CONTINUE
c         call checksum('abab 1',icore(i010),numdsz*dissyz,S)
C
220     CONTINUE
C
c        call checksum('abab',icore(i010),numdsz*dissyz,S)
C
C W(Ab,If)*Q(fj)
C
        LISTW=29
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ)) 
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ)) 
        MAXTMP=MAX(DISSYW,NUMDSW,DISSYZ,NUMDSZ)
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXTMP
        ITMP3=ITMP2+IINTFP*MAXTMP
        IEND=ITMP3+IINTFP*MAXTMP
        IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2A4',IEND,MAXCOR)
C
        IOFFW=I000
        IOFFZ=I010
        IOFFLIST=1
C
C Y(Ab,Ij) <= W(Ab,If)*Q(fj)
C
        DO 230 IRREPJ=1,NIRREP
         IRREPI=DIRPRD(IRREPJ,IRREP)
         IRREPF=IRREPJ
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
         NUMF=VRT(IRREPF,2)
         NROW=DISSYW*NUMI
         NCOL=NUMJ
         NSUM=1
         IOFFQ=I0Q(2)+IINTFP*(ISYMOFF(IRREPJ,1,10)-1)
C
         DO 225 F=1,NUMF
C
          CALL GETLST(ICORE(IOFFW),IOFFLIST,NUMI,1,IRREP,LISTW)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),NROW,
     &              Q1(IOFFQ),NUMF,ONE,ICORE(IOFFZ),NROW)
C
          IOFFQ=IOFFQ+IINTFP
          IOFFLIST=IOFFLIST+NUMI
225      CONTINUE
C
         IOFFZ=IOFFZ+IINTFP*NROW*NCOL
C 
230     CONTINUE
C
c        call checksum('abab',icore(i010),numdsz*dissyz,S)
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
210    CONTINUE
C
      ENDIF
C
      RETURN
      END
