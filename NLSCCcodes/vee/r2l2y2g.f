      SUBROUTINE R2L2Y2G(ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = -1/2 P(Ij) R(mn,ef)*L(in,ef)*W(mj,ab)
C
C  RHF : SPIN-ADPAPTED CODE
C  UHF : ALL SPIN CASES ARE EXPLICITLY CONSIDERED
C
CEND
C
C CODED SEPTEMBER/93 JFS AND JG
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
C
      DIMENSION ICORE(MAXCOR)
      DIMENSION I00G(2)
C
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
      IF(IUHF.EQ.0)THEN
       LISTW=16
       LISTZ=63
       I0G=1
       I000=I0G+IINTFP*IRPDPD(1,21)
C
C SPIN ADAPTED RHF CODE.
C
C  Y1(Ab,Ij) = Z(Ab,Ij) + Z(Ba,Ji)
C
C WHERE
C
C   Z(Ab,Ij) = [2 R(Mn,Ef)-R(Nm,Ef)]*L(In,Ef)*W(Ab,Im)
C
C
C FIRST PICK UP G(MI) = L(In,Ef)*[2*R(Mn,Ef)-R(Nm,Ef)] FROM LIST 191
C
       CALL GETLST(ICORE(I0G),1,1,1,1,191)
C
C FORM PRODUCT Z(Ab,Ij)=W(Ab,Mj)*G(MI)
C
       DO 10 IRREP=1,NIRREP
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ))
        MAXSIZ=MAX(DISSYZ*NUMDSZ,DISSYW*NUMDSW)
        MAXT  =MAX(DISSYZ,NUMDSZ,DISSYW,NUMDSW)
        I010=I000+IINTFP*MAXSIZ
        I020=I010+IINTFP*MAXSIZ
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXT
        ITMP3=ITMP2+IINTFP*MAXT
        ITMP4=ITMP3+IINTFP*MAXT
        CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREP,LISTW)
        CALL SYMTR1(IRREP,POP(1,1),POP(1,1),DISSYW,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C NOW FORM Z(Ij,Ab) = L(Ab,jM)*G(MI)
C
        DO 20 IRREPM=1,NIRREP
         IRREPJ=DIRPRD(IRREPM,IRREP)
         IRREPI=IRREPM
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,1)
         NUMM=POP(IRREPM,1)
         NROW=DISSYW*NUMJ
         NCOL=NUMI
         NSUM=NUMM
         IOFFW=I000+(ISYMOFF(IRREPM,IRREP,21)-1)*DISSYW*IINTFP
         IOFFZ=I010+(ISYMOFF(IRREPI,IRREP,21)-1)*DISSYZ*IINTFP
         IOFFG=I0G+(ISYMOFF(IRREPI,1,21)-1)*IINTFP
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),NROW,
     &              ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),NROW)
20      CONTINUE
        CALL SYMTR1(IRREP,POP(1,1),POP(1,1),DISSYW,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL SYMRHF(IRREP,VRT(1,1),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1) 
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
10     CONTINUE
C
      ELSE
C
       I00G(1)=1
       I00G(2)=I00G(1)+IINTFP*IRPDPD(1,21)
       ISTART=I00G(2)+IINTFP*IRPDPD(1,22)
       CALL GETLST(ICORE(I00G(1)),1,1,1,1,191)
       CALL GETLST(ICORE(I00G(2)),1,1,1,2,191)
C
C DO FIRST SPIN CASES AAAA AND BBBB
C
       DO 1000 ISPIN=1,2
C
        LISTW=13+ISPIN
        LISTZ=60+ISPIN
C
C LOOP OVER IRREPS
C
        DO 100 IRREP=1,NIRREP
C
         NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
         DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
         NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ))
         DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
         NOCCSQ=IRPDPD(IRREP,20+ISPIN)
C
         I000=ISTART
         I010=I000+IINTFP*DISSYW*NOCCSQ
         ITMP=I010+IINTFP*DISSYZ*NOCCSQ
         IEND=ITMP+IINTFP*3*MAX(DISSYZ,NOCCSQ)
         IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y2G',IEND,MAXCOR)
C
         CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREP,LISTW)
C
         CALL SYMEXP(IRREP,POP(1,ISPIN),DISSYW,ICORE(I000))
C
         DO 120 IRREPM=1,NIRREP
C
          IRREPI=DIRPRD(IRREPM,IRREP)
          IRREPJ=IRREPM
          NUMM=POP(IRREPM,ISPIN)
          NUMI=POP(IRREPI,ISPIN)
          NUMJ=POP(IRREPJ,ISPIN)
C
          NROW=DISSYW*NUMI
          NCOL=NUMJ
          NSUM=NUMM
C
          IOFFW=I000+IINTFP*DISSYW*
     &               (ISYMOFF(IRREPM,IRREP,20+ISPIN)-1)
          IOFFZ=I010+IINTFP*DISSYZ*
     &               (ISYMOFF(IRREPJ,IRREP,20+ISPIN)-1)
          IOFFG=I00G(ISPIN)+IINTFP*
     &               (ISYMOFF(IRREPJ,1,20+ISPIN)-1)
C
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),
     &               NROW,ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),
     &               NROW)
C
120      CONTINUE
C
         CALL ASSYM(IRREP,POP(1,ISPIN),DISSYZ,DISSYZ,
     &              ICORE(I000),ICORE(I010))
C
         CALL GETLST(ICORE(I010),1,NUMDSZ,1,IRREP,LISTZ)
         CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I000),1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),1,NUMDSZ,1,IRREP,LISTZ)
C
100     CONTINUE
C
1000   CONTINUE
C
C DO ABAB SPIN CASE
C
       LISTW=16
       LISTZ=63
C
C LOOP OVER IRREPS
C
       DO 1100 IRREP=1,NIRREP
C
        NUMDSW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,LISTZ))
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
C
        I000=ISTART
        I010=I000+IINTFP*NUMDSW*DISSYW
        ITMP=I010+IINTFP*NUMDSZ*DISSYZ
        IEND=ITMP+IINTFP*3*MAX(NUMDSZ,DISSYZ)
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y2G',IEND,MAXCOR)
C
        CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREP,LISTW)
C
        DO 1120 IRREPM=1,NIRREP
C
         IRREPI=DIRPRD(IRREPM,IRREP)
         IRREPJ=IRREPM
         NUMM=POP(IRREPM,2)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
C
         NROW=DISSYW*NUMI
         NCOL=NUMJ
         NSUM=NUMM
C
         IOFFW=I000+IINTFP*DISSYW*
     &              (ISYMOFF(IRREPM,IRREP,14)-1)
         IOFFZ=I010+IINTFP*DISSYZ*
     &              (ISYMOFF(IRREPJ,IRREP,14)-1)
         IOFFG=I00G(2)+IINTFP*
     &              (ISYMOFF(IRREPJ,1,22)-1)
C
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),
     &              NROW,ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),
     &              NROW)
C
1120    CONTINUE
C
        CALL SYMTR1(IRREP,POP(1,1),POP(1,2),DISSYW,ICORE(I000),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYW),
     &              ICORE(ITMP+2*IINTFP*DISSYW))
        CALL SYMTR1(IRREP,POP(1,1),POP(1,2),DISSYZ,ICORE(I010),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYZ),
     &              ICORE(ITMP+2*IINTFP*DISSYZ))

        DO 1130 IRREPM=1,NIRREP
C
         IRREPJ=DIRPRD(IRREPM,IRREP)
         IRREPI=IRREPM
         NUMM=POP(IRREPM,1)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
C
         NROW=DISSYW*NUMJ
         NCOL=NUMI
         NSUM=NUMM
C
         IOFFW=I000+IINTFP*DISSYW*
     &              (ISYMOFF(IRREPM,IRREP,24)-1)
         IOFFZ=I010+IINTFP*DISSYZ*
     &              (ISYMOFF(IRREPI,IRREP,24)-1)
         IOFFG=I00G(1)+IINTFP*
     &              (ISYMOFF(IRREPI,1,21)-1)
C
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),
     &              NROW,ICORE(IOFFG),NSUM,ONE,ICORE(IOFFZ),
     &              NROW)
C
1130    CONTINUE
C
        CALL SYMTR1(IRREP,POP(1,2),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYZ),
     &              ICORE(ITMP+2*IINTFP*DISSYZ))
C
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
        CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ)
C
1100   CONTINUE
C
      ENDIF
C
C ALL DONE, RETURN
C
      RETURN
      END  
