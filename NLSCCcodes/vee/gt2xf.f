      SUBROUTINE GT2XF(Z,IRREPA,IRREPB,LISTA0,LISTB,IOF0,SCR,MAXCOR,
     &                 IUHF,IMODE)
C
C THIS ROUTINE PERFORMS A GENERIC CONTRACTION OF THE TYPE
C
C    Z(ai) = A(ai,em) * B(em)
C
C WHERE A AND B [AND HENCE Z] HAVE ARBITRARY TOTAL SYMMETRIES.
C
C THE RESULT IS RETURNED IN Z.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYA
      DIMENSION SCR(MAXCOR),Z(*)
      DIMENSION I0Z1(2),I0B1(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
C
      DATA TWO,ONE,ONEM,ZILCH /2.0D0,1.0D0,-1.0D0,0.0D0/
C
      IRREPZ=DIRPRD(IRREPA,IRREPB)
C
      IF(IUHF.NE.0)THEN
       I0Z1(1)=1
       I0Z1(2)=I0Z1(1)+IRPDPD(IRREPZ,9)
       I0B1(1)=1
       I0B1(2)=I0B1(1)+IRPDPD(IRREPB,9)
       CALL GETLST(SCR(I0B1(1)),1,1,1,1+IOF0,LISTB)
       CALL GETLST(SCR(I0B1(2)),1,1,1,2+IOF0,LISTB)
      ELSE
       I0Z1(1)=1
       I0Z1(2)=I0Z1(1)
       I0B1(1)=1
       I0B1(2)=I0B1(1)
       CALL GETLST(SCR(I0B1(1)),1,1,1,1+IOF0,LISTB)
      ENDIF
      IF(IMODE.EQ.2)I0Z1(2)=1
      ITOP   =I0B1(2)+IRPDPD(IRREPB,10)
C
C
C    Z(AI) = A(AI,EM)*B(EM) + A(AI,em)*B(em) [ISPIN=1, UHF]
C    Z(ai) = A(ai,em)*B(em) + A(ai,EM)*B(EM) [ISPIN=2, UHF]
C    Z(ai) = [2*A(ai,em) - A(ei,am)] * B(em) [ISPIN=1, RHF]
C
      I000=ITOP
      IF(IMODE.EQ.0)THEN
       IBOT=1
       ITOP=1+IUHF
      ELSE
       IBOT=IMODE
       ITOP=IMODE
      ENDIF
      DO 10 ISPIN=IBOT,ITOP
       IRREPAR=IRREPB
       IRREPAL=IRREPZ
       LISTA1=LISTA0+4-ISPIN
       LISTA2=LISTA0-1+ISPIN
C
C FIRST CONTRACTION : A(AI,em)*B(em) OR A(ai,EM)*B(EM)
C
       DISSYA=IRPDPD(IRREPAL,ISYTYP(1,LISTA1))
       NUMDSA=IRPDPD(IRREPAR,ISYTYP(2,LISTA1))
       I010=I000+NUMDSA*DISSYA
       CALL GETLST(SCR(I000),1,NUMDSA,1,IRREPAR,LISTA1)
       IF(IUHF.EQ.0)THEN
        I020=I010+NUMDSA*DISSYA
        CALL GETLST(SCR(I010),1,NUMDSA,1,IRREPAR,LISTA0+5)
        CALL SSCAL(NUMDSA*DISSYA,TWO,SCR(I000),1)
        CALL SAXPY(NUMDSA*DISSYA,ONEM,SCR(I010),1,SCR(I000),1)
       ENDIF
       CALL XGEMM('N','N',DISSYA,1,NUMDSA,ONE,SCR(I000),DISSYA,
     &            SCR(I0B1(3-ISPIN)),NUMDSA,ZILCH,Z(I0Z1(ISPIN)),
     &            DISSYA)
C
C SECOND CONTRACTION : L(AI,EM)*S(EM) OR L(ai,em)*S(em)
C
C [MINUS SIGN BECAUSE OF SCREWED UP WAY THE LISTS ARE STORED]
C
       IF(IUHF.NE.0)THEN
        DISSYA=IRPDPD(IRREPAL,ISYTYP(1,LISTA2))
        NUMDSA=IRPDPD(IRREPAR,ISYTYP(2,LISTA2))
        CALL GETLST(SCR(I000),1,NUMDSA,1,IRREPAR,LISTA2)
        CALL XGEMM('N','N',DISSYA,1,NUMDSA,ONEM,SCR(I000),DISSYA,
     &             SCR(I0B1(ISPIN)),NUMDSA,ONE,Z(I0Z1(ISPIN)),
     &             DISSYA)
       ENDIF
C        
10    CONTINUE
C
      RETURN
      END
