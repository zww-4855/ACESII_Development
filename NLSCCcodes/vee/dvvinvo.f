      SUBROUTINE DVVINVO(IRREPX,IVO,DVV,ICORE,MXCOR,IUHF,LISTW0)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS:
C
C      IVO(A,I) =  [SUM D(E,F) * <EA||FI> + D(e,f) * <eA|fI>] (ISPIN=1)
C
C      IVO(a,i) =  [SUM D(e,f) * <ea||fi> + D(E,F) * <Ea|Fi>] (ISPIN=2)
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION IVO,DVV
      DIMENSION DVV(1),IVO(1),ICORE(MXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE
C
       LISTW=LISTW0-1+4
       DO 120 IRREP=1,NIRREP
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,LISTW))
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*NUMDIS
        I030=I020+IINTFP*NUMDIS
        IF(I030.LE.MXCOR)THEN
         CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTW)
         CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &                ICORE(I010),ICORE(I020))
         CALL DDOT24(IRREPX,IRREP,IVO,DVV,ICORE(I000),ICORE(I010),
     &               DISSIZ,
     &               VRT(1,1),POP(1,1),VRT(1,1),VRT(1,1),VRT(1,1),
     &               POP(1,1),'STST')
        ELSE
         CALL DDOT24X(IRREP,IVO,DVV,ICORE(I000),
     &                VRT(1,1),VRT(1,1),VRT(1,1),POP(1,1),LISTW,
     &                9,19,19,'STST',IRREPX,IRREPX,1,.TRUE.,.FALSE.)
        ENDIF
120    CONTINUE
      ELSEIF(IUHF.NE.0)THEN
       DO 10 ISPIN=1,1+IUHF
        LISTW1=LISTW0-1+ISPIN
        LISTW2=LISTW0-1+2+ISPIN
        DO 20 IRREP=1,NIRREP
         NDSZ1=IRPDPD(IRREP,ISYTYP(1,LISTW1))
         NDSZF=IRPDPD(IRREP,18+ISPIN)
         NDIS1=IRPDPD(IRREP,ISYTYP(2,LISTW1))
         NDSZ2=IRPDPD(IRREP,ISYTYP(1,LISTW2))
         NDIS2=IRPDPD(IRREP,ISYTYP(2,LISTW2))
         IOFFI=1+(ISPIN-1)*IRPDPD(IRREPX,9)
C
C DO FIRST PART - D(E,F) * <EA||FI> [D(e,f) * <ea||fi>]
C
         I000=1
         I010=I000+IINTFP*NDSZF*NDIS1
         I020=I010+IINTFP*NFEA(ISPIN)
         IOFFD=1+(ISPIN-1)*IRPDPD(IRREPX,19)
         IF(I020.LE.MXCOR)THEN
          CALL GETLST(ICORE(I000),1,NDIS1,2,IRREP,LISTW1)
          CALL SYMEXP2(IRREP,VRT(1,ISPIN),NDSZF,NDSZ1,NDIS1,ICORE(I000),
     &                 ICORE(I000))
          CALL DDOT24(IRREPX,IRREP,IVO(IOFFI),DVV(IOFFD),ICORE(I000),
     &               ICORE(I010),NDSZF,VRT(1,ISPIN),POP(1,ISPIN),
     &               VRT(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),
     &               POP(1,ISPIN),'STST')
         ELSE
          CALL DDOT24X(IRREP,IVO(IOFFI),DVV(IOFFD),ICORE(I000),
     &                 VRT(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),
     &                 POP(1,ISPIN),LISTW1,
     &                 8+ISPIN,18+ISPIN,18+ISPIN,'STST',
     &                 IRREPX,IRREPX,1,.FALSE.,.TRUE.)
         ENDIF
C
C NOW DO SECOND PART 
C
C          D(e,f) * <Ae|If>   [ISPIN=1]
C
C          D(E,F) * <Ea|Fi>   [ISPIN=2]
C
         I000=1
         I010=I000+IINTFP*NDSZ2*NDIS2
         IOFFD=1+(2-ISPIN)*IRPDPD(IRREPX,19)
         IF(I010.LE.MXCOR)THEN
          CALL GETLST(ICORE(I000),1,NDIS2,2,IRREP,LISTW2)
          IF(ISPIN.EQ.1)THEN
           CALL DDOT24(IRREPX,IRREP,IVO(IOFFI),DVV(IOFFD),ICORE(I000),
     &                 ICORE(I010),NDSZ2,VRT(1,ISPIN),POP(1,ISPIN),
     &                 VRT(1,1),VRT(1,2),POP(1,1),VRT(1,2),'TSTS')
          ELSE
           CALL DDOT24(IRREPX,IRREP,IVO(IOFFI),DVV(IOFFD),ICORE(I000),
     &                 ICORE(I010),NDSZ2,VRT(1,ISPIN),POP(1,ISPIN),
     &                 VRT(1,1),VRT(1,2),VRT(1,1),POP(1,2),'STST')
          ENDIF
         ELSE
          IF(ISPIN.EQ.1)THEN
           CALL DDOT24X(IRREP,IVO(IOFFI),DVV(IOFFD),ICORE(I000),
     &                  VRT(1,1),VRT(1,2),POP(1,1),VRT(1,2),LISTW2,
     &                  9,20,13,'TSTS',IRREPX,IRREPX,1,
     &                  .FALSE.,.FALSE.)
          ELSE
           CALL DDOT24X(IRREP,IVO(IOFFI),DVV(IOFFD),ICORE(I000),
     &                  VRT(1,1),VRT(1,2),VRT(1,1),POP(1,2),LISTW2,
     &                  10,19,13,'STST',IRREPX,IRREPX,1,
     &                  .FALSE.,.FALSE.)
          ENDIF
         ENDIF
20      CONTINUE
10     CONTINUE
      ENDIF
      RETURN
      END
