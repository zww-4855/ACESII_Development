      SUBROUTINE R1L2Y2H(R1,ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = P(ij)P(ab) [R(me)*L(ae,in)*W(mj,nb)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,TWO,HALF
      CHARACTER*4 SPCASE(2)
      DIMENSION ICORE(MAXCOR),R1(*)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,ONEM,TWO,ZILCH/1.0D0,-1.0D0,2.0D0,0.0D0/
      DATA HALF/0.5D0/
      DATA SPCASE/'AAAA','BBBB'/
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE.
C
C   Y(Ab,Ij) = Q(Ab,Ij) + Q(Ba,Ji)
C
C WHERE
C
C   Q(ab,ij) = 1/2 R(me) * [2L(Ae,In) - L(Ea,In)] * [2W(Mj,Nb)-W(Jm,Nb)]
C
C            - 1/2 R(me)*L(Ea,In)*W(Jm,Nb) - R(me)*L(Eb,In)*W(Jm,Na)]
C
C NOW FORM SPIN ADAPTED L AMPLITUDES AND STORE IN L(AI,en)
C
       I000=1
       DO 30 IRREPR=1,NIRREP
        LISTL=437
        LISTZ=42
        LISTX=426
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        I010=I000+IINTFP*NUMDSL*DISSYL 
        I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL GETLST(ICORE(I010),1,NUMDSL,1,IRREPR,LISTL+2)
        CALL SSCAL (NUMDSL*DISSYL,TWO,ICORE(I000),1)
        CALL SAXPY (NUMDSL*DISSYL,ONEM,ICORE(I010),1,ICORE(I000),1)
C
        CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(ai,bj) = L(AI,en) * X(bj,EN)
C
        CALL XGEMM('N','T',DISSYL,DISSYX,NUMDSX,HALF,ICORE(I000),DISSYL,
     &             ICORE(I010),DISSYX,ZILCH,ICORE(I020),DISSYZ)
        CALL PUTLST(ICORE(I020),1,NUMDSZ,1,IRREPL,LISTZ)
30     CONTINUE 
C
C NOW DEAL WITH EXCHANGE CONTRIBUTION
C
       DO 130 IRREPR=1,NIRREP
        LISTL=439
        LISTZ=43
        LISTX=428
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPR,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPL,ISYTYP(2,LISTX))
        DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
        I010=I000+IINTFP*NUMDSL*DISSYL 
        I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPL,LISTX)
C
C FORM PRODUCT Y(aJ,bI) = X(aJ,eN) * L(En,bI)
C
        CALL XGEMM('N','N',DISSYX,NUMDSL,NUMDSX,ONE,ICORE(I010),DISSYX,
     &             ICORE(I000),DISSYL,ZILCH,ICORE(I020),DISSYZ)      
        CALL PUTLST(ICORE(I020),1,NUMDSZ,1,IRREPR,LISTZ)
130    CONTINUE 
C
C NOW PROCESS INCREMENTS
C
       ISIZE=ISYMSZ(ISYTYP(1,42),ISYTYP(2,42))
       I000=1
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
C
C READ Y(aJ,Bi) FROM LIST 43 AND FORM
C
C        X(aJ,Bi) = - Y(aJ,Bi) - 1/2 Y(BJ,ai)
C
       CALL GETALL(ICORE(I000),ISIZE,1,43)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,1),
     &             VRT(1,1),POP(1,1),ICORE(I020),1,'1432')
       CALL SAXPY(ISIZE,HALF,ICORE(I010),1,ICORE(I000),1)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,1),
     &             VRT(1,1),POP(1,1),ICORE(I020),1,'1432')
       CALL GETALL(ICORE(I000),ISIZE,1,42)
       CALL SAXPY(ISIZE,ONEM,ICORE(I010),1,ICORE(I000),1)
       IOFF=I000
       DO 200 IRREP=1,NIRREP
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,42))
        NUMDSZ=IRPDPD(IRREP,ISYTYP(2,42))
        CALL MPMT(ICORE(IOFF),NUMDSZ)
        IOFF=IOFF+IINTFP*NUMDSZ*NUMDSZ
200    CONTINUE        
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,1),
     &             VRT(1,1),POP(1,1),ICORE(I020),1,'1324')
       CALL GETALL(ICORE(I000),ISIZE,1,63)
       CALL SAXPY(ISIZE,ONEM,ICORE(I010),1,ICORE(I000),1)
       CALL PUTALL(ICORE(I000),ISIZE,1,63)
C
      ELSE
C
C UHF.  
C
C AAAA FIRST : Y(AI,BJ) = L(AI,EM)*X(BJ,EM) + L(AI,em)*X(BJ,em)
C
       DO 100 ISPIN=1,2
        LISTZ=39+ISPIN
        DO 110 IRREPR=1,NIRREP
         IRREPL=DIRPRD(IRREPR,IRREPX)
         DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
         NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
         I0Z=1
         I000=I0Z+IINTFP*NUMDSZ*DISSYZ
C
         LISTL=433+ISPIN
         LISTX=423+ISPIN
         IRREPL=DIRPRD(IRREPR,IRREPX)
         DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
         NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
         DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
         NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
         I010=I000+IINTFP*NUMDSL*DISSYL 
         I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
         CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
         CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(AI,BJ) = L(AI,EM)*X(BJ,EM)
C
         CALL XGEMM('N','T',DISSYL,DISSYX,NUMDSX,ONEM,ICORE(I000),
     &              DISSYL,ICORE(I010),DISSYX,ZILCH,ICORE(I0Z),DISSYZ)
C
         LISTL=438-ISPIN
         LISTX=425+ISPIN
         IRREPL=DIRPRD(IRREPR,IRREPX)
         DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
         NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
         DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
         NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
         I010=I000+IINTFP*NUMDSL*DISSYL 
         I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
         CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
         CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(AI,BJ) = L(AI,em)*X(BJ,em)
C
         CALL XGEMM('N','T',DISSYL,DISSYX,NUMDSX,ONE,ICORE(I000),
     &              DISSYL,ICORE(I010),DISSYX,ONE,ICORE(I0Z),DISSYZ)
C
         CALL PUTLST(ICORE(I0Z),1,NUMDSZ,1,IRREPL,LISTZ)
110     CONTINUE 
C
        ISIZE=ISYMSZ(ISYTYP(1,LISTZ),ISYTYP(1,LISTZ))
        I000=1
        I010=I000+IINTFP*ISIZE
        I020=I010+IINTFP*ISIZE
        CALL GETALL(ICORE(I000),ISIZE,1,LISTZ)
        CALL DSST03I(ICORE(I000),ICORE(I010),ISIZE,ISIZE,ICORE(I020),
     &               SPCASE(ISPIN),1)
        CALL GETALL(ICORE(I000),ISIZE,1,60+ISPIN)
        CALL SAXPY (ISIZE,ONEM,ICORE(I010),1,ICORE(I000),1)
        CALL PUTALL(ICORE(I000),ISIZE,1,60+ISPIN)
C
100    CONTINUE
C
C  ABAB SPIN CASE
C
       DO 120 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
C
        LISTZ=42
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        I0Z=1
        I000=I0Z+IINTFP*NUMDSZ*DISSYZ
C
C TERM I : L(AI,EN)*X(bj,EN)
C
        LISTL=434
        LISTX=427
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
        I010=I000+IINTFP*NUMDSL*DISSYL 
        I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(AI,bj) = L(AI,EN)*X(bj,EN)
C
        CALL XGEMM('N','T',DISSYL,DISSYX,NUMDSX,ONEM,ICORE(I000),DISSYL,
     &            ICORE(I010),DISSYX,ZILCH,ICORE(I0Z),DISSYZ)
C
C TERM II : L(AI,en)*X(bj,en)
C
        LISTL=437
        LISTX=425
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
        I010=I000+IINTFP*NUMDSL*DISSYL 
        I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(AI,bj) = L(AI,en)*X(bj,en)
C
        CALL XGEMM('N','T',DISSYL,DISSYX,NUMDSX,ONE,ICORE(I000),DISSYL,
     &            ICORE(I010),DISSYX,ONE,ICORE(I0Z),DISSYZ)
C
C TERM III : X(AI,EN)*L(EN,bj)
C
        LISTL=437
        LISTX=424
        DISSYL=IRPDPD(IRREPR,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPL,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
        I010=I000+IINTFP*NUMDSL*DISSYL 
        I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPL,LISTL)
        CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(AI,bj) = X(AI,EN)*L(EN,bj)
C
        CALL XGEMM('N','N',DISSYZ,NUMDSZ,DISSYL,ONE,ICORE(I010),DISSYX,
     &            ICORE(I000),DISSYL,ONE,ICORE(I0Z),DISSYZ)
C
C TERM IV : X(AI,en)*L(en,bj)
C
        LISTL=435
        LISTX=426
        DISSYL=IRPDPD(IRREPR,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPL,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
        I010=I000+IINTFP*NUMDSL*DISSYL 
        I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPL,LISTL)
        CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(AI,bj) = X(AI,en)*L(en,bj)
C
        CALL XGEMM('N','N',DISSYZ,NUMDSZ,DISSYL,ONEM,ICORE(I010),DISSYX,
     &            ICORE(I000),DISSYL,ONE,ICORE(I0Z),DISSYZ)
C
        CALL PUTLST(ICORE(I0Z),1,NUMDSZ,1,IRREPL,LISTZ)
C
        LISTZ=43
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        I0Z=1
        I000=I0Z+IINTFP*DISSYZ*NUMDSZ
       
C
C TERM V : L(Aj,eN)*X(bI,eN)
C
        LISTL=439
        LISTX=429
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
        I010=I000+IINTFP*NUMDSL*DISSYL 
        I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(Aj,bI) = L(Aj,eN)*X(bI,eN)
C
        CALL XGEMM('N','T',DISSYL,DISSYX,NUMDSX,ONE,ICORE(I000),DISSYL,
     &            ICORE(I010),DISSYX,ZILCH,ICORE(I0Z),DISSYZ)
C
C TERM VI : X(Aj,En)*L(bI,En)
C
        LISTL=438
        LISTX=428
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYX=IRPDPD(IRREPL,ISYTYP(1,LISTX))
        NUMDSX=IRPDPD(IRREPR,ISYTYP(2,LISTX))
        I010=I000+IINTFP*NUMDSL*DISSYL 
        I020=I010+IINTFP*MAX(NUMDSL*DISSYL,NUMDSX*DISSYX)
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL GETLST(ICORE(I010),1,NUMDSX,1,IRREPR,LISTX)
C
C FORM PRODUCT Y(Aj,bI) = X(Aj,En)*L(bI,En)
C
        CALL XGEMM('N','T',DISSYZ,NUMDSZ,NUMDSX,ONE,ICORE(I010),DISSYX,
     &             ICORE(I000),DISSYL,ONE,ICORE(I0Z),DISSYZ)
C
        CALL PUTLST(ICORE(I0Z),1,NUMDSZ,1,IRREPL,LISTZ)
120    CONTINUE
C
C RESORT PRODUCTS
C
       ISIZE=ISYMSZ(ISYTYP(1,42),ISYTYP(2,42))
       I000=1
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
       CALL GETALL(ICORE(I000),ISIZE,1,43)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,2),
     &             VRT(1,2),POP(1,1),ICORE(I020),1,'1432')
       CALL GETALL(ICORE(I000),ISIZE,1,42)
       CALL SAXPY (ISIZE,ONEM,ICORE(I010),1,ICORE(I000),1)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,1),
     &             VRT(1,2),POP(1,2),ICORE(I020),1,'1324')
       CALL GETALL(ICORE(I000),ISIZE,1,63)
       CALL SAXPY (ISIZE,ONEM,ICORE(I010),1,ICORE(I000),1)
       CALL PUTALL(ICORE(I000),ISIZE,1,63)
C
      ENDIF
C
      RETURN
      END
