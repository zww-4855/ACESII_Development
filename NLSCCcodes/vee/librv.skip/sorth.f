
      SUBROUTINE SORTH(ICORE,MAXCOR,IUHF,LISTW,IRREPX)
C
C   THIS ROUTINE DRIVES THE SORT OF THE H(IA,JB) INTERMEDIATES.
C
C   ALL H ARE RESORTED IN THE FOLLOWING WAY :
C
C    H(IA,JB) :   A,I ; B,J ---->     A,J ; B,I
C
CEND
C
C  CODED SEPTEMBER/93  JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL CHANGE
      INTEGER DIRPRD,DISSYT
      CHARACTER*4 SPTYP,SPAAAA(2),SPABAB(2),SPABBA(2)
      DIMENSION ICORE(MAXCOR)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA SPAAAA /'AAAA','BBBB'/,SPABAB /'ABAB','BABA'/
      DATA SPABBA /'AABB','BBAA'/ 
C    
C  LOOP OVER ISPINS TO PERFORM SORTS, WRITE ALL H4-AMPLITUDES BACK TO
C  THE SAME LIST
C
C
      DO 1000 ISPIN=1,IUHF+1
C
C  DO FIRST AAAA AND BBBB (UHF ONLY)
C
c      IF(IUHF.EQ.1) THEN
C
      LISTH=LISTW-1+ISPIN
      NSIZE=IDSYMSZ(IRREPX,ISYTYP(1,LISTH),ISYTYP(2,LISTH))
      I001=1
      I002=I001+IINTFP*NSIZE
      I003=I002+IINTFP*NSIZE
      I004=I003+2*NOCCO(ISPIN)*NVRTO(ISPIN)*IINTFP
      IF(MAXCOR.LT.I004) THEN
       CALL INSMEM('SORTH',I004,MAXCOR)
      ENDIF
      CALL GETALL(ICORE(I001),NSIZE,IRREPX,LISTH)
      call checksum('shaaaa',icore(i001),nsize)
      SPTYP=SPAAAA(ISPIN)
      CALL GSSTRNG(ICORE(I001),ICORE(I002),NSIZE,NSIZE,ICORE(I003),
     &            SPTYP,IRREPX)
      CALL PUTALL(ICORE(I002),NSIZE,IRREPX,LISTH)
C
c      ENDIF
C
C  ABAB CASES 
C
      LISTH=LISTW+3+ISPIN
      NSIZE=IDSYMSZ(IRREPX,ISYTYP(1,LISTH),ISYTYP(2,LISTH))
      I001=1
      I002=I001+IINTFP*NSIZE
      I003=I002+IINTFP*NSIZE
      I004=I003+2*IINTFP*NVRTO(ISPIN)*NOCCO(3-ISPIN)
      IF(MAXCOR.LT.I004) THEN
       CALL INSMEM('SORTH',I004,MAXCOR)
      ENDIF
      CALL GETALL(ICORE(I001),NSIZE,IRREPX,LISTH)
      call checksum('shabab',icore(i001),nsize)
      SPTYP=SPABAB(ISPIN)
      CALL GSSTRNG(ICORE(I001),ICORE(I002),NSIZE,NSIZE,ICORE(I003),
     &            SPTYP,IRREPX)
      CALL PUTALL(ICORE(I002),NSIZE,IRREPX,LISTH)
C
C  DO ABBA CASES
C
      LISTH=LISTW+1+ISPIN
      NSIZE=IDSYMSZ(IRREPX,ISYTYP(1,LISTH),ISYTYP(2,LISTH))
      I001=1
      I002=I001+IINTFP*NSIZE
      I003=I002+IINTFP*NSIZE
      I004=I003+IINTFP*(NVRTO(ISPIN)+NVRTO(3-ISPIN))*
     &                 (NOCCO(ISPIN)+NOCCO(3-ISPIN))
      IF(MAXCOR.LT.I004) THEN
       CALL INSMEM('SORTH',I004,MAXCOR)
      ENDIF
      CALL GETALL(ICORE(I001),NSIZE,IRREPX,LISTH)
      call checksum('shabba',icore(i001),nsize)
      SPTYP=SPABBA(ISPIN)
      CALL GSSTRNG(ICORE(I001),ICORE(I002),NSIZE,NSIZE,ICORE(I003),
     &            SPTYP,IRREPX)
      LISTN=20+ISPIN
      CHANGE=.TRUE.
      CALL NEWTYP2(IRREPX,LISTH,ISYTYP(1,LISTN),ISYTYP(2,LISTN),CHANGE)
      IOFF=I002
      DO 110 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPX,IRREPR)
       NUMSYT=IRPDPD(IRREPR,ISYTYP(2,LISTN))
       DISSYT=IRPDPD(IRREPL,ISYTYP(1,LISTN))
       CALL PUTLST(ICORE(IOFF),1,NUMSYT,1,IRREPR,LISTH)
       IOFF=IOFF+IINTFP*NUMSYT*DISSYT
110   CONTINUE 
1000  CONTINUE
      RETURN
      END
