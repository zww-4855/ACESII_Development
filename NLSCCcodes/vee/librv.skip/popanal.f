      SUBROUTINE POPANAL(NAO,NMO,DMO,DAO,SCR,MAXCOR,ISPIN)
C
C THIS ROUTINE CARRIES OUT A POPULATION ANALYSIS ON THE INPUT
C ONE-PARTICLE DENSITY MATRIX DMO.  IT IS ASSUMED THAT DMO IS
C EXPRESSED IN THE MOLECULAR ORBITAL BASIS ON INPUT
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (MXATMS=150)
      CHARACTER*5 SPTYPE(2)
      CHARACTER*1 ORBTYP(7)
      DIMENSION DMO(NMO,NMO),DAO(NAO,NAO),SCR(MAXCOR),
     &          ISCR(100,3),IJUNK(MXATMS)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,ZILCH/1.0D0,0.0D0/
      DATA ORBTYP/'S','P','D','F','G','H','I'/
      DATA SPTYPE/'alpha','beta '/
C
      WRITE(6,9999)SPTYPE(ISPIN)
C
      IONE=1
      CALL GETREC(20,'JOBARC','NAOBASFN',IONE,NAOTOT)
      CALL GETREC(20,'JOBARC','NATOMS  ',IONE,NATOMS)
      CALL GETREC(20,'JOBARC','MAP2ZMAT',NATOMS,IJUNK)
      CALL GETREC(20,'JOBARC','CENTERBF',NAOTOT,ISCR(1,1))
      CALL GETREC(20,'JOBARC','ANGMOMBF',NAOTOT,ISCR(1,2))
C
C TRANSFORM DENSITY MATRIX TO THE SYMMETRY ADAPTED AO BASIS
C
      I000=1
      I010=I000+NAOTOT*NAOTOT
      I020=I010+NAOTOT*NAOTOT
      CALL MO2AO3(DMO,DAO,SCR(I000),SCR(I010),NAO,NMO,
     &            ISPIN)
C
C EVALUATE D*S
C
      CALL GETREC(20,'JOBARC','AOOVRLAP',IINTFP*NAO*NAO,SCR)
      CALL XGEMM ('N','N',NAO,NAO,NAO,ONE,DAO,NAO,SCR,NAO,
     &            ZILCH,SCR(I010),NAO)
C
C TRANSFORM THIS TO THE FULL BASIS
C
      CALL GETREC(20,'JOBARC','AO2SOINV',IINTFP*NAO*NAOTOT,SCR(I000))
      CALL XGEMM ('N','N',NAO,NAOTOT,NAO,ONE,SCR(I010),NAO,SCR(I000),
     &            NAO,ZILCH,SCR(I020),NAO)
      CALL GETREC(20,'JOBARC','AO2SO   ',IINTFP*NAO*NAOTOT,SCR(I000))
      CALL XGEMM ('N','N',NAOTOT,NAOTOT,NAO,ONE,SCR(I000),NAOTOT,
     &            SCR(I020),NAO,ZILCH,SCR(I010),NAOTOT)
C
C COPY DIAGONAL ELEMENTS TO SCRATCH VECTOR AND THEN TRANSFORM IT
C TO THE NON-SYMMETRY ADAPTED AO BASIS
C
      CALL SCOPY(NAOTOT,SCR(I010),NAOTOT+1,SCR,1)
C
      WRITE(6,1005)
      WRITE(6,1002)
      WRITE(6,1000)
      WRITE(6,1002)
C
      DO 101 I=1,NAOTOT
       WRITE(6,1001)IJUNK(ISCR(I,1)),ORBTYP(1+ISCR(I,2)),SCR(I)        
101   CONTINUE
      WRITE(6,1002)
C
C CALCULATE POPULATIONS PER ATOM
C
      WRITE(6,1004)
      WRITE(6,1002)
      WRITE(6,1000)
      WRITE(6,1002)
      DO 102 IATOM=1,NATOMS
       CALL WHENEQ(NAOTOT,ISCR(1,1),1,IATOM,ISCR(1,3),NMATCH)
       IF(NMATCH.NE.0)THEN
        CALL GATHER(NMATCH,SCR(I010),SCR(I000),ISCR(1,3))
        X=SSUM(NMATCH,SCR(I010),1)
        WRITE(6,1003)IJUNK(IATOM),X
       ENDIF
102   CONTINUE
      WRITE(6,1002)
C
      RETURN
1000  FORMAT(T6,'Z-matrix',/,T7,'Center',T23,'Function',
     &       T41,'Population')
1001  FORMAT(T8,I3,T27,A,T40,F12.8)
1002  FORMAT(' ',55('-'))
1003  FORMAT(T8,I3,T40,F12.8)
1004  FORMAT(T13,'Atomic population analysis')
1005  FORMAT(T13,'Population analysis by orbital')
9999  FORMAT(T3,'@POPANAL2-I, Population analysis of ',A5,
     &          ' difference density.')
      END
