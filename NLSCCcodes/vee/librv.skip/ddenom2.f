      SUBROUTINE DDENOM2(ICORE,MAXCOR,IUHF,IRREPX,LISTD0)
C
C THIS ROUTINE ADDS THE TWO-PARTICLE CONTRIBUTIONS TO THE DIAGONAL
C PARTS OF HBAR
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM
      DIMENSION ICORE(MAXCOR),I0IJ(3),I0AB(3)
      DIMENSION LENIJ(3),LENAB(3)
      DIMENSION IOFFIJ(8),IOFFAB(8),IOFFIA1(8),IOFFIA2(8)
      DIMENSION IOFFIA3(8),IOFFIA4(8)
      CHARACTER*8 LABIJ(3),LABAB(3),LABIA1,LABIA2,LABIA3,LABIA4
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),IDX(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/INFO/NOCCO(2),NVRTO(2)
C
      DATA LABIJ /'WDIAG51 ','WDIAG52 ','WDIAG53 '/
      DATA LABAB /'WDIAG31 ','WDIAG32 ','WDIAG33 '/
      DATA LABIA1 /'WDIAG54 '/
      DATA LABIA2 /'WDIAG55 '/
      DATA LABIA3 /'WDIAG58 '/
      DATA LABIA4 /'WDIAG59 '/
      DATA ONE,ONEM/1.0D0,-1.0D0/
C
      NNM1O2(I)=(I*(I-1))/2
C
      IOFF=1
      DO 1 ISPIN=3,3-2*IUHF,-1
       IF(ISPIN.LE.2)THEN
        LENIJ(ISPIN)=NNM1O2(NOCCO(ISPIN))
        LENAB(ISPIN)=NNM1O2(NVRTO(ISPIN))
       ELSE
        LENIJ(ISPIN)=NOCCO(1)*NOCCO(2)
        LENAB(ISPIN)=NVRTO(1)*NVRTO(2)
       ENDIF
       LENIA1=NVRTO(1)*NOCCO(1)
       LENIA2=NVRTO(2)*NOCCO(2)
       LENIA3=NVRTO(1)*NOCCO(2)
       LENIA4=NVRTO(2)*NOCCO(1)
       I0IJ(ISPIN) =IOFF
       I0AB(ISPIN) =I0IJ(ISPIN)+IINTFP*LENIJ(ISPIN)
       IF(ISPIN.EQ.3)THEN
        I0IA1=I0AB(ISPIN)+IINTFP*LENAB(ISPIN)
        I0IA2=I0IA1+IUHF*IINTFP*LENIA1
        I0IA3=I0IA2+IINTFP*LENIA2
        I0IA4=I0IA3+IUHF*IINTFP*LENIA3
        IOFF=I0IA4+IINTFP*LENIA4
        CALL GETREC(20,'JOBARC',LABIA1,LENIA1*IINTFP,ICORE(I0IA1))
        CALL GETREC(20,'JOBARC',LABIA3,LENIA3*IINTFP,ICORE(I0IA3))
        IF(IUHF.NE.0)THEN
         CALL GETREC(20,'JOBARC',LABIA2,LENIA2*IINTFP,ICORE(I0IA2))
         CALL GETREC(20,'JOBARC',LABIA4,LENIA4*IINTFP,ICORE(I0IA4))
        ENDIF
       ELSE
        IOFF=I0AB(ISPIN)+IINTFP*LENAB(ISPIN)
       ENDIF
       CALL GETREC(20,'JOBARC',LABIJ(ISPIN),LENIJ(ISPIN)*IINTFP,
     &             ICORE(I0IJ(ISPIN)))
       CALL GETREC(20,'JOBARC',LABAB(ISPIN),LENAB(ISPIN)*IINTFP,
     &             ICORE(I0AB(ISPIN)))
1     CONTINUE
C
      DO 5 ISPIN=3,3-2*IUHF,-1
       LISTD=LISTD0+ISPIN
       IF(ISPIN.LE.2)THEN
        ITYPEIJ=2+ISPIN
        ITYPEAB=ISPIN
       ELSE
        ITYPEIJ=14
        ITYPEAB=13
       ENDIF
       ITYPEIA1=9
       ITYPEIA2=10
       ITYPEIA3=11
       ITYPEIA4=12
       INDIJ=I0IJ(ISPIN)
       INDAB=I0AB(ISPIN)
       INDIA1=I0IA1
       INDIA2=I0IA2
       INDIA3=I0IA3
       INDIA4=I0IA4
       DO 7 IRREP=1,NIRREP
        IOFFIJ(IRREP)=INDIJ
        IOFFAB(IRREP)=INDAB
        IOFFIA1(IRREP)=INDIA1
        IOFFIA2(IRREP)=INDIA2
        IOFFIA3(IRREP)=INDIA3
        IOFFIA4(IRREP)=INDIA4
        INDIJ=INDIJ+IINTFP*IRPDPD(IRREP,ITYPEIJ)
        INDAB=INDAB+IINTFP*IRPDPD(IRREP,ITYPEAB)
        INDIA1=INDIA1+IINTFP*IRPDPD(IRREP,ITYPEIA1)
        INDIA2=INDIA2+IINTFP*IRPDPD(IRREP,ITYPEIA2)
        INDIA3=INDIA3+IINTFP*IRPDPD(IRREP,ITYPEIA3)
        INDIA4=INDIA4+IINTFP*IRPDPD(IRREP,ITYPEIA4)
7      CONTINUE

C
C DO IJIJ AND ABAB FIRST
C
       DO 10 IRREPIJ=1,NIRREP
        IRREPAB=DIRPRD(IRREPIJ,IRREPX)
        DISSYD=IRPDPD(IRREPAB,ISYTYP(1,LISTD))
        NUMDSD=IRPDPD(IRREPIJ,ISYTYP(2,LISTD))
        I000=IOFF
        I010=I000+DISSYD*NUMDSD
        CALL GETLST(ICORE(I000),1,NUMDSD,1,IRREPIJ,LISTD)
        IOFFWIJ=IOFFIJ(IRREPIJ)
        IOFFWAB=IOFFAB(IRREPAB)
        IOFFD=I000
        DO 20 AB=1,DISSYD
         CALL SAXPY(NUMDSD,ONEM,ICORE(IOFFWIJ),1,ICORE(IOFFD),DISSYD)
         IOFFD=IOFFD+IINTFP
20      CONTINUE
        IOFFD=I000
        DO 21 IJ=1,NUMDSD
         CALL SAXPY(DISSYD,ONEM,ICORE(IOFFWAB),1,ICORE(IOFFD),1)
         IOFFD=IOFFD+IINTFP*DISSYD
21      CONTINUE
C
        CALL PUTLST(ICORE(I000),1,NUMDSD,1,IRREPIJ,LISTD)
C
10     CONTINUE
C
C NOW RESORT DENOMINATORS AND ADD IN RING TERMS
C
       IF(ISPIN.EQ.3)THEN
        ISIZE=IDSYMSZ(IRREPX,ISYTYP(1,LISTD),ISYTYP(2,LISTD))
        I000=IOFF
        I010=I000+IINTFP*ISIZE
        I020=I010+IINTFP*ISIZE
        CALL GETALL(ICORE(I000),ISIZE,IRREPX,LISTD)
        CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),VRT(1,2),
     &              POP(1,1),POP(1,2),ICORE(I020),IRREPX,'1324')
C
C AIbj
C
        IOFFD0=I010
        DO 50 IRREPBJ=1,NIRREP
         IRREPAI=DIRPRD(IRREPBJ,IRREPX)
         DISSYD=IRPDPD(IRREPAI,ITYPEIA1)
         NUMDSD=IRPDPD(IRREPBJ,ITYPEIA2)
         IOFFWAI=IOFFIA1(IRREPAI)
         IOFFWBJ=IOFFIA2(IRREPBJ)
         IOFFD=IOFFD0
         DO 51 AI=1,DISSYD
          CALL SAXPY(NUMDSD,ONE,ICORE(IOFFWBJ),1,ICORE(IOFFD),DISSYD)
          IOFFD=IOFFD+IINTFP
51       CONTINUE
         IOFFD=IOFFD0
         DO 52 BJ=1,NUMDSD
          CALL SAXPY(DISSYD,ONE,ICORE(IOFFWAI),1,ICORE(IOFFD),1)
          IOFFD=IOFFD+IINTFP*DISSYD
52       CONTINUE
         IOFFD0=IOFFD0+IINTFP*DISSYD*NUMDSD
50      CONTINUE
C          
        CALL SSTGEN(ICORE(I010),ICORE(I000),ISIZE,VRT(1,1),POP(1,1),
     &              VRT(1,2),POP(1,2),ICORE(I020),IRREPX,'1432')
C
C AjbI
C
        IOFFD0=I000
        DO 60 IRREPBI=1,NIRREP
         IRREPAJ=DIRPRD(IRREPBI,IRREPX)
         DISSYD=IRPDPD(IRREPAJ,ITYPEIA3)
         NUMDSD=IRPDPD(IRREPBI,ITYPEIA4)
         IOFFWAJ=IOFFIA3(IRREPAJ)
         IOFFWBI=IOFFIA4(IRREPBI)
         IOFFD=IOFFD0
         DO 61 AJ=1,DISSYD
          CALL SAXPY(NUMDSD,ONE,ICORE(IOFFWBI),1,ICORE(IOFFD),DISSYD)
          IOFFD=IOFFD+IINTFP
61       CONTINUE
         IOFFD=IOFFD0
         DO 62 BI=1,NUMDSD
          CALL SAXPY(DISSYD,ONE,ICORE(IOFFWAJ),1,ICORE(IOFFD),1)
          IOFFD=IOFFD+IINTFP*DISSYD
62       CONTINUE
         IOFFD0=IOFFD0+IINTFP*DISSYD*NUMDSD
60      CONTINUE
C          
        CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,1),POP(1,2),
     &              VRT(1,2),POP(1,1),ICORE(I020),IRREPX,'1342')
        CALL PUTALL(ICORE(I010),ISIZE,IRREPX,LISTD)
C
       ELSEIF(ISPIN.LE.2)THEN
C
C EXPAND D(A<B,I<J) TO D(AB,IJ) FIRST
C
        ISIZE=IDSYMSZ(IRREPX,18+ISPIN,20+ISPIN)
        I000=IOFF
        I010=I000+IINTFP*ISIZE
        I020=I010+IINTFP*ISIZE
        ID=I000
        DO 70 IRREPIJ=1,NIRREP
         IRREPAB=DIRPRD(IRREPIJ,IRREPX)
         DISSYD=IRPDPD(IRREPAB,18+ISPIN)
         NUMDSD=IRPDPD(IRREPIJ,20+ISPIN)
         DISSYDX=IRPDPD(IRREPAB,ISYTYP(1,LISTD))
         NUMDSDX=IRPDPD(IRREPIJ,ISYTYP(2,LISTD))
         CALL GETLST(ICORE(ID),1,NUMDSDX,1,IRREPIJ,LISTD)
         CALL SYMEXP(IRREPIJ,POP(1,ISPIN),DISSYDX,ICORE(ID)) 
         CALL SYMEXP2(IRREPAB,VRT(1,ISPIN),DISSYD,DISSYDX,NUMDSD,
     &                ICORE(ID),ICORE(ID)) 
         ID=ID+IINTFP*DISSYD*NUMDSD
70      CONTINUE
C
        CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,VRT(1,ISPIN),
     &              VRT(1,ISPIN),POP(1,ISPIN),POP(1,ISPIN),
     &              ICORE(I020),IRREPX,'1324')
C
C AIBJ
C
        IOFFD0=I010
        DO 80 IRREPBJ=1,NIRREP
         IRREPAI=DIRPRD(IRREPBJ,IRREPX)
         IF(ISPIN.EQ.1)THEN
          DISSYD=IRPDPD(IRREPAI,ITYPEIA1)
          NUMDSD=IRPDPD(IRREPBJ,ITYPEIA1)
          IOFFWAI=IOFFIA1(IRREPAI)
          IOFFWBJ=IOFFIA1(IRREPBJ)
         ELSE
          DISSYD=IRPDPD(IRREPAI,ITYPEIA2)
          NUMDSD=IRPDPD(IRREPBJ,ITYPEIA2)
          IOFFWAI=IOFFIA2(IRREPAI)
          IOFFWBJ=IOFFIA2(IRREPBJ)
         ENDIF
         IOFFD=IOFFD0
         DO 81 AI=1,DISSYD
          CALL SAXPY(NUMDSD,ONE,ICORE(IOFFWBJ),1,ICORE(IOFFD),DISSYD)
          IOFFD=IOFFD+IINTFP
81       CONTINUE
         IOFFD=IOFFD0
         DO 82 BJ=1,NUMDSD
          CALL SAXPY(DISSYD,ONE,ICORE(IOFFWAI),1,ICORE(IOFFD),1)
          IOFFD=IOFFD+IINTFP*DISSYD
82       CONTINUE
         IOFFD0=IOFFD0+IINTFP*DISSYD*NUMDSD
80      CONTINUE
C          
        CALL SSTGEN(ICORE(I010),ICORE(I000),ISIZE,
     &              VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     &              ICORE(I020),IRREPX,'1432')
C
C AJBI
C
        IOFFD0=I000
        DO 90 IRREPBI=1,NIRREP
         IRREPAJ=DIRPRD(IRREPBI,IRREPX)
         IF(ISPIN.EQ.1)THEN
          DISSYD=IRPDPD(IRREPAJ,ITYPEIA1)
          NUMDSD=IRPDPD(IRREPBI,ITYPEIA1)
          IOFFWAJ=IOFFIA1(IRREPAJ)
          IOFFWBI=IOFFIA1(IRREPBI)
         ELSE
          DISSYD=IRPDPD(IRREPAJ,ITYPEIA2)
          NUMDSD=IRPDPD(IRREPBI,ITYPEIA2)
          IOFFWAJ=IOFFIA2(IRREPAJ)
          IOFFWBI=IOFFIA2(IRREPBI)
         ENDIF
         IOFFD=IOFFD0
         DO 91 AJ=1,DISSYD
          CALL SAXPY(NUMDSD,ONE,ICORE(IOFFWBI),1,ICORE(IOFFD),DISSYD)
          IOFFD=IOFFD+IINTFP
91       CONTINUE
         IOFFD=IOFFD0
         DO 92 BI=1,NUMDSD
          CALL SAXPY(DISSYD,ONE,ICORE(IOFFWAJ),1,ICORE(IOFFD),1)
          IOFFD=IOFFD+IINTFP*DISSYD
92       CONTINUE
         IOFFD0=IOFFD0+IINTFP*DISSYD*NUMDSD
90      CONTINUE
C          
        CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,
     &              VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     &              ICORE(I020),IRREPX,'1342')
C
C NOW WE HAVE TO SQUEEZE IT BACK TO A<B,I<J
C
        ID=I010
        DO 95 IRREPIJ=1,NIRREP
         IRREPAB=DIRPRD(IRREPIJ,IRREPX)
         DISSYD=IRPDPD(IRREPAB,18+ISPIN)
         NUMDSD=IRPDPD(IRREPIJ,20+ISPIN)
         DISSYDX=IRPDPD(IRREPAB,ISYTYP(1,LISTD))
         NUMDSDX=IRPDPD(IRREPIJ,ISYTYP(2,LISTD))
         CALL SQSYM(IRREPAB,VRT(1,ISPIN),DISSYDX,DISSYD,NUMDSD,
     &              ICORE(I000),ICORE(ID))
         CALL TRANSP(ICORE(I000),ICORE(ID),NUMDSD,DISSYDX)
         CALL SQSYM(IRREPIJ,POP(1,ISPIN),NUMDSDX,NUMDSD,DISSYDX,
     &              ICORE(I000),ICORE(ID))
         CALL TRANSP(ICORE(I000),ICORE(ID),DISSYDX,NUMDSDX)
         CALL PUTLST(ICORE(ID),1,NUMDSDX,1,IRREPIJ,LISTD) 
         ID=ID+IINTFP*DISSYD*NUMDSD
95      CONTINUE
C
       ENDIF
C
5     CONTINUE
C
      RETURN
      END
