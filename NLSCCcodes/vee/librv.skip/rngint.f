      SUBROUTINE RNGINT(ICORE,MAXCOR,IUHF,IRREPX)
C
C THIS ROUTINE CALCULATES THE FOLLOWING RING INTERMEDIATES FOR
C EOM-CC METHODS:
C
C  R(AI,EM)*L(EM,BJ) + R(AI,em)*L(em,BJ) = Z(AI,BJ)  -> LIST 134 [UHF ONLY]
C  R(ai,em)*L(em,bj) + R(ai,EM)*L(EM,bj) = Z(ai,bj)  -> LIST 135 [UHF ONLY]
C  R(AI,em)*L(em,bj) + R(AI,EM)*L(EM,bj) = Z(AI,bj)  -> LIST 137
C  R(ai,EM)*L(EM,BJ) + R(ai,em)*L(em,BJ) = Z(ai,BJ)  -> LIST 136 [UHF ONLY]
C  R(Ai,eM)*L(eM,Bj)                     = Z(Ai,Bj)  -> LIST 25
C  R(aI,Em)*L(Em,bJ)                     = Z(aI,bJ)  -> LIST 26  [UHF ONLY]
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      DIMENSION ICORE(MAXCOR)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
C
      DATA ONE,ONEM,ZILCH /1.0D0,-1.0D0,0.0D0/
C
C PUT RESORTED L VECTOR ON 434-439, RESORTED R VECTOR ON 454-459
C
      CALL RESORT(ICORE,MAXCOR,IUHF,IRREPX,444,434)
      CALL RESORT(ICORE,MAXCOR,IUHF,IRREPX,461,454)
      IF(IUHF.NE.0)THEN
C
C DO AAAA AND BBBB SPIN CASES
C
       DO 10 ISPIN=1,2
        DO 11 IRREPR=1,NIRREP 
         IRREPL=DIRPRD(IRREPR,IRREPX)
         LISTZ=133+ISPIN
         DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
         NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
         I000=1
         I010=I000+IINTFP*DISSYZ*NUMDSZ
C
C R(AI,EM)*L(EM,BJ)  and R(ai,em)*L(em,bj) CONTRACTIONS
C
         LISTL=433+ISPIN
         LISTR=453+ISPIN
         DISSYR=IRPDPD(IRREPR,ISYTYP(1,LISTR)) 
         NUMDSR=IRPDPD(IRREPL,ISYTYP(2,LISTR)) 
         DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL)) 
         NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL)) 
         I020=I010+IINTFP*DISSYR*NUMDSR
         I030=I020+IINTFP*DISSYL*NUMDSL
         CALL GETLST(ICORE(I010),1,NUMDSR,1,IRREPL,LISTR)
         CALL GETLST(ICORE(I020),1,NUMDSL,1,IRREPR,LISTL)
         CALL XGEMM('N','N',DISSYZ,NUMDSZ,DISSYL,ONE,ICORE(I010),
     &              DISSYR,ICORE(I020),DISSYL,ZILCH,ICORE(I000),DISSYZ)
C
C R(AI,em)*L(em,BJ)  and R(ai,EM)*L(EM,bj) CONTRACTIONS
C
         LISTL=435+ISPIN
         LISTR=458-ISPIN
         DISSYR=IRPDPD(IRREPR,ISYTYP(1,LISTR)) 
         NUMDSR=IRPDPD(IRREPL,ISYTYP(2,LISTR)) 
         DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL)) 
         NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL)) 
         I020=I010+IINTFP*DISSYR*NUMDSR
         I030=I020+IINTFP*DISSYL*NUMDSL
         CALL GETLST(ICORE(I010),1,NUMDSR,1,IRREPL,LISTR)
         CALL GETLST(ICORE(I020),1,NUMDSL,1,IRREPR,LISTL)
         CALL XGEMM('N','N',DISSYZ,NUMDSZ,DISSYL,ONE,ICORE(I010),
     &              DISSYR,ICORE(I020),DISSYL,ONE,ICORE(I000),DISSYZ)
C
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
11      CONTINUE
10     CONTINUE
C
      ENDIF
C 
C  R(AI,em)*L(em,bj) + R(AI,EM)*L(EM,bj) = Z(AI,bj)  -> LIST 137
C  R(ai,EM)*L(EM,BJ) + R(ai,em)*L(em,BJ) = Z(ai,BJ)  -> LIST 136
C        
      DO 20 ISPIN=1,1+IUHF
       DO 21 IRREPR=1,NIRREP 
        IRREPL=DIRPRD(IRREPR,IRREPX)
        LISTZ=138-ISPIN
        DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
        I000=1
        I010=I000+IINTFP*DISSYZ*NUMDSZ
C
C R(AI,em)*L(em,bj) and R(ai,EM)*L(EM,BJ) CONTRACTIONS
C
        LISTL=436-ISPIN
        LISTR=458-ISPIN
        DISSYR=IRPDPD(IRREPR,ISYTYP(1,LISTR)) 
        NUMDSR=IRPDPD(IRREPL,ISYTYP(2,LISTR)) 
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL)) 
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL)) 
        I020=I010+IINTFP*DISSYR*NUMDSR
        I030=I020+IINTFP*DISSYL*NUMDSL
        CALL GETLST(ICORE(I010),1,NUMDSR,1,IRREPL,LISTR)
        CALL GETLST(ICORE(I020),1,NUMDSL,1,IRREPR,LISTL)
        CALL XGEMM('N','N',DISSYZ,NUMDSZ,DISSYL,ONEM,ICORE(I010),
     &             DISSYR,ICORE(I020),DISSYL,ZILCH,ICORE(I000),DISSYZ)
C
C R(AI,EM)*L(EM,bj) and R(ai,em)*L(em,BJ) CONTRACTIONS
C
        LISTL=438-ISPIN
        LISTR=453+ISPIN
        DISSYR=IRPDPD(IRREPR,ISYTYP(1,LISTR)) 
        NUMDSR=IRPDPD(IRREPL,ISYTYP(2,LISTR)) 
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL)) 
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL)) 
        I020=I010+IINTFP*DISSYR*NUMDSR
        I030=I020+IINTFP*DISSYL*NUMDSL
        CALL GETLST(ICORE(I010),1,NUMDSR,1,IRREPL,LISTR)
        CALL GETLST(ICORE(I020),1,NUMDSL,1,IRREPR,LISTL)
        CALL XGEMM('N','N',DISSYZ,NUMDSZ,DISSYL,ONEM,ICORE(I010),
     &             DISSYR,ICORE(I020),DISSYL,ONE,ICORE(I000),DISSYZ)
C 
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
21     CONTINUE
20    CONTINUE
C
C  R(Ai,eM)*L(eM,Bj)                     = Z(Ai,Bj)  -> LIST 139
C  R(aI,Em)*L(Em,bJ)                     = Z(aI,bJ)  -> LIST 138 [UHF ONLY]
C
      DO 30 ISPIN=1,1+IUHF
       DO 31 IRREPR=1,NIRREP 
        IRREPL=DIRPRD(IRREPR,IRREPX)
        LISTZ=124+ISPIN
        DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
        I000=1
        I010=I000+IINTFP*DISSYZ*NUMDSZ
C
C R(Ai,eM)*L(eM,Bj) and R(aI,Em)*L(Em,bJ)
C
        LISTL=437+ISPIN
        LISTR=460-ISPIN
        DISSYR=IRPDPD(IRREPR,ISYTYP(1,LISTR)) 
        NUMDSR=IRPDPD(IRREPL,ISYTYP(2,LISTR)) 
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL)) 
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL)) 
        I020=I010+IINTFP*DISSYR*NUMDSR
        I030=I020+IINTFP*DISSYL*NUMDSL
        CALL GETLST(ICORE(I010),1,NUMDSR,1,IRREPL,LISTR)
        CALL GETLST(ICORE(I020),1,NUMDSL,1,IRREPR,LISTL)
        CALL XGEMM('N','N',DISSYZ,NUMDSZ,DISSYL,ONE,ICORE(I010),
     &             DISSYR,ICORE(I020),DISSYL,ZILCH,ICORE(I000),DISSYZ)
C 
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
31     CONTINUE
30    CONTINUE
C
      RETURN
      END
