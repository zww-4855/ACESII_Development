      SUBROUTINE GTTAU(G,GIK,T1,DISSYG,NUMSYG,POPG,POPT,
     &                VRTT,IRREPX,IRREP,ISPIN)
C
C THIS ROUTINE FORMS THE FOLLOWING CONTRIBUTION TO
C G(IJ,KA) REQUIRED IN EOM-CCSD GRADIENTS
C
C  G(IJ,KA) = G(IJ,KA) - HALF GIK(IK)*T1(JA)
C                      + HALF GIK(JK)*T1(IA)
C
C FOR ISPIN =1 (AAAA CASE) AND ISPIN =2 (BBBB CASE)
C THE EQUATION GIVEN APLIES DIRECTLY. FOR ISPIN=3 
C (ABAB CASE) IT REDUCES TO
C
C G(Ij,Ka) = G(Ij,Ka) - HALF GIK(IK)*T1(ja)
C
C AND FOR ISPIN=4 (BABA) CASE IT REDUCES TO
C
C G(Ij,Ak) = G(Ij,Ak) - HALF GIK(jk)*T1(IA)
C
CEND 
C
C CODED ORIGINALLY FOR DENS IN SEPTEMBER/90 JG
C MODIFIED FOR NON TOTALLY SYMMETRIC GIK AND T1, SEPTEMBER/93 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYG,DIRPRD,POPT,VRTT,POPG
      DIMENSION G(DISSYG,NUMSYG),T1(1),GIK(1)
      DIMENSION POPT(8),VRTT(8),POPG(8)
      DIMENSION IOFFGR(8),IOFFGL(8),IOFFT1(8),
     &          IOFFGIK(8)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
C
      DATA HALF /0.5D0/
C
      NNM1O2(I)=((I-1)*I)/2
C
      IF(ISPIN.LT.3) THEN
C
C AAAA OR BBBB SPIN CASE
C
C DETERMINE FIRST OFFSETS FOR T2 AND T1
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGIK(1)=0
       DO 1 IRREPJR=1,NIRREP-1
        IRREPJL=DIRPRD(IRREPJR,IRREPX)
        IOFFT1(IRREPJR+1)=IOFFT1(IRREPJR)
     &                    +POPT(IRREPJR)*VRTT(IRREPJL)
        IOFFGIK(IRREPJR+1)=IOFFGIK(IRREPJR)
     &                    +POPG(IRREPJR)*POPG(IRREPJL)
1      CONTINUE
       IF(IRREP.EQ.1) THEN
        DO 2 IRREPJ=1,NIRREP-1
         IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPG(IRREPJ)*VRTT(IRREPJ)
         IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+NNM1O2(POPG(IRREPJ))
2       CONTINUE
       ELSE
        DO 3 IRREPJ=1,NIRREP-1
         IRREPI=DIRPRD(IRREP,IRREPJ)
         IF(IRREPI.LT.IRREPJ) THEN
          IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPG(IRREPI)*VRTT(IRREPJ)
          IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+POPG(IRREPI)*POPT(IRREPJ)
         ELSE
          IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPG(IRREPI)*VRTT(IRREPJ)
          IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)
         ENDIF
3       CONTINUE
       ENDIF
C
       DO 100 IRREPA=1,NIRREP
C
        IRREPK=DIRPRD(IRREP,IRREPA)
        IRREPJ=DIRPRD(IRREPX,IRREPA)
        IRREPI=DIRPRD(IRREPX,IRREPK)
C
C  FIRST CASE IRREPI=IRREPJ AND IRREPA=IRREPK
C
        IF(IRREPK.EQ.IRREPA) THEN
         NUMA=VRTT(IRREPA)
         NUMI=POPG(IRREPI)
         NUMJ=POPG(IRREPJ)
         NUMK=POPG(IRREPK)
C
         INDKA=IOFFGR(IRREPA)
         INDT=IOFFT1(IRREPJ)
         INDG=IOFFGIK(IRREPK)
         INDL=IOFFGL(IRREPJ)
C
         IF(NNM1O2(NUMJ).EQ.0) GO TO 100
C
         DO 10 IA=1,NUMA
         DO 10 IK=1,NUMK
          INDG1=INDG+(IK-1)*NUMI
C
          INDKA=INDKA+1
C 
          INDIJ=INDL
C
          DO 20 IJ=2,NUMJ
          INDJA=INDT+(IJ-1)*NUMA+IA
          INDJK=INDG1+IJ
          DO 20 II=1,IJ-1
           INDIA=INDT+(II-1)*NUMA+IA
           INDIK=INDG1+II
           INDIJ=INDIJ+1
C
           G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GIK(INDIK)*T1(INDJA)
     &                                  +HALF*GIK(INDJK)*T1(INDIA)
20        CONTINUE
10       CONTINUE
C
C  SECOND CASE
C
        ELSE 
C
         IF(IRREPI.LT.IRREPJ) THEN
C
          NUMA=VRTT(IRREPA)
          NUMK=POPG(IRREPK)
          NUMI=POPG(IRREPI)
          NUMJ=POPT(IRREPJ)
C
          INDT=IOFFT1(IRREPJ)
          INDG=IOFFGIK(IRREPK)
          INDKA=IOFFGR(IRREPA)
          INDL=IOFFGL(IRREPJ)
C         
          DO 30 IA=1,NUMA
          DO 30 IK=1,NUMK
C
           INDG1=INDG+(IK-1)*NUMI
C
           INDKA=INDKA+1         
C
           INDIJ=INDL
C
           DO 50 IJ=1,NUMJ
            INDJA=INDT+(IJ-1)*NUMA+IA
            DO 60 II=1,NUMI
             INDIK=INDG1+II
             INDIJ=INDIJ+1
             G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GIK(INDIK)*T1(INDJA)
60          CONTINUE
50         CONTINUE
30        CONTINUE
C
C  NOW THE SECOND PART IRREPI.GT.IRREPJ)
C
         ELSE IF(IRREPI.GT.IRREPJ) THEN
C
          NUMA=VRTT(IRREPA)
          NUMK=POPG(IRREPK)
          NUMI=POPG(IRREPI)
          NUMJ=POPT(IRREPJ)
C
          INDT=IOFFT1(IRREPJ)
          INDG=IOFFGIK(IRREPK)
          INDKA=IOFFGR(IRREPA)
          INDL=IOFFGL(IRREPI)
C    
          DO 31 IA=1,NUMA
          DO 31 IK=1,NUMK
           INDG1=INDG+(IK-1)*NUMI
C
           INDKA=INDKA+1
C
           INDIJ=INDL
C
           DO 51 II=1,NUMI
            INDIK=INDG1+II
            DO 61 IJ=1,NUMJ
             INDJA=INDT+(IJ-1)*NUMA+IA
             INDIJ=INDIJ+1
             G(INDIJ,INDKA)=G(INDIJ,INDKA)+HALF*GIK(INDIK)*T1(INDJA) 
61          CONTINUE
51         CONTINUE
31        CONTINUE
C
         ENDIF
        ENDIF
C
100    CONTINUE
C
      ELSE IF(ISPIN.EQ.3) THEN
C
C  ABAB SPIN CASE
C
C  GET FIRST OFFSETS OF GIK, T1, AND G
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGIK(1)=0
       DO 101 IRREPJ=1,NIRREP-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
        IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTT(IRREPJ)*POPG(IRREPI)
        IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+POPT(IRREPJ)*POPG(IRREPI)
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+POPT(IRREPJ)
     &                        *VRTT(DIRPRD(IRREPJ,IRREPX))
        IOFFGIK(IRREPJ+1)=IOFFGIK(IRREPJ)+POPG(IRREPJ)
     &                        *POPG(DIRPRD(IRREPJ,IRREPX))
101    CONTINUE
C
       DO 200 IRREPA=1,NIRREP
C
        IRREPK=DIRPRD(IRREP,IRREPA)
        IRREPI=DIRPRD(IRREPX,IRREPK)
        IRREPJ=DIRPRD(IRREP,IRREPI)
C
        NUMJ=POPT(IRREPJ)
        NUMA=VRTT(IRREPA)
        NUMI=POPG(IRREPI)
        NUMK=POPG(IRREPK)
        INDL=IOFFGL(IRREPJ)
        INDKA=IOFFGR(IRREPA)
        INDT=IOFFT1(IRREPJ)
        INDG=IOFFGIK(IRREPK)
C
        DO 210 IA=1,NUMA
        DO 210 IK=1,NUMK
         INDKA=INDKA+1
         INDG1=INDG+(IK-1)*NUMI
         INDIJ=INDL
         DO 220 IJ=1,NUMJ
         INDJA=INDT+(IJ-1)*NUMA+IA
         DO 220 II=1,NUMI
          INDIJ=INDIJ+1
          INDIK=INDG1+II
          G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GIK(INDIK)*T1(INDJA)
220      CONTINUE

210     CONTINUE
200    CONTINUE
C
      ELSE IF(ISPIN.EQ.4) THEN
C
C  BABA SPIN CASE
C
C  GET FIRST OFFSETS OF GIK, T1, AND G
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGIK(1)=0
       DO 201 IRREPJ=1,NIRREP-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
        IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTT(IRREPI)*POPG(IRREPJ)
        IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+POPT(IRREPI)*POPG(IRREPJ)
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+POPT(IRREPJ)
     &                    *VRTT(DIRPRD(IRREPJ,IRREPX))
        IOFFGIK(IRREPJ+1)=IOFFGIK(IRREPJ)+POPG(IRREPJ)
     &                    *POPG(DIRPRD(IRREPJ,IRREPX))
201    CONTINUE
C
       DO 300 IRREPK=1,NIRREP
C
        IRREPA=DIRPRD(IRREP,IRREPK)
        IRREPI=DIRPRD(IRREPX,IRREPK)
        IRREPJ=DIRPRD(IRREP,IRREPI)
C
        NUMJ=POPT(IRREPJ)
        NUMA=VRTT(IRREPA)
        NUMI=POPG(IRREPI)
        NUMK=POPG(IRREPK)
C
        INDL=IOFFGL(IRREPI)
        INDKA=IOFFGR(IRREPK)
        INDT=IOFFT1(IRREPJ)
        INDG=IOFFGIK(IRREPK)
C
        DO 310 IK=1,NUMK
        INDG1=INDG+(IK-1)*NUMI
        DO 310 IA=1,NUMA
         INDKA=INDKA+1
         INDIJ=INDL
         DO 320 II=1,NUMI
         INDIK=INDG1+II
         DO 320 IJ=1,NUMJ
          INDJA=INDT+(IJ-1)*NUMA+IA
          INDIJ=INDIJ+1
          G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GIK(INDIK)*T1(INDJA)
320      CONTINUE

310     CONTINUE
300    CONTINUE
      ENDIF
      RETURN
      END
