      SUBROUTINE MODTDA(TDAMAT, NDIM, SCR, MAXCOR, IUHF,
     &   NSIZEC, IRREPX)
C
C  THE DIAGONAL OF TDAMAT IS MODIFIED SUCH THAT THE
C  LOWEST EIGENVALUES CORRESPOND TO EIGENVECTORS OF INTEREST
C  AS DETERMINED BY IPATTERN (SEE CALCEXCP). THIS IS EFFECTIVELY
C  EQUIVALENT TO A PROJECTION OF EXPANSION VECTORS OF INTEREST.
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION TDAMAT(NDIM, NDIM), SCR(MAXCOR), HUGE
C
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/FLAGS/IFLAGS(100)
C
      HUGE = 1.0D8
C
C  READ IN THE EXCITATION PATTERNS
C
       IF(IUHF.EQ.0) THEN
        NTDA=IRPDPD(IRREPX,9)
        CALL GETLST(SCR,1,1,1, 1, 490)
      ELSE
        NUMAA=IRPDPD(IRREPX,ISYTYP(1,19))
        NUMBB=IRPDPD(IRREPX,ISYTYP(1,20))
        NTDA = NUMAA + NUMBB
        I000 = 1
        I010 = I000 + NUMAA
        I020 = I010 + NUMBB
        CALL GETLST(SCR(I000),1,1,1, 1, 490)
        CALL GETLST(SCR(I010),1,1,1, 2, 490)
      ENDIF
C
      IF (NTDA .NE. NDIM) THEN
        WRITE(6,*) ' SOMETHING WRONG IN MODTDA', NDIM, NTDA
        CALL ERREX
      ENDIF
C
C  MODIFY DIAGONAL OF TDAMAT IF EXCITATION-PATTERN = 0
C
      IF (IFLAGS(1) .GE. 5) THEN
        WRITE(6,*) ' INFO ON MODTDA'
        DO I = 1 ,NDIM
          WRITE(6,1000) I, SCR(I), TDAMAT(I,I)
        ENDDO
      ENDIF
 1000   FORMAT(2X,I5,F5.2,F12.4)
C
      DO I = 1, NDIM
        IF (SCR(I) .LT. 0.25) THEN
          TDAMAT(I,I) = TDAMAT(I,I) + HUGE
        ENDIF
      ENDDO
C
      RETURN
      END


