      SUBROUTINE GTTAU2(G,GAC,T1,DISSYG,NUMSYG,VRTG,POPT,
     &                  VRTT,IRREPX,IRREP,ISPIN,IDIS1,NUMDIS)
C
C THIS ROUTINE FORMS THE FOLLOWING CONTRIBUTION TO
C G(IJ,KA) REQUIRED IN EOM-CCSD GRADIENTS
C
C  G(AB,CI) = G(AB,CI) + HALF GAC(AC)*T1(IB)
C                      - HALF GAC(BC)*T1(IA)
C
C FOR ISPIN =1 (AAAA CASE) AND ISPIN =2 (BBBB CASE)
C THE EQUATION GIVEN APLIES DIRECTLY. FOR ISPIN=3 
C (ABAB CASE) IT REDUCES TO
C
C G(Ij,Ka) = G(Ij,Ka) + HALF GAC(IK)*T1(ja)
C
C AND FOR ISPIN=4 (BABA) CASE IT REDUCES TO
C
C G(Ij,Ak) = G(Ij,Ak) + HALF GAC(jk)*T1(IA)
C
CEND 
C
C CODED ORIGINALLY FOR DENS IN SEPTEMBER/90 JG
C MODIFIED FOR NON TOTALLY SYMMETRIC GAC AND T1, SEPTEMBER/93 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYG,DIRPRD,POPT,VRTT,VRTG
      DIMENSION G(DISSYG,*),T1(1),GAC(1)
      DIMENSION POPT(8),VRTT(8),VRTG(8)
      DIMENSION IOFFGR(8),IOFFGL(8),IOFFT1(8),
     &          IOFFGAC(8)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
C
      DATA HALF /0.5D0/
C
      NNM1O2(I)=((I-1)*I)/2
C
      IF(ISPIN.LT.3) THEN
C
C AAAA OR BBBB SPIN CASE
C
C DETERMINE FIRST OFFSETS FOR T2 AND T1
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGAC(1)=0
       DO 1 IRREPJR=1,NIRREP-1
        IRREPJL=DIRPRD(IRREPJR,IRREPX)
        IOFFT1(IRREPJR+1)=IOFFT1(IRREPJR)
     &                    +VRTT(IRREPJR)*POPT(IRREPJL)
        IOFFGAC(IRREPJR+1)=IOFFGAC(IRREPJR)
     &                    +VRTG(IRREPJR)*VRTG(IRREPJL)
1      CONTINUE
       IF(IRREP.EQ.1) THEN
        DO 2 IRREPJ=1,NIRREP-1
         IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTG(IRREPJ)*POPT(IRREPJ)
         IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+NNM1O2(VRTG(IRREPJ))
2       CONTINUE
       ELSE
        DO 3 IRREPJ=1,NIRREP-1
         IRREPI=DIRPRD(IRREP,IRREPJ)
         IF(IRREPI.LT.IRREPJ) THEN
          IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTG(IRREPI)*POPT(IRREPJ)
          IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+VRTG(IRREPI)*VRTT(IRREPJ)
         ELSE
          IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTG(IRREPI)*POPT(IRREPJ)
          IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)
         ENDIF
3       CONTINUE
       ENDIF
C
       DO 100 IRREPA=1,NIRREP
C
        IRREPK=DIRPRD(IRREP,IRREPA)
        IRREPJ=DIRPRD(IRREPX,IRREPA)
        IRREPI=DIRPRD(IRREPX,IRREPK)
C
C  FIRST CASE IRREPI=IRREPJ AND IRREPA=IRREPK
C
        IF(IRREPK.EQ.IRREPA) THEN
         NUMA=POPT(IRREPA)
         NUMI=VRTG(IRREPI)
         NUMJ=VRTG(IRREPJ)
         NUMK=VRTG(IRREPK)
C
c         INDKA=IOFFGR(IRREPA)
         INDKA=IOFFGR(IRREPA)-IDIS1+1
         INDT=IOFFT1(IRREPJ)
         INDG=IOFFGAC(IRREPK)
         INDL=IOFFGL(IRREPJ)
C
         IF(NNM1O2(NUMJ).EQ.0) GO TO 100
C
         DO 10 IA=1,NUMA
         DO 10 IK=1,NUMK
          INDG1=INDG+(IK-1)*NUMI
C
          INDKA=INDKA+1
C
          IF(INDKA.LE.NUMDIS.AND.INDKA.GT.0)THEN
C 
           INDIJ=INDL
C
           DO 20 IJ=2,NUMJ
            INDJA=INDT+(IJ-1)*NUMA+IA
            INDJK=INDG1+IJ
            DO 20 II=1,IJ-1
            INDIA=INDT+(II-1)*NUMA+IA
            INDIK=INDG1+II
            INDIJ=INDIJ+1
C
            G(INDIJ,INDKA)=G(INDIJ,INDKA)+HALF*GAC(INDIK)*T1(INDJA)
     &                                   -HALF*GAC(INDJK)*T1(INDIA)
20         CONTINUE
C
          ENDIF
10       CONTINUE
C
C  SECOND CASE
C
        ELSE 
C
         IF(IRREPI.LT.IRREPJ) THEN
C
          NUMA=POPT(IRREPA)
          NUMK=VRTG(IRREPK)
          NUMI=VRTG(IRREPI)
          NUMJ=VRTT(IRREPJ)
C
          INDT=IOFFT1(IRREPJ)
          INDG=IOFFGAC(IRREPK)
c          INDKA=IOFFGR(IRREPA)
          INDKA=IOFFGR(IRREPA)-IDIS1+1
          INDL=IOFFGL(IRREPJ)
C         
          DO 30 IA=1,NUMA
          DO 30 IK=1,NUMK
C
           INDG1=INDG+(IK-1)*NUMI
C
           INDKA=INDKA+1         
C
           IF(INDKA.LE.NUMDIS.AND.INDKA.GT.0)THEN
C
            INDIJ=INDL
C
            DO 50 IJ=1,NUMJ
             INDJA=INDT+(IJ-1)*NUMA+IA
             DO 60 II=1,NUMI
              INDIK=INDG1+II
              INDIJ=INDIJ+1
              G(INDIJ,INDKA)=G(INDIJ,INDKA)+HALF*GAC(INDIK)*T1(INDJA)
60           CONTINUE
50          CONTINUE
           ENDIF
30        CONTINUE
C
C  NOW THE SECOND PART IRREPI.GT.IRREPJ)
C
         ELSE IF(IRREPI.GT.IRREPJ) THEN
C
          NUMA=POPT(IRREPA)
          NUMK=VRTG(IRREPK)
          NUMI=VRTG(IRREPI)
          NUMJ=VRTT(IRREPJ)
C
          INDT=IOFFT1(IRREPJ)
          INDG=IOFFGAC(IRREPK)
          INDKA=IOFFGR(IRREPA)-IDIS1+1
c          INDKA=IOFFGR(IRREPA)
          INDL=IOFFGL(IRREPI)
C    
          DO 31 IA=1,NUMA
          DO 31 IK=1,NUMK
           INDG1=INDG+(IK-1)*NUMI
C
           INDKA=INDKA+1
           IF(INDKA.LE.NUMDIS.AND.INDKA.GT.0)THEN
C
            INDIJ=INDL
C
            DO 51 II=1,NUMI
             INDIK=INDG1+II
             DO 61 IJ=1,NUMJ
              INDJA=INDT+(IJ-1)*NUMA+IA
              INDIJ=INDIJ+1
              G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GAC(INDIK)*T1(INDJA) 
61           CONTINUE
51          CONTINUE
           ENDIF
31        CONTINUE
C
C
         ENDIF
        ENDIF
C
100    CONTINUE
C
      ELSE IF(ISPIN.EQ.3) THEN
C
C  ABAB SPIN CASE
C
C  GET FIRST OFFSETS OF GAC, T1, AND G
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGAC(1)=0
       DO 101 IRREPJ=1,NIRREP-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
        IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPT(IRREPJ)*VRTG(IRREPI)
        IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+VRTT(IRREPJ)*VRTG(IRREPI)
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+VRTT(IRREPJ)
     &                        *POPT(DIRPRD(IRREPJ,IRREPX))
        IOFFGAC(IRREPJ+1)=IOFFGAC(IRREPJ)+VRTG(IRREPJ)
     &                        *VRTG(DIRPRD(IRREPJ,IRREPX))
101    CONTINUE
C
       DO 200 IRREPA=1,NIRREP
C
        IRREPK=DIRPRD(IRREP,IRREPA)
        IRREPI=DIRPRD(IRREPX,IRREPK)
        IRREPJ=DIRPRD(IRREP,IRREPI)
C
        NUMJ=VRTT(IRREPJ)
        NUMA=POPT(IRREPA)
        NUMI=VRTG(IRREPI)
        NUMK=VRTG(IRREPK)
        INDL=IOFFGL(IRREPJ)
c        INDKA=IOFFGR(IRREPA)
        INDKA=IOFFGR(IRREPA)-IDIS1+1
        INDT=IOFFT1(IRREPJ)
        INDG=IOFFGAC(IRREPK)
C
        DO 210 IA=1,NUMA
        DO 210 IK=1,NUMK
         INDKA=INDKA+1
         IF(INDKA.LE.NUMDIS.AND.INDKA.GT.0)THEN
          INDG1=INDG+(IK-1)*NUMI
          INDIJ=INDL
          DO 220 IJ=1,NUMJ
          INDJA=INDT+(IJ-1)*NUMA+IA
          DO 220 II=1,NUMI
           INDIJ=INDIJ+1
           INDIK=INDG1+II
          G(INDIJ,INDKA)=G(INDIJ,INDKA)+HALF*GAC(INDIK)*T1(INDJA)
220       CONTINUE
         ENDIF

210     CONTINUE
200    CONTINUE
C
      ELSE IF(ISPIN.EQ.4) THEN
C
C  BABA SPIN CASE
C
C  GET FIRST OFFSETS OF GAC, T1, AND G
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGAC(1)=0
       DO 201 IRREPJ=1,NIRREP-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
        IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPT(IRREPI)*VRTG(IRREPJ)
        IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+VRTT(IRREPI)*VRTG(IRREPJ)
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+VRTT(IRREPJ)
     &                    *POPT(DIRPRD(IRREPJ,IRREPX))
        IOFFGAC(IRREPJ+1)=IOFFGAC(IRREPJ)+VRTG(IRREPJ)
     &                    *VRTG(DIRPRD(IRREPJ,IRREPX))
201    CONTINUE
C
       DO 300 IRREPK=1,NIRREP
C
        IRREPA=DIRPRD(IRREP,IRREPK)
        IRREPI=DIRPRD(IRREPX,IRREPK)
        IRREPJ=DIRPRD(IRREP,IRREPI)
C
        NUMJ=VRTT(IRREPJ)
        NUMA=POPT(IRREPA)
        NUMI=VRTG(IRREPI)
        NUMK=VRTG(IRREPK)
C
        INDL=IOFFGL(IRREPI)
c        INDKA=IOFFGR(IRREPK)
        INDKA=IOFFGR(IRREPK)-IDIS1+1
        INDT=IOFFT1(IRREPJ)
        INDG=IOFFGAC(IRREPK)
C
C
        DO 310 IK=1,NUMK
        INDG1=INDG+(IK-1)*NUMI
        DO 310 IA=1,NUMA
         INDKA=INDKA+1
C
         IF(INDKA.LE.NUMDIS.AND.INDKA.GT.0)THEN
          INDIJ=INDL
          DO 320 II=1,NUMI
          INDIK=INDG1+II
          DO 320 IJ=1,NUMJ
           INDJA=INDT+(IJ-1)*NUMA+IA
           INDIJ=INDIJ+1
           G(INDIJ,INDKA)=G(INDIJ,INDKA)+HALF*GAC(INDIK)*T1(INDJA)
320       CONTINUE
         ENDIF
C
310     CONTINUE
300    CONTINUE
      ENDIF
      RETURN
      END
