      SUBROUTINE EXPDEN(DOO,DVV,DVO,DOV,F,NMO,IRREPX,ISPIN,REFCONT)
C
C THIS ROUTINE EXPANDS THE ONE-PARTICLE DENSITY MATRIX OR SIMILAR
C QUANTITY TO A FULL UNPACKED MATRIX REPRESENTATION.
C
CEND
      IMPLICIT INTEGER (A-Z)
      LOGICAL REFCONT
      DOUBLE PRECISION F(NMO,NMO),DOO(*),DVV(*),DVO(*),DOV(*),ONE
      DIMENSION IOFFO(8),IOFFV(8)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFEA(2),NFMI(2)
      COMMON /INFO/ NOCCO(2),NVRTO(2)
C
      DATA  ONE /1.0D0/
C
C CALCULATE OFFSETS
C
      IOFFO(1)=0
      IOFFV(1)=NOCCO(ISPIN)
      DO 1 IRREP=1,NIRREP-1
       IOFFO(IRREP+1)=IOFFO(IRREP)+POP(IRREP,ISPIN)
       IOFFV(IRREP+1)=IOFFV(IRREP)+VRT(IRREP,ISPIN)
1     CONTINUE
C
      CALL ZERO(F,NMO*NMO)
C
C EXPAND SYMMETRY PACKED OCCUPIED-OCCUPIED PART
C
      ITHRU=0
      DO 10 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPX)
       DO 20 J=1,POP(IRREPR,ISPIN)
        DO 21 I=1,POP(IRREPL,ISPIN)
         ITHRU=ITHRU+1
         F(I+IOFFO(IRREPL),J+IOFFO(IRREPR))=DOO(ITHRU)
21      CONTINUE
20     CONTINUE
10    CONTINUE
C
C ADD IN REFERENCE FUNCTION CONTRIBUTION (FINAL STATE DENSITY ONLY)
C
      IF(REFCONT)THEN
       DO 11 IRREPR=1,NIRREP
        DO 12 I=1,POP(IRREPR,ISPIN)
         F(I+IOFFO(IRREPR),I+IOFFO(IRREPR))=
     &                  F(I+IOFFO(IRREPR),I+IOFFO(IRREPR))+ONE
12      CONTINUE
11     CONTINUE
      ENDIF          
C
C EXPAND SYMMETRY PACKED VIRTUAL-VIRTUAL PART
C
      ITHRU=0
      DO 110 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPX)
       DO 120 J=1,VRT(IRREPR,ISPIN)
        DO 121 I=1,VRT(IRREPL,ISPIN)
         ITHRU=ITHRU+1
         F(I+IOFFV(IRREPL),J+IOFFV(IRREPR))=DVV(ITHRU)
121     CONTINUE
120    CONTINUE
110   CONTINUE
C
C EXPAND SYMMETRY PACKED VIRTUAL-OCCUPIED PARTS
C
      ITHRU=0
      DO 210 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPX)
       DO 220 I=1,POP(IRREPR,ISPIN)
        DO 221 A=1,VRT(IRREPL,ISPIN)
         ITHRU=ITHRU+1
         F(A+IOFFV(IRREPL),I+IOFFO(IRREPR))=DVO(ITHRU)
         F(I+IOFFO(IRREPR),A+IOFFV(IRREPL))=DOV(ITHRU)
221     CONTINUE
220    CONTINUE
210   CONTINUE
C
      RETURN
      END 
