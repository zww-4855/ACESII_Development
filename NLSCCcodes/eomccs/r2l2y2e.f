      SUBROUTINE R2L2Y2E(ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = - 1/2 P(i,j) R(mn,ef)*W(in,ef)*L(mj,ab)
C
C  RHF : SPIN-ADPAPTED CODE
C  UHF : ALL SPIN CASES ARE EXPLICITLY CONSIDERED
C  
CEND
C
C CODED SEPTEMBER/93 JFS AND JG
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
C
      DIMENSION ICORE(MAXCOR)
      DIMENSION I00G(2)
C
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
      IF(IUHF.EQ.0)THEN
       LISTL=446
       LISTZ=63
       I0G=1
       I000=I0G+IINTFP*IRPDPD(IRREPX,21)
C
C SPIN ADAPTED RHF CODE.
C
C  Y1(Ab,Ij) = Z(Ab,Ij) + Z(Ba,Ji)
C
C WHERE
C
C   Z(Ab,Ij) = [2 R(Mn,Ef)-R(Nm,Ef)]*W(In,Ef)*L(Ab,Im)
C
C
C FIRST PICK UP G(MI) = W(In,Ef)*[2*R(Mn,Ef)-R(Nm,Ef)] FROM LIST 491
C
       CALL GETLST(ICORE(I0G),1,1,1,1,491)
C
C FORM PRODUCT Z(Ab,Ij)=L(Ab,Mj)*G(MI)
C
       DO 10 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        MAXSIZ=MAX(DISSYZ*NUMDSZ,DISSYL*NUMDSL)
        MAXT  =MAX(DISSYZ,NUMDSZ,DISSYL,NUMDSL)
        I010=I000+IINTFP*MAXSIZ
        I020=I010+IINTFP*MAXSIZ
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXT
        ITMP3=ITMP2+IINTFP*MAXT
        ITMP4=ITMP3+IINTFP*MAXT
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
        CALL SYMTR1(IRREPR,POP(1,1),POP(1,1),DISSYL,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C NOW FORM Z(Ab,jI) = L(Ab,jM)*G(MI)
C
        DO 20 IRREPM=1,NIRREP
         IRREPJ=DIRPRD(IRREPM,IRREPR)
         IRREPI=DIRPRD(IRREPM,IRREPX)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,1)
         NUMM=POP(IRREPM,1)
         NROW=DISSYL*NUMJ
         NCOL=NUMI
         NSUM=NUMM
         IOFFL=I000+(ISYMOFF(IRREPM,IRREPR,21)-1)*DISSYL*IINTFP
         IOFFZ=I010+(ISYMOFF(IRREPI,IRREPL,21)-1)*DISSYZ*IINTFP
         IOFFG=I0G+(ISYMOFF(IRREPI,IRREPX,21)-1)*IINTFP
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),NROW,
     &              ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),NROW)
20      CONTINUE
        CALL SYMTR1(IRREPL,POP(1,1),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL SYMRHF(IRREPL,VRT(1,1),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1) 
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
C
10     CONTINUE
C
      ELSE
C
       I00G(1)=1
       I00G(2)=I00G(1)+IINTFP*IRPDPD(IRREPX,21)
       ISTART=I00G(2)+IINTFP*IRPDPD(IRREPX,22)
       CALL GETLST(ICORE(I00G(1)),1,1,1,1,491)
       CALL GETLST(ICORE(I00G(2)),1,1,1,2,491)
C
C DO FIRST SPIN CASES AAAA AND BBBB
C
       DO 1000 ISPIN=1,2
C
        LISTL=443+ISPIN
        LISTZ=60+ISPIN
C
C LOOP OVER IRREPS
C
        DO 100 IRREPR=1,NIRREP
C
         IRREPL=DIRPRD(IRREPR,IRREPX)
         NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
         DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
         NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
         DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
         NOCCSQL=IRPDPD(IRREPL,20+ISPIN)
         NOCCSQR=IRPDPD(IRREPR,20+ISPIN)
C
         I000=ISTART
         I010=I000+IINTFP*MAX(DISSYL*NOCCSQR,NUMDSZ*DISSYZ)
         ITMP=I010+IINTFP*DISSYZ*NOCCSQL
         IEND=ITMP+IINTFP*3*MAX(DISSYZ,DISSYL,NOCCSQL,NOCCSQR)
         IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y2E',IEND,MAXCOR)
C
         CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
C
         CALL SYMEXP(IRREPR,POP(1,ISPIN),DISSYL,ICORE(I000))
C
         DO 120 IRREPM=1,NIRREP
C
          IRREPI=DIRPRD(IRREPM,IRREPR)
          IRREPJ=DIRPRD(IRREPX,IRREPM)
          NUMM=POP(IRREPM,ISPIN)
          NUMI=POP(IRREPI,ISPIN)
          NUMJ=POP(IRREPJ,ISPIN)
C
          NROW=DISSYL*NUMI
          NCOL=NUMJ
          NSUM=NUMM
C
          IOFFL=I000+IINTFP*DISSYL*
     &               (ISYMOFF(IRREPM,IRREPR,20+ISPIN)-1)
          IOFFZ=I010+IINTFP*DISSYZ*
     &               (ISYMOFF(IRREPJ,IRREPL,20+ISPIN)-1)
          IOFFG=I00G(ISPIN)+IINTFP*
     &               (ISYMOFF(IRREPJ,IRREPX,20+ISPIN)-1)
C
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),
     &               NROW,ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),
     &               NROW)
C
120      CONTINUE
C
         CALL ASSYM(IRREPL,POP(1,ISPIN),DISSYZ,DISSYZ,
     &              ICORE(I000),ICORE(I010))
C
         CALL GETLST(ICORE(I010),1,NUMDSZ,1,IRREPL,LISTZ)
         CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I000),1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),1,NUMDSZ,1,IRREPL,LISTZ)
C
100     CONTINUE
C
1000   CONTINUE
C
C DO ABAB SPIN CASE
C
       LISTL=446
       LISTZ=63
C
C LOOP OVER IRREPS
C
       DO 1100 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPR,IRREPX)
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
C
        I000=ISTART
        I010=I000+IINTFP*MAX(NUMDSL*DISSYL,NUMDSZ*DISSYZ)
        ITMP=I010+IINTFP*NUMDSZ*DISSYZ
        IEND=ITMP+IINTFP*3*MAX(NUMDSZ,NUMDSL,DISSYL,DISSYZ)
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L2Y2E',IEND,MAXCOR)
C
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
C
        DO 1120 IRREPM=1,NIRREP
C
         IRREPI=DIRPRD(IRREPM,IRREPR)
         IRREPJ=DIRPRD(IRREPX,IRREPM)
         NUMM=POP(IRREPM,2)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
C
         NROW=DISSYL*NUMI
         NCOL=NUMJ
         NSUM=NUMM
C
         IOFFL=I000+IINTFP*DISSYL*
     &              (ISYMOFF(IRREPM,IRREPR,14)-1)
         IOFFZ=I010+IINTFP*DISSYZ*
     &              (ISYMOFF(IRREPJ,IRREPL,14)-1)
         IOFFG=I00G(2)+IINTFP*
     &              (ISYMOFF(IRREPJ,IRREPX,22)-1)
C
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),
     &              NROW,ICORE(IOFFG),NSUM,ZILCH,ICORE(IOFFZ),
     &              NROW)
C
1120    CONTINUE
C
        CALL SYMTR1(IRREPR,POP(1,1),POP(1,2),DISSYL,ICORE(I000),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYL),
     &              ICORE(ITMP+2*IINTFP*DISSYL))
        CALL SYMTR1(IRREPL,POP(1,1),POP(1,2),DISSYZ,ICORE(I010),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYZ),
     &              ICORE(ITMP+2*IINTFP*DISSYZ))

        DO 1130 IRREPM=1,NIRREP
C
         IRREPJ=DIRPRD(IRREPM,IRREPR)
         IRREPI=DIRPRD(IRREPX,IRREPM)
         NUMM=POP(IRREPM,1)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
C
         NROW=DISSYL*NUMJ
         NCOL=NUMI
         NSUM=NUMM
C
         IOFFL=I000+IINTFP*DISSYL*
     &              (ISYMOFF(IRREPM,IRREPR,24)-1)
         IOFFZ=I010+IINTFP*DISSYZ*
     &              (ISYMOFF(IRREPI,IRREPL,24)-1)
         IOFFG=I00G(1)+IINTFP*
     &              (ISYMOFF(IRREPI,IRREPX,21)-1)
C
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),
     &              NROW,ICORE(IOFFG),NSUM,ONE,ICORE(IOFFZ),
     &              NROW)
C
1130    CONTINUE
C
        CALL SYMTR1(IRREPL,POP(1,2),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYZ),
     &              ICORE(ITMP+2*IINTFP*DISSYZ))
C
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
        CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
C
1100   CONTINUE
C
      ENDIF
C
      RETURN
      END  
