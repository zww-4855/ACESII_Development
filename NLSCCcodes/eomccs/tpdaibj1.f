      SUBROUTINE TPDAIBJ1(ICORE,MAXCOR,IUHF)
C
C CALCULATION OF THE SECOND AIBJ CONTRIBUTION TO
C THE EOM-CCSD TWO-PARTICLE DENSITY MATRIX
C
C G(IB,AJ) <= L(IM,AE)*R(ME)*T(JB)
C
C SPIN CASES:
C
C AAAA : X(AI)*T(BJ) [LIST  ]
C BBBB : X(ai)*T(bj) [LIST  ]
C ABAB : X(AI)*T(bj) [LIST  ]
C BABA : X(ai)*T(BJ) [LIST  ]
C ABBA : no contribution
C BAAB : no contribution
C
C THE X INTERMEDIATES ARE DEFINED BY:
C
C X(AI) = L(IM,AE)*R(ME) + L(Im,Ae)*R(me)
C X(ai) = L(im,ae)*R(me) + L(iM,aE)*R(ME)
C
C AND ARE A TOTALLY SYMMETRIC QUANTITY
C
CEND 
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,TWO,ONEM
      DIMENSION ICORE(MAXCOR),I0T(2),I0R(2),I0X(2)
      COMMON/STATSYM/IRREPX
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,TWO,ONEM /1.0D0,0.0D0,-1.0D0/
C
C READ IN R1 AND T1 AMPLITUDES AND ALLOCATE CORE FOR THE X INTERMEDIATES
C
      I0T(1)=1
      I0T(2)=I0T(1)+IRPDPD(1,9)*IINTFP*IUHF
      I0R(1)=I0T(2)+IRPDPD(1,10)*IINTFP
      I0R(2)=I0R(1)+IRPDPD(IRREPX,9)*IINTFP*IUHF
      I0X(1)=I0R(2)+IRPDPD(IRREPX,10)*IINTFP
      I0X(2)=I0X(1)+IRPDPD(1,9)*IINTFP*IUHF
      ISTART=I0X(2)+IRPDPD(1,10)*IINTFP
      MXCOR=MAXCOR-ISTART+1
C
      CALL GETLST(ICORE(I0T(1)),1,1,1,1,90)
      CALL GETLST(ICORE(I0R(1)),1,1,1,3,490)
      IF(IUHF.EQ.1) THEN
       CALL GETLST(ICORE(I0T(2)),1,1,1,2,90)
       CALL GETLST(ICORE(I0R(2)),1,1,1,4,490)
      ENDIF
C
C CALCULATE X AMPLITUDES
C
      CALL GT2XF(ICORE(I0X(1)),IRREPX,IRREPX,434,490,2,ICORE(ISTART),
     &           MXCOR,IUHF,0)
C
      I000=ISTART
C
      DO 10 ISPIN=1,1+IUHF
       LISTG=122+ISPIN
       DISSYG=IRPDPD(1,ISYTYP(1,LISTG))
       NUMDSG=IRPDPD(1,ISYTYP(2,LISTG))
       CALL GETLST(ICORE(I000),1,NUMDSG,1,1,LISTG)
       CALL XGEMM('N','N',DISSYG,NUMDSG,1,ONEM,ICORE(I0X(ISPIN)),
     &            DISSYG,ICORE(I0T(ISPIN)),1,ONE,ICORE(I000),DISSYG)
       CALL PUTLST(ICORE(I000),1,NUMDSG,1,1,LISTG)
10    CONTINUE
C
      DO 20 ISPIN=1,1+IUHF 
       LISTG=119-ISPIN
       DISSYG=IRPDPD(1,ISYTYP(1,LISTG))
       NUMDSG=IRPDPD(1,ISYTYP(2,LISTG))
       CALL GETLST(ICORE(I000),1,NUMDSG,1,1,LISTG)
       CALL XGEMM('N','N',DISSYG,NUMDSG,1,ONE,ICORE(I0X(ISPIN)),
     &            DISSYG,ICORE(I0T(3-ISPIN)),1,ONE,ICORE(I000),DISSYG)
       CALL PUTLST(ICORE(I000),1,NUMDSG,1,1,LISTG)
20    CONTINUE
C
      CALL CHECKGAM(ICORE,23,123,TWO)
      CALL CHECKGAM(ICORE,25,125,TWO)
      CALL CHECKGAM(ICORE,18,118,TWO)
      IF(IUHF.NE.0) THEN
       CALL CHECKGAM(ICORE,24,124,TWO)
       CALL CHECKGAM(ICORE,26,126,TWO)
       CALL CHECKGAM(ICORE,17,117,TWO)
      ENDIF
C
      RETURN
      END
