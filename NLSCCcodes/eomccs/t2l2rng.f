      SUBROUTINE T2L2RNG(ICORE,MAXCOR,SPCASE,IUHF,IRREPX,R0)
C
C  THIS ROUTINE COMPUTES THE RING-TYPE
C
C  H(ME,IA) = - SUM L(MN,EF) T(IN,AF)
C               N,F
C
C  INTERMEDIATE FOR ALL SIX POSSIBLE SPIN CASES. 
C
CEND
C
C  CODED SEPTEMBER/93  JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,DISSYG,DSSYT1A,DSSYT2A,DSSYT1B,DSSYT2B,
     &        DSSYT1,DSSYT2,DSSYT3A,DSSYT3B,DSSYT4A,DSSYT4B,
     &        DISSYG2  
      CHARACTER*4 SPCASE
      DIMENSION ICORE(MAXCOR)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
C
      DATA AZERO,HALF,ONE,ONEM,TWO /0.0D0,0.5D0,1.0D0,-1.D0,2.D0/
C
C
C DO SPIN CASE AAAA AND BBBB:
C
C   H(MEJB) =  - SUM N,F L(MN,EF) T(NJ,FB) 
C                      + L(Mn,Ef) T(Jn,Fb)
C
C  STORAGE MODE :   EM,BJ    ACTUALLY  = R(MN,FE) L(NJ,BF)
C                                  STORAGE HERE   EM,NF AND NF, BJ
C                                  SIGN = ONE  * -1 --> ONEM
C                            R(Mn,Ef) L(Jn,Bf)  STORAGE HERE BJ nf
C                                   SIGN = ONE * -1 --> ONEM
C  AAAA :     UHF ONLY
C
C  BBBB :     UHF ONLY           
C
      IF(SPCASE.EQ.'AAAA'.OR.SPCASE.EQ.'BBBB')THEN
       IF(SPCASE.EQ.'AAAA')THEN
        ISPIN=1
       ELSEIF(SPCASE.EQ.'BBBB')THEN
        ISPIN=2
       ENDIF
       LISTG =413+ISPIN
       LIST1A=433+ISPIN
       LIST2A=33+ISPIN
       LIST1B=438-ISPIN
       LIST2B=38-ISPIN
       FACT=ONEM
       DO 100 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPX,IRREPR) 
        DSSYT1A=IRPDPD(IRREPR,ISYTYP(1,LIST1A))
        DSSYT2A=IRPDPD(IRREPL,ISYTYP(1,LIST2A))
        NMSYT1A=IRPDPD(IRREPL,ISYTYP(2,LIST1A))
        NMSYT2A=IRPDPD(IRREPL,ISYTYP(2,LIST2A))
        DSSYT1B=IRPDPD(IRREPR,ISYTYP(1,LIST1B))
        DSSYT2B=IRPDPD(IRREPL,ISYTYP(1,LIST2B))
        NMSYT1B=IRPDPD(IRREPL,ISYTYP(2,LIST1B))
        NMSYT2B=IRPDPD(IRREPL,ISYTYP(2,LIST2B))
        DISSYG=IRPDPD(IRREPR,ISYTYP(1,LISTG))
        NUMSYG=IRPDPD(IRREPL,ISYTYP(2,LISTG))
C
C FIRST TERM
C
        I000=1
        I010=I000+IINTFP*DISSYG*NUMSYG
        I020=I010+IINTFP*NMSYT1A*DSSYT1A
        I030=I020+IINTFP*NMSYT2A*DSSYT2A
        IF(MAXCOR.LT.I030) CALL INSMEM('R2L2RNG',I030,MAXCOR)
        CALL ZERO(ICORE(I000),DISSYG*NUMSYG)
        CALL GETLST(ICORE(I020),1,NMSYT2A,2,IRREPL,LIST2A)
        CALL GETLST(ICORE(I010),1,NMSYT1A,1,IRREPL,LIST1A)
        CALL XGEMM('N','N',DISSYG,NUMSYG,DSSYT2A,FACT,ICORE(I010),
     &             DSSYT1A,ICORE(I020),DSSYT2A,AZERO,ICORE(I000),
     &             DISSYG)
C
C SECOND TERM
C
        I020=I010+IINTFP*NMSYT1B*DSSYT1B
        I030=I020+IINTFP*NMSYT2B*DSSYT2B
        IF(MAXCOR.LT.I030) CALL INSMEM('R2L2RNG',I030,MAXCOR)
        CALL GETLST(ICORE(I020),1,NMSYT2B,2,IRREPL,LIST2B)
        CALL GETLST(ICORE(I010),1,NMSYT1B,1,IRREPL,LIST1B)
        CALL XGEMM('N','T',DISSYG,NUMSYG,NMSYT2B,FACT,ICORE(I010),
     &             DSSYT1B,ICORE(I020),DSSYT2B,ONE,ICORE(I000),
     &             DISSYG)
C
C  SAVE H INTERMEDIATE ON DISK
C
        CALL PUTLST(ICORE(I000),1,NUMSYG,2,IRREPL,LISTG)
        CALL GETLST(ICORE(I000),1,NUMSYG,2,IRREPL,LISTG)
C
100    CONTINUE
C
C DO SPIN CASES ABBA AND BAAB:
C
C   H(MEia) = - SUM N,F L(MN,EF) T(iN,aF) + L(Mn,Ef) T(in,af)
C
C  THIS CORRESPONDS TO <Ia//jB> = <Ia/jB> = - <Ij//Ba> (LIST 18)
C
C STORAGE : EM,bj (ACTUALLY HERE WE ARE INTERESTED IN THE NEGATIVE OF
C                  THIS TERM (<Ia//jB> =  - <Ia//Bj> = - <Ba//Ij>)
C
C      ACTUALLY L(MN,EF) T(jN,bF) = - L(MN,FE) T(Nj,Fb)
C
C      STORAGE    EM, FN ; FN, bj    OVERALL SIGN :  ONE * -1 --> ONEM
C
C      ACTUALLY  L(Mn,Ef) R(jn,bf) = - L(Mn,Ef) R(jn,fb)
C
C      STORAGE    EM, nf ;  nf,  jb   OVERALL SIGN : ONE * -1 --> ONEM
C
C                       RHF CASE : CALCULATE ONLY ABBA
C                       UHF CASE : CALCULATE ABBA AND BAAB
C    
      ELSEIF(SPCASE.EQ.'ABBA'.OR.SPCASE.EQ.'BAAB')THEN
       IF(SPCASE.EQ.'ABBA')THEN
        LISTG=416
        FACT=ONEM
        LIST1A=434
        LIST2A=37
        LIST1B=437
        LIST2B=34+IUHF
C
C FOR RHF, EXPLOIT SPIN ADAPTION
C
        IF(IUHF.EQ.0) THEN
         LIST1A=437
         LIST2B=37
         FACT=HALF
        ENDIF
C
       ENDIF
       IF(SPCASE.EQ.'ABBA'.AND.IUHF.EQ.1)THEN 
        LISTG2=417
        FACT=ONEM
        LIST3A=435
        LIST4A=36
        LIST3B=436
        LIST4B=34
       ENDIF
       DO 200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPX,IRREPR) 
        DSSYT1A=IRPDPD(IRREPR,ISYTYP(1,LIST1A))
        DSSYT2A=IRPDPD(IRREPL,ISYTYP(1,LIST2A))
        NMSYT1A=IRPDPD(IRREPL,ISYTYP(2,LIST1A))
        NMSYT2A=IRPDPD(IRREPL,ISYTYP(2,LIST2A))
        DSSYT1B=IRPDPD(IRREPR,ISYTYP(1,LIST1B))
        DSSYT2B=IRPDPD(IRREPL,ISYTYP(1,LIST2B))
        NMSYT1B=IRPDPD(IRREPL,ISYTYP(2,LIST1B))
        NMSYT2B=IRPDPD(IRREPL,ISYTYP(2,LIST2B))
        DISSYG=IRPDPD(IRREPR,ISYTYP(1,LISTG))
        NUMSYG=IRPDPD(IRREPL,ISYTYP(2,LISTG))
C
C  FIRST TERM
C
        I000=1
        I010=I000+IINTFP*NUMSYG*DISSYG
        I020=I010+IINTFP*NMSYT1A*DSSYT1A
        I030=I020+IINTFP*NMSYT2A*DSSYT2A
        I040=I030+IINTFP*MAX(NMSYT1A*DSSYT1A,NMSYT2B*DSSYT2B)
        IF(MAXCOR.LT.I040) CALL INSMEM('R2L2RNG',I030,MAXCOR)
        CALL GETLST(ICORE(I020),1,NMSYT2A,1,IRREPL,LIST2A)
        IF(IUHF.EQ.0) THEN
         CALL SSCAL(NMSYT2A*DSSYT2A,TWO,ICORE(I020),1) 
         CALL GETLST(ICORE(I040),1,NMSYT2A,1,IRREPL,LIST2A+2)
         CALL SAXPY (NMSYT2A*DSSYT2A,ONEM,ICORE(I040),1,ICORE(I020),1)
        ENDIF
        CALL GETLST(ICORE(I010),1,NMSYT1A,1,IRREPL,LIST1A)
        IF(IUHF.EQ.0) THEN
         CALL SSCAL(NMSYT1A*DSSYT1A,TWO,ICORE(I010),1)  
         CALL GETLST(ICORE(I040),1,NMSYT1A,1,IRREPL,LIST1A+2)
         CALL SAXPY (NMSYT1A*DSSYT1A,ONEM,ICORE(I040),1,ICORE(I010),1)
        ENDIF
        CALL ZERO(ICORE(I000),DISSYG*NUMSYG)
        CALL XGEMM('N','N',DISSYG,NUMSYG,DSSYT2A,FACT,ICORE(I010),
     &             DSSYT1A,ICORE(I020),DSSYT2A,AZERO,ICORE(I000),
     &             DISSYG)
C
        IF(IUHF.EQ.1) THEN
C
C UHF CODE ONLY
C
        I020=I010+IINTFP*NMSYT1B*DSSYT1B
        I030=I020+IINTFP*NMSYT2B*DSSYT2B
        IF(MAXCOR.LT.I030) CALL INSMEM('R2L2RNG',I030,MAXCOR)
        CALL GETLST(ICORE(I020),1,NMSYT2B,1,IRREPL,LIST2B)
        CALL GETLST(ICORE(I010),1,NMSYT1B,1,IRREPL,LIST1B)
        CALL XGEMM('N','N',DISSYG,NUMSYG,DSSYT2B,FACT,ICORE(I010),
     &             DSSYT1B,ICORE(I020),DSSYT2B,ONE,ICORE(I000),
     &             DISSYG)
        ENDIF
C
C  SAVE ON LIST 416
C
        CALL PUTLST(ICORE(I000),1,NUMSYG,2,IRREPL,LISTG) 
C
C  EVALUATE FOR UHF AS WELL ABBA
C
        IF(IUHF.EQ.1) THEN
C
         DSSYT3A=IRPDPD(IRREPR,ISYTYP(1,LIST3A))
         DSSYT4A=IRPDPD(IRREPL,ISYTYP(1,LIST4A))
         NMSYT3A=IRPDPD(IRREPL,ISYTYP(2,LIST3A))
         NMSYT4A=IRPDPD(IRREPL,ISYTYP(2,LIST4A))
         DSSYT3B=IRPDPD(IRREPR,ISYTYP(1,LIST3B))
         DSSYT4B=IRPDPD(IRREPL,ISYTYP(1,LIST4B))
         NMSYT3B=IRPDPD(IRREPL,ISYTYP(2,LIST3B))
         NMSYT4B=IRPDPD(IRREPL,ISYTYP(2,LIST4B))
c         NMSYT4B=IRPDPD(IRREPR,ISYTYP(2,LIST4B))
         DISSYG2=IRPDPD(IRREPR,ISYTYP(1,LISTG2))
         NUMSYG2=IRPDPD(IRREPL,ISYTYP(2,LISTG2))
C
C  FIRST TERM
C
         I010=I000+IINTFP*DISSYG2*NUMSYG2
         I020=I010+IINTFP*NMSYT3A*DSSYT3A
         I030=I020+IINTFP*NMSYT4A*DSSYT4A
         IF(MAXCOR.LT.I030) CALL INSMEM('T2L2RNG',I030,MAXCOR)
         CALL ZERO(ICORE(I000),DISSYG2*NUMSYG2)
         CALL GETLST(ICORE(I020),1,NMSYT4A,1,IRREPL,LIST4A)
         CALL GETLST(ICORE(I010),1,NMSYT3A,1,IRREPL,LIST3A)
         CALL XGEMM('N','N',DISSYG2,NUMSYG2,DSSYT4A,FACT,ICORE(I010),
     &              DSSYT3A,ICORE(I020),DSSYT4A,AZERO,ICORE(I000),
     &              DISSYG2)
         I020=I010+IINTFP*NMSYT3B*DSSYT3B
         I030=I020+IINTFP*NMSYT4B*DSSYT4B
         IF(MAXCOR.LT.I030) CALL INSMEM('T2L2RNG',I030,MAXCOR)
         CALL GETLST(ICORE(I020),1,NMSYT4B,1,IRREPL,LIST4B)
         CALL GETLST(ICORE(I010),1,NMSYT3B,1,IRREPL,LIST3B)
         CALL XGEMM('N','N',DISSYG2,NUMSYG2,DSSYT4B,FACT,ICORE(I010),
     &              DSSYT3B,ICORE(I020),DSSYT4B,ONE,ICORE(I000),
     &              DISSYG2)
C
C  SAVE 417 ON DISK
C
         CALL PUTLST(ICORE(I000),1,NUMSYG2,2,IRREPL,LISTG2)
        ENDIF
200    CONTINUE   
C
      ELSEIF(SPCASE.EQ.'ABAB'.OR.SPCASE.EQ.'BABA')THEN
C
C DO SPIN CASES ABAB AND BABA:
C
C     H(MeIa) =  - SUM n,F L(Mn,Fe) T(In,Fa)
C
C  STORAGE  Em, Ai
C
       IF(SPCASE.EQ.'ABAB')THEN
        LISTG=418
        LIST1=439
        LIST2=39
        FACT=ONEM
       ELSEIF(SPCASE.EQ.'BABA')THEN
        LISTG=419
        FACT=ONEM
        LIST1=438
        LIST2=38
       ENDIF
       DO 300 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPX,IRREPR) 
        DSSYT1=IRPDPD(IRREPR,ISYTYP(1,LIST1))
        DSSYT2=IRPDPD(IRREPL,ISYTYP(1,LIST2))
        NMSYT1=IRPDPD(IRREPL,ISYTYP(2,LIST1))
        NMSYT2=IRPDPD(IRREPL,ISYTYP(2,LIST2))
        DISSYG=IRPDPD(IRREPR,ISYTYP(1,LISTG))
        NUMSYG=IRPDPD(IRREPL,ISYTYP(2,LISTG))
        I000=1
        I010=I000+IINTFP*DISSYG*NUMSYG
        I020=I010+IINTFP*NMSYT1*DSSYT1
        I030=I020+IINTFP*NMSYT2*DSSYT2
        IF(MAXCOR.LT.I030) CALL INSMEM('T2L2RNG',I030,MAXCOR)
        CALL ZERO(ICORE(I000),DISSYG*NUMSYG)
        CALL GETLST(ICORE(I020),1,NMSYT2,1,IRREPL,LIST2)
        CALL GETLST(ICORE(I010),1,NMSYT1,1,IRREPL,LIST1)
        CALL XGEMM('N','T',DISSYG,NUMSYG,NMSYT1,FACT,ICORE(I010),
     &             DSSYT1,ICORE(I020),DSSYT2,AZERO,ICORE(I000),DISSYG)
C
C SAVE H INTERMEDIATE ON DISK
C
        CALL PUTLST(ICORE(I000),1,NUMSYG,2,IRREPL,LISTG)
        IF(IUHF.EQ.0) THEN
         CALL GETLST(ICORE(I010),1,NUMSYG,1,IRREPL,LISTG-2)
         CALL SAXPY(NUMSYG*DISSYG,HALF,ICORE(I000),1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),1,NUMSYG,1,IRREPL,LISTG-2)
        ENDIF
C
C
300    CONTINUE
C
      ENDIF
      RETURN
      END
