      SUBROUTINE R1L2Y2F(R1,ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = P(ab) L(ab,in)*R(me)*W(jm,ne)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      DIMENSION ICORE(MAXCOR),R1(*),I0X(2)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
C
C FORM INTERMEDIATE X(jn) = R(me) * W(jm,ne)
C
      I0X(1)=1
      IF(IUHF.NE.0)THEN
       I0X(2)=I0X(1)+IINTFP*IRPDPD(IRREPX,21)
      ELSE
       I0X(2)=I0X(1)
      ENDIF
      I000=I0X(2)+IINTFP*IRPDPD(IRREPX,22)
      MXCOR = MAXCOR - I000 + 1
      LENIJ=IRPDPD(IRREPX,21)+IUHF*IRPDPD(IRREPX,22)
      CALL ZERO(ICORE(I0X(1)),LENIJ) 
      IOFF=1
      DO 1 ISPIN=1,1+IUHF
       CALL SYMTRA (IRREPX,VRT(1,ISPIN),POP(1,ISPIN),1,R1(IOFF),
     &              ICORE(I000))
       CALL SCOPY  (IRPDPD(IRREPX,8+ISPIN),ICORE(I000),1,R1(IOFF),1)
       IOFF=IOFF+IINTFP*IRPDPD(IRREPX,8+ISPIN)
1     CONTINUE 
      CALL DVOINOO(IRREPX,ICORE(I0X(1)),R1,ICORE(I000),MXCOR,IUHF,7)
      IOFF=1
      DO 2 ISPIN=1,1+IUHF
       CALL SYMTRA (IRREPX,POP(1,ISPIN),VRT(1,ISPIN),1,R1(IOFF),
     &              ICORE(I000))
       CALL SCOPY  (IRPDPD(IRREPX,8+ISPIN),ICORE(I000),1,R1(IOFF),1)
       IOFF=IOFF+IINTFP*IRPDPD(IRREPX,8+ISPIN)
2     CONTINUE
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE.
C
C   Y(Ab,Ij) = Q(Ab,Ij) + Q(Ba,Ji)
C
C WHERE
C
C   Q(ab,ij) = L(Ab,In) * R(em)* [2W(Jm,Ne) - W(Mj,Ne)]
C
C            = L(Ab,In) * X(nj)
C
C
C
       LISTL=446
       LISTZ=63
       DO 10 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        MAXSIZ=MAX(DISSYL,NUMDSL,DISSYZ,NUMDSZ)
        I010=I000+IINTFP*MAX(DISSYL*NUMDSL,DISSYZ*NUMDSZ,3*MAXSIZ)
        I020=I010+IINTFP*MAX(DISSYL*NUMDSL,DISSYZ*NUMDSZ)
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXSIZ
        ITMP3=ITMP2+IINTFP*MAXSIZ
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
C
C                            +
C Y(Ab,Ij) = L(Ab,In) * X(jn)
C
        DO 20 IRREPN=1,NIRREP
         IRREPI=DIRPRD(IRREPN,IRREPR)
         IRREPJ=DIRPRD(IRREPN,IRREPX)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,1)
         NUMN=POP(IRREPN,1)
         NROW=DISSYL*NUMI
         NCOL=NUMJ
         NSUM=NUMN
         IOFFL=I000+IINTFP*(ISYMOFF(IRREPN,IRREPR,21)-1)*DISSYL
         IOFFZ=I010+IINTFP*(ISYMOFF(IRREPJ,IRREPL,21)-1)*DISSYZ
         IOFFX=I0X(1) +IINTFP*(ISYMOFF(IRREPN,IRREPX,21)-1)
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),NROW,
     &              ICORE(IOFFX),NCOL,ZILCH,ICORE(IOFFZ),NROW)
20      CONTINUE
C
        CALL SYMRHF(IRREPL,VRT(1,1),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
C
10     CONTINUE
C
      ELSE
C  
C
C UHF:
C 
C Y1(AB,IJ) = L(AB,IN)*X(JN) + ANTISYMMETRIZATION
C Y1(Ab,Ij) = L(Ab,In)*X(jn) + L(Ab,Nj)*X(IN)
C
C
C AAAA AND BBBB:
C
       DO 100 ISPIN=1,2
        DO 110 IRREPR=1,NIRREP
         LISTL=443+ISPIN
         LISTZ=60+ISPIN
         IRREPL=DIRPRD(IRREPR,IRREPX)
         DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
         NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
         NUMDSLX=IRPDPD(IRREPR,20+ISPIN)
         DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
         NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
         NUMDSZX=IRPDPD(IRREPL,20+ISPIN)
         I010=I000+IINTFP*MAX(DISSYL*NUMDSLX,DISSYZ*NUMDSZX)
         I020=I010+IINTFP*MAX(DISSYL*NUMDSLX,DISSYZ*NUMDSZX)
         CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
         CALL SYMEXP(IRREPR,POP(1,ISPIN),DISSYL,ICORE(I000))
C                           
C Y(AB,IJ) = L(AB,IN)*X(JN)
C
         DO 120 IRREPJ=1,NIRREP
          IRREPN=DIRPRD(IRREPJ,IRREPX)
          IRREPI=DIRPRD(IRREPN,IRREPR)
          NUMN=POP(IRREPN,ISPIN)
          NUMI=POP(IRREPI,ISPIN)
          NUMJ=POP(IRREPJ,ISPIN)
          NROW=DISSYL*NUMI
          NCOL=NUMJ
          NSUM=NUMN
          IT=20+ISPIN
          IOFFL=I000+IINTFP*(ISYMOFF(IRREPN,IRREPR,IT)-1)*DISSYL
          IOFFZ=I010+IINTFP*(ISYMOFF(IRREPJ,IRREPL,IT)-1)*DISSYL
          IOFFX=I0X(ISPIN)+IINTFP*(ISYMOFF(IRREPN,IRREPX,IT)-1)
          CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),NROW,
     &               ICORE(IOFFX),NCOL,ZILCH,ICORE(IOFFZ),NROW)
120      CONTINUE
C
         CALL ASSYM2(IRREPL,POP(1,ISPIN),DISSYZ,ICORE(I010))
         CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
         CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
C
110     CONTINUE
C
100    CONTINUE
C
C ABAB
C
C Y1(Ab,Ij) = L(Ab,In)*X(jn) + L(Ab,Nj)*X(IN)
C
       LISTL=446
       LISTZ=63
       DO 210 IRREPR=1,NIRREP
C
C Y1(Ab,Ij) <= L(Ab,In)*X(jn)
C
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYL=IRPDPD(IRREPL,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPR,ISYTYP(2,LISTL))
        DISSYZ=IRPDPD(IRREPL,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPL,ISYTYP(2,LISTZ))
        MAXSIZ=MAX(DISSYL,NUMDSL,DISSYZ,NUMDSZ)
        I010=I000+IINTFP*MAX(DISSYL*NUMDSL,DISSYZ*NUMDSZ)
        I020=I010+IINTFP*MAX(DISSYL*NUMDSL,DISSYZ*NUMDSZ)
        ITMP1=I020
        ITMP2=ITMP1+IINTFP*MAXSIZ
        ITMP3=ITMP2+IINTFP*MAXSIZ
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPR,LISTL)
C
C Y1(Ab,Ij) <= L(Ab,In)*X(jn)
C
        DO 220 IRREPJ=1,NIRREP
         IRREPN=DIRPRD(IRREPJ,IRREPX)
         IRREPI=DIRPRD(IRREPN,IRREPR)
         NUMN=POP(IRREPN,2)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
         NROW=DISSYL*NUMI
         NCOL=NUMJ
         NSUM=NUMN
         IOFFL=I000+IINTFP*(ISYMOFF(IRREPN,IRREPR,14)-1)*DISSYL
         IOFFZ=I010+IINTFP*(ISYMOFF(IRREPJ,IRREPL,14)-1)*DISSYL
         IOFFX=I0X(2)+IINTFP*(ISYMOFF(IRREPN,IRREPX,22)-1)
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),NROW,
     &              ICORE(IOFFX),NCOL,ZILCH,ICORE(IOFFZ),NROW)
220     CONTINUE
C
C Y1(Ab,Ij) -> Y1(Ab,jI)
C L(Ab,Ij)  ->  L(Ab,jI)
C
        CALL SYMTR1(IRREPL,POP(1,1),POP(1,2),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL SYMTR1(IRREPR,POP(1,1),POP(1,2),DISSYL,ICORE(I000),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
C
C Y1(Ab,jI) <= L(Ab,jN)*X(IN)
C
        DO 230 IRREPI=1,NIRREP
         IRREPN=DIRPRD(IRREPI,IRREPX)
         IRREPJ=DIRPRD(IRREPN,IRREPR)
         NUMN=POP(IRREPN,1)
         NUMI=POP(IRREPI,1)
         NUMJ=POP(IRREPJ,2)
         NROW=DISSYL*NUMJ
         NCOL=NUMI
         NSUM=NUMN
         IOFFL=I000+IINTFP*(ISYMOFF(IRREPN,IRREPR,24)-1)*DISSYL
         IOFFZ=I010+IINTFP*(ISYMOFF(IRREPI,IRREPL,24)-1)*DISSYL
         IOFFX=I0X(1)+IINTFP*(ISYMOFF(IRREPN,IRREPX,21)-1)
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFL),NROW,
     &              ICORE(IOFFX),NCOL,ONE,ICORE(IOFFZ),NROW)
230     CONTINUE
C
C Y1(Ab,jI) -> Y(Ab,Ij)
C
        CALL SYMTR1(IRREPL,POP(1,2),POP(1,1),DISSYZ,ICORE(I010),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPL,LISTZ)
C
210    CONTINUE
C
      ENDIF
C
      RETURN
      END    
