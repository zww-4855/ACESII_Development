      SUBROUTINE R2L1Y1A(XI1,L1,ICORE,MAXCOR,IUHF)
C
C XI1A(ai) = L(em)*R(mn,ef)*W(ni,fa)
C
C SPIN CASES:
C
C XI1A(AI) = L(EM)*R(MN,EF)*W(NI,FA)+L(EM)*R(Mn,Ef)*W(nI,fA)+
C           L(em)*R(mN,eF)*W(NI,FA)+L(em)*R(mn,ef)*W(nI,fA)
C
C         = Q(NF)*W(NI,FA) + Q(nf)*W(nI,fA)
C
C    WHERE  Q(NF) = L(EM)*R(MN,EF) + L(em)*R(mN,eF)
C           Q(nf) = L(em)*R(mn,ef) + L(EM)*R(Mn,Ef)
C
C ONLY SPIN-ADAPTED RHF CONTRACTIONS ARE CODED THUS FAR
C  
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,TWO
      DIMENSION ICORE(MAXCOR),XI1(*),L1(*)
      COMMON/STATSYM/IRREPX
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,ONEM,TWO,ZILCH/1.0D0,-1.0D0,2.0D0,0.0D0/
C
      IF(IUHF.EQ.0)THEN
       I0Q=1
       I000=I0Q+IINTFP*NT(1)
C
C SPIN ADAPTED RHF CODE.
C
C  XI1(AI) = L(EM)*[2R(Mn,Ef)-R(Nm,Ef)]*[2W(Ni,Fa)-W(In,Fa)]
C
C FIRST FORM 2*R(Mn,Ef) - R(Nm,Ef) AND PUT IN R(EM,fn) MATRIX
C
       LISTR1=457
       LISTR2=459
       DISSYR=IRPDPD(IRREPX,ISYTYP(1,LISTR1))
       NUMDSR=IRPDPD(1,ISYTYP(2,LISTR1))
       I010=I000+IINTFP*DISSYR*NUMDSR
       I020=I010+IINTFP*DISSYR*NUMDSR
       IF(I020.GE.MAXCOR) CALL INSMEM('R2L1Y1A1',I020,MAXCOR)
       CALL GETLST(ICORE(I000),1,NUMDSR,1,1,LISTR1)
       CALL GETLST(ICORE(I010),1,NUMDSR,1,1,LISTR2)
       CALL SSCAL (NUMDSR*DISSYR,TWO,ICORE(I000),1)
       CALL SAXPY (NUMDSR*DISSYR,ONEM,ICORE(I010),1,ICORE(I000),1)
C
C                 _
C CALCULATE L(EM)*R(EM,fn) = Q(fn)
C
       CALL XGEMM('N','N',1,NUMDSR,DISSYR,ONE,L1,
     &            1,ICORE(I000),DISSYR,ZILCH,ICORE(I0Q),1)
C
C NOW FORM 2*W(Ni,Fa) - W(In,Fa) AND PUT IN W(FN,ai) MATRIX
C
       LISTW1=18
       LISTW2=21
       DISSYW=IRPDPD(1,ISYTYP(1,LISTW1))
       NUMDSW=IRPDPD(1,ISYTYP(2,LISTW1))
       I010=I000+IINTFP*DISSYW*NUMDSW
       I020=I010+IINTFP*DISSYW*NUMDSW
       IF(I020.GE.MAXCOR) CALL INSMEM('R2L1Y1A2',I020,MAXCOR)
       CALL GETLST(ICORE(I000),1,NUMDSW,1,1,LISTW1)
       CALL GETLST(ICORE(I010),1,NUMDSW,1,1,LISTW2)
       CALL SSCAL (NUMDSW*DISSYW,TWO,ICORE(I000),1)
       CALL SAXPY (NUMDSW*DISSYW,ONEM,ICORE(I010),1,ICORE(I000),1)
C
C                           _
C CALCULATE Z(AI) = Q(fn) * W(FN,ai)
C
       CALL XGEMM('N','N',1,NUMDSW,DISSYW,ONE,ICORE(I0Q),
     &            1,ICORE(I000),DISSYW,ONE,XI1,1)
C
      ELSE
C
       IOFFQ=1
       I000=IOFFQ+IINTFP*(NT(1)+NT(2))
       DO 10 ISPIN=1,2
C
C CALCULATE Q INTERMEDIATE
C
C     Q(NF) = L(EM)*R(MN,EF) + L(em)*R(mN,eF)
C     Q(nf) = L(em)*R(mn,ef) + L(EM)*R(Mn,Ef)
C
        LISTRAA=453+ISPIN
        DISSYR=IRPDPD(IRREPX,ISYTYP(1,LISTRAA))
        NUMDSR=IRPDPD(1,ISYTYP(2,LISTRAA))
        IEND=I000+NUMDSR*DISSYR
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L1Y1A3',IEND,MAXCOR)
        IOFFL=1+(ISPIN-1)*IRPDPD(IRREPX,9)*IINTFP
        CALL GETLST(ICORE(I000),1,NUMDSR,1,1,LISTRAA)
        CALL XGEMM('N','N',1,NUMDSR,DISSYR,ONEM,L1(IOFFL),1,
     &             ICORE(I000),DISSYR,ZILCH,ICORE(IOFFQ),1)
        LISTRAB=455+ISPIN
        DISSYR=IRPDPD(IRREPX,ISYTYP(1,LISTRAB))
        NUMDSR=IRPDPD(1,ISYTYP(2,LISTRAB))
        IEND=I000+NUMDSR*DISSYR
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L1Y1A4',IEND,MAXCOR)
        IOFFL=1+(2-ISPIN)*IRPDPD(IRREPX,9)*IINTFP
        CALL GETLST(ICORE(I000),1,NUMDSR,1,1,LISTRAB)
        CALL XGEMM('N','N',1,NUMDSR,DISSYR,ONE,L1(IOFFL),1,
     &             ICORE(I000),DISSYR,ONE,ICORE(IOFFQ),1)
        IOFFQ=IOFFQ+IINTFP*NT(ISPIN)
10     CONTINUE
C
C EVALUATE CONTRIBUTION TO XI:
C
C         Q(NF)*W(NI,FA) + Q(nf)*W(nI,fA) 
C         Q(nf)*W(ni,fa) + Q(NF)*W(Ni,Fa)
C 
       IOFFXI=1
       DO 20 ISPIN=1,2
        LISTWAA=18+ISPIN
        DISSYW=IRPDPD(1,ISYTYP(1,LISTWAA))
        NUMDSW=IRPDPD(1,ISYTYP(2,LISTWAA))
        IEND=I000+NUMDSW*DISSYW
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L1Y1A5',IEND,MAXCOR)
        IOFFQ=1+(ISPIN-1)*NT(1)*IINTFP
        CALL GETLST(ICORE(I000),1,NUMDSW,1,1,LISTWAA)
        CALL XGEMM('N','N',1,NUMDSW,DISSYW,ONE,ICORE(IOFFQ),1,
     &             ICORE(I000),DISSYW,ONE,XI1(IOFFXI),1)
        LISTWAB=16+ISPIN
        DISSYW=IRPDPD(1,ISYTYP(1,LISTWAB))
        NUMDSW=IRPDPD(1,ISYTYP(2,LISTWAB))
        IEND=I000+NUMDSW*DISSYW
        IF(IEND.GE.MAXCOR) CALL INSMEM('R2L1Y1A6',IEND,MAXCOR)
        IOFFQ=1+(2-ISPIN)*NT(1)*IINTFP
        CALL GETLST(ICORE(I000),1,NUMDSW,1,1,LISTWAB)
        CALL XGEMM('N','N',1,NUMDSW,DISSYW,ONE,ICORE(IOFFQ),1,
     &             ICORE(I000),DISSYW,ONE,XI1(IOFFXI),1)
        IOFFXI=IOFFXI+IINTFP*NT(ISPIN)
20     CONTINUE
C        
      ENDIF
C
      RETURN
      END
