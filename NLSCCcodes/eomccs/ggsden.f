      SUBROUTINE GGSDEN(IRREPX,DENOO,DENVV,DENVO,DENOV,
     &                 LSTL1OFF,LISTL1,LSTT1OFF,LISTT1,
     &                 LISTL2,LISTT2,LSTGRL,LSTGRLOF,
     &                 LZERO,ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE CALCULATES ELEMENTS OF THE GENERAL REDUCED DENSITY
C MATRIX
C
C  D(pq) = <0|Z exp(-T) p q exp(T)|0> 
C
C WHERE Z IS AN ARBITRARY OPERATOR.  IN THE CASE THAT
C Z=LAMBDA, THIS GIVES THE USUAL CC DENSITY
C MATRIX, BUT OTHER CHOICES OF Z ARE REQUIRED IN, E.G. EOM-CC
C UNRELAXED GRADIENTS OR E.G. FOR TRANSITION DENSITIES.
C
CEND
C 
C J.G. DEC/93 IN AUSTIN
C
C MODIFIED VERSION OF GSDEN
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION DENOO,DENVO,DENOV,DENVV
      DOUBLE PRECISION ONE,ONEM,ZILCH,TWO,LZERO
      DIMENSION DENOO(*),DENVO(*),DENVV(*),DENOV(*),ICORE(MAXCOR)
      LOGICAL MBPT2,CC,CCD,RCCD,DRCCD,LCCD,LCCSD,CC2
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYMLOC/ISYMOFF(8,8,25)
      COMMON /REFTYPE/ MBPT2,CC,CCD,RCCD,DRCCD,LCCD,LCCSD,CC2
C
      DATA ONE,ONEM,ZILCH,TWO /1.0D0,-1.0D0,0.0D0,2.0D0/
C
C ALLOCATE MEMORY FOR DENSITY MATRICES AND INTERMEDIATE
C QUANTITITES USED IN THIS ROUTINE
C
      LENOO=IRPDPD(IRREPX,21)+IUHF*IRPDPD(IRREPX,22)
      LENVV=IRPDPD(IRREPX,19)+IUHF*IRPDPD(IRREPX,20)
      IT1=1
      IZOO=IT1+IINTFP*(IRPDPD(1,9)+IUHF*IRPDPD(1,10))
      IZVV=IZOO+LENOO*IINTFP
      I000=IZVV+LENVV*IINTFP
      MXCOR=MAXCOR-I000+1
C
      CALL GFORMG(IRREPX,1,LISTL2,LISTT2,LSTGRL,ICORE(I000),MXCOR,
     &            LSTGRLOF,ONEM,ONEM,IUHF)
C
C INITIALIZE DVO AND DOV WITH T1 AND L1 AND PUT T2*L2 PARTS INTO
C   DIJ AND DAB
C
      DO 10 ISPIN=1,1+IUHF
       IOFFOO=(ISPIN-1)*IRPDPD(IRREPX,21)
       IOFFVV=(ISPIN-1)*IRPDPD(IRREPX,19)
       IOFFVOT=(ISPIN-1)*IRPDPD(1,9)
       IOFFVOL=(ISPIN-1)*IRPDPD(IRREPX,9)
       CALL GETLST(ICORE(IT1+IINTFP*IOFFVOT),1,1,1,
     &             ISPIN+LSTT1OFF,LISTT1)
c       IF(IRREPX.EQ.1) THEN
c        CALL SCOPY(NT(ISPIN),ICORE(IT1+IINTFP*IOFFVOT),1,
c     &             DENVO(1+IOFFVOL),1)
c        CALL SSCAL(NT(ISPIN),LZERO,DENVO(1+IOFFVOL),1)
c       ENDIF
       CALL GETLST(DENOV(1+IOFFVOL),1,1,1,ISPIN+LSTL1OFF,LISTL1)
       CALL GETLST(DENOO(1+IOFFOO),1,1,1,ISPIN+LSTGRLOF,LSTGRL+91)
       CALL GETLST(DENVV(1+IOFFVV),1,1,1,ISPIN+LSTGRLOF,LSTGRL+92)
C
C CALCULATE INTERMEDIATES
C
C  Z(ij) = T(EI)*L(EJ)   ;  Z(AB) = T(AM)*L(BM)
C
       DO 15 IRREPJ=1,NIRREP
        IRREPI=DIRPRD(IRREPJ,IRREPX)
        IRREPE=IRREPI
        NOCCJ=POP(IRREPJ,ISPIN)
        NOCCI=POP(IRREPI,ISPIN)
        NVRTE=VRT(IRREPE,ISPIN)
        IOFFT=IT1+IINTFP*(IOFFVOT+ISYMOFF(IRREPI,1,8+ISPIN)-1)
        IOFFL=1+IOFFVOL+ISYMOFF(IRREPJ,IRREPX,8+ISPIN)-1
        IOFFZOO=IZOO+IINTFP*(IOFFOO+ISYMOFF(IRREPJ,IRREPX,20+ISPIN)-1)
        CALL XGEMM('T','N',NOCCI,NOCCJ,NVRTE,ONE,ICORE(IOFFT),NVRTE,
     &             DENOV(IOFFL),NVRTE,ZILCH,ICORE(IOFFZOO),NOCCI)
        IRREPM=IRREPJ
        IRREPB=DIRPRD(IRREPX,IRREPM)
        IRREPA=IRREPM
        NVRTA=VRT(IRREPA,ISPIN)
        NVRTB=VRT(IRREPB,ISPIN)
        NOCCM=POP(IRREPM,ISPIN)
        IOFFT=IT1+IINTFP*(IOFFVOT+ISYMOFF(IRREPM,1,8+ISPIN)-1)
        IOFFL=1+IOFFVOL+ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1
        IOFFZVV=IZVV+IINTFP*(IOFFVV+ISYMOFF(IRREPB,IRREPX,18+ISPIN)-1)
        CALL XGEMM('N','T',NVRTA,NVRTB,NOCCM,ONE,ICORE(IOFFT),NVRTA,
     &             DENOV(IOFFL),NVRTB,ZILCH,ICORE(IOFFZVV),NVRTA)
15     CONTINUE
C
C FORM COMPLETE OCC-OCC DENSITY
C
       CALL SAXPY(IRPDPD(IRREPX,20+ISPIN),ONEM,
     &            ICORE(IZOO+IOFFOO*IINTFP),1,
     &            DENOO(1+IOFFOO),1)
C
C CALCULATE CONTRIBUTIONS TO VRT-OCC DENSITY COMING FROM Z 
C AND T2*L2 TERMS
C
       DO 20 IRREPI=1,NIRREP
        IRREPJ=DIRPRD(IRREPX,IRREPI)
        IRREPA=IRREPJ
        NOCCI=POP(IRREPI,ISPIN)
        NOCCJ=POP(IRREPJ,ISPIN)
        NVRTA=VRT(IRREPA,ISPIN)
        IF (CC2) THEN

        IOFFT1=IT1+IINTFP*(IOFFVOT+ISYMOFF(IRREPJ,1,8+ISPIN)-1)
        IOFFZOO=IZOO+IINTFP*(ISYMOFF(IRREPJ,IRREPX,20+ISPIN)-1)
        IOFFDVO=1+IOFFVOL+ISYMOFF(IRREPI,IRREPX,8+ISPIN)-1
        CALL XGEMM('N','T',NVRTA,NOCCI,NOCCJ,ONEM,ICORE(IOFFT1),NVRTA,
     &             ICORE(IOFFZOO),NOCCI,ONE,DENVO(IOFFDVO),NVRTA)
        ELSE

        IOFFT1=IT1+IINTFP*(IOFFVOT+ISYMOFF(IRREPJ,1,8+ISPIN)-1)
        IOFFDOO=1+IOFFOO+ISYMOFF(IRREPJ,IRREPX,20+ISPIN)-1
        IOFFDVO=1+IOFFVOL+ISYMOFF(IRREPI,IRREPX,8+ISPIN)-1
        CALL XGEMM('N','T',NVRTA,NOCCI,NOCCJ,ONE,ICORE(IOFFT1),NVRTA,
     &             DENOO(IOFFDOO),NOCCI,ONE,DENVO(IOFFDVO),NVRTA)
        IRREPA=DIRPRD(IRREPX,IRREPI)
        IRREPB=IRREPI
        NOCCI=POP(IRREPI,ISPIN)
        NVRTA=VRT(IRREPA,ISPIN) 
        NVRTB=VRT(IRREPB,ISPIN)
        IOFFT1=IT1+IINTFP*(IOFFVOT+ISYMOFF(IRREPI,1,8+ISPIN)-1)
        IOFFDVV=1+IOFFVV+ISYMOFF(IRREPB,IRREPX,18+ISPIN)-1
        IOFFDVO=1+IOFFVOL+ISYMOFF(IRREPI,IRREPX,8+ISPIN)-1
        CALL XGEMM('N','N',NVRTA,NOCCI,NVRTB,ONEM,DENVV(IOFFDVV),NVRTA,
     &             ICORE(IOFFT1),NVRTB,ONE,DENVO(IOFFDVO),NVRTA)

       ENDIF 
20     CONTINUE
C
C FORM COMPLETE VRT-VRT DENSITY
C
       CALL SAXPY(IRPDPD(IRREPX,18+ISPIN),ONE,
     &            ICORE(IZVV+IOFFVV*IINTFP),1,
     &            DENVV(1+IOFFVV),1)
C
10    CONTINUE
C
C NOW CALCULATE T(AE,IM)*L(EM) CONTRIBUTION TO DAI
C
C   T(AI,EM)*L(EM) + T(AI,em)*L(em)  [ISPIN=1]
C
C   T(ai,em)*L(em) + T(ai,EM)*L(EM)  [ISPIN=2]
C
C  2T(Ae,Im)-T(Ea,Im) * L(AI)        [SPIN-ADAPTED RHF]
C
      DO 11 ISPIN=1,1+IUHF
       IOFFVO=(ISPIN-1)*IRPDPD(IRREPX,9)
       IF(IUHF.EQ.0)THEN
        LISTT=37
        DISSYT=IRPDPD(IRREPX,ISYTYP(1,LISTT))
        NUMDST=IRPDPD(IRREPX,ISYTYP(2,LISTT))
        I010=I000+DISSYT*NUMDST*IINTFP
        I020=I010+DISSYT*NUMDST*IINTFP
        CALL GETLST(ICORE(I000),1,NUMDST,1,IRREPX,37)
        CALL GETLST(ICORE(I010),1,NUMDST,1,IRREPX,39)
        CALL SSCAL (DISSYT*NUMDST,TWO,ICORE(I000),1)
        CALL SAXPY (DISSYT*NUMDST,ONEM,ICORE(I010),1,ICORE(I000),1)
        CALL XGEMM ('N','N',DISSYT,1,NUMDST,ONE,ICORE(I000),
     &              DISSYT,DENOV(1+IOFFVO),NUMDST,ONE,DENVO(1+IOFFVO),
     &              DISSYT)
       ELSE
        LISTAA=33+ISPIN
        LISTAB=38-ISPIN
        DISSYT=IRPDPD(IRREPX,ISYTYP(1,LISTAA))
        NUMDST=IRPDPD(IRREPX,ISYTYP(2,LISTAA))
        CALL GETLST(ICORE(I000),1,NUMDST,1,IRREPX,LISTAA)
        CALL XGEMM('N','N',DISSYT,1,NUMDST,ONEM,ICORE(I000),
     &             DISSYT,DENOV(1+IOFFVO),NUMDST,ONE,
     &             DENVO(1+IOFFVO),DISSYT)
        DISSYT=IRPDPD(IRREPX,ISYTYP(1,LISTAB))
        NUMDST=IRPDPD(IRREPX,ISYTYP(2,LISTAB))
        CALL GETLST(ICORE(I000),1,NUMDST,1,IRREPX,LISTAB)
        CALL XGEMM('N','N',DISSYT,1,NUMDST,ONE,ICORE(I000),
     &             DISSYT,DENOV(1+(2-ISPIN)*IRPDPD(IRREPX,9)),NUMDST,
     &             ONE,DENVO(1+IOFFVO),DISSYT)
       ENDIF
C
11    CONTINUE
C
      RETURN
      END
