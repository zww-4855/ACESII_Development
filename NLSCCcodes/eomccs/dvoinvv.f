      SUBROUTINE DVOINVV(IRREPX,IVV,DVO,ICORE,MXCOR,IUHF,LISTW0)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS:
C
C IVV(A,B) =  SUM [D(M,E) * <AM||BE> + D(m,e) * <Am|Be>] (ISPIN=1)
C                                              
C
C IVV(a,b) =  SUM [D(m,e) * <am||be> + D(M,E) * <Ma|Eb>] (ISPIN=2)
C                                             
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION IVV,DVO,ONE
      DIMENSION DVO(1),IVV(1),ICORE(MXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      DATA ONE /1.0/
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE
C IVV(A,B) = -  SUM [D(M,E) * <AM||BE> + D(m,e) * <Am|Be>] (ISPIN=1)
C                                              
C      
       IOFFI=1
       LISTW=LISTW0-1+4
       LENTAR=IRPDPD(IRREPX,19)
       DO 120 IRREP=1,NIRREP
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,LISTW))
        I000=1
        I010=I000+IINTFP*LENTAR
        I020=I010+IINTFP*NUMDIS*DISSIZ
        I030=I020+IINTFP*NUMDIS
        I040=I030+IINTFP*NUMDIS
        CALL IZERO(ICORE(I000),IINTFP*LENTAR)
        IF(I040.LE.MXCOR)THEN
         CALL GETLST(ICORE(I010),1,NUMDIS,1,IRREP,LISTW)
         CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,NUMDIS,ICORE(I010),
     &                ICORE(I020),ICORE(I030))
         CALL DDOT24(IRREPX,IRREP,ICORE(I000),DVO,ICORE(I010),
     &              ICORE(I020),DISSIZ,VRT(1,1),VRT(1,1),
     &              VRT(1,1),VRT(1,1),VRT(1,1),POP(1,1),'TSTS')
        ELSE
         CALL DDOT24X(IRREP,ICORE(I000),DVO,ICORE(I010),
     &                VRT(1,1),VRT(1,1),VRT(1,1),POP(1,1),LISTW,
     &                19,9,19,'TSTS',IRREPX,IRREPX,1,.TRUE.,.FALSE.)
        ENDIF
        CALL SAXPY(LENTAR,ONE,ICORE(I000),1,IVV(IOFFI),1)
120    CONTINUE
      ELSEIF(IUHF.NE.0)THEN
       DO 10 ISPIN=1,1+IUHF
        LISTW1=LISTW0-1+ISPIN
        LISTW2=LISTW0-1+5-ISPIN
        LENTAR=IRPDPD(IRREPX,18+ISPIN)   
        LEND  =IRPDPD(IRREPX,8+ISPIN)
        DO 20 IRREP=1,NIRREP
         NDSZ1=IRPDPD(IRREP,ISYTYP(1,LISTW1))
         NDIS1=IRPDPD(IRREP,ISYTYP(2,LISTW1))
         NDSZ1F=IRPDPD(IRREP,18+ISPIN)
         NDSZ2=IRPDPD(IRREP,ISYTYP(1,LISTW2))
         NDIS2=IRPDPD(IRREP,ISYTYP(2,LISTW2))
         IOFFI=1+(ISPIN-1)*IRPDPD(IRREPX,19)
C
C DO FIRST PART   D(M,E) * <AM||BE>  (ISPIN=1)
C                 D(m,e) * <am||be>  (ISPIN=2)
C
C AND KEEP RESULT AT BOTTOM OF CORE
C
         I000=1
         I010=I000+IINTFP*LENTAR
         I020=I010+IINTFP*NDSZ1F*NDIS1
         I030=I020+IINTFP*LEND
         CALL IZERO(ICORE(I000),LENTAR*IINTFP)
         IOFFD=1+(ISPIN-1)*IRPDPD(IRREPX,9)
         IF(I030.LE.MXCOR)THEN
          CALL GETLST(ICORE(I010),1,NDIS1,2,IRREP,LISTW1)
          CALL SYMEXP2(IRREP,VRT(1,ISPIN),NDSZ1F,NDSZ1,NDIS1,
     &                 ICORE(I010),ICORE(I010))
          CALL DDOT24(IRREPX,IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &               ICORE(I020),NDSZ1F,VRT(1,ISPIN),VRT(1,ISPIN),
     &               VRT(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),
     &               POP(1,ISPIN),'TSTS')
         ELSE
          CALL DDOT24X(IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &                 VRT(1,ISPIN),VRT(1,ISPIN),
     &                 VRT(1,ISPIN),POP(1,ISPIN),LISTW1,
     &                 18+ISPIN,8+ISPIN,18+ISPIN,'TSTS',
     &                 IRREPX,IRREPX,1,.FALSE.,.TRUE.)
         ENDIF
C
C NOW DO SECOND PART 
C
C         D(m,e) * <Am|Be> (ISPIN = 1)
C         D(M,E) * <Ma|Eb> (ISPIN = 2)
C
         I020=I010+IINTFP*NDSZ2*NDIS2
         I030=I020+IRPDPD(IRREPX,11-ISPIN)
         IOFFD=1+(2-ISPIN)*IRPDPD(IRREPX,9)
         IF(I030.LE.MXCOR)THEN
          CALL GETLST(ICORE(I010),1,NDIS2,2,IRREP,LISTW2)
          IF(ISPIN.EQ.1)THEN
           CALL DDOT24(IRREPX,IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &                ICORE(I020),NDSZ2,VRT(1,1),VRT(1,1),
     &                VRT(1,1),VRT(1,2),VRT(1,1),POP(1,2),'TSTS')
          ELSE
           CALL DDOT24(IRREPX,IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &                ICORE(I020),NDSZ2,VRT(1,2),VRT(1,2),
     &                VRT(1,1),VRT(1,2),POP(1,1),VRT(1,2),'STST')
          ENDIF
         ELSE
          IF(ISPIN.EQ.1)THEN
           CALL DDOT24X(IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &                  VRT(1,1),VRT(1,2),VRT(1,1),POP(1,2),LISTW2,
     &                  19,10,13,'TSTS',
     &                  IRREPX,IRREPX,1,.FALSE.,.FALSE.)
          ELSE
           CALL DDOT24X(IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &                  VRT(1,1),VRT(1,2),POP(1,1),VRT(1,2),LISTW2,
     &                  20,9,13,'STST',
     &                  IRREPX,IRREPX,1,.FALSE.,.FALSE.)
          ENDIF
         ENDIF
C
C NOW FORM TARGET(I,J)+TARGET(J,I) [i,j;j,i] AND DECREMENT IVV.
C
         CALL SAXPY(LENTAR,ONE,ICORE(I000),1,IVV(IOFFI),1)
20      CONTINUE
10     CONTINUE
      ENDIF
      RETURN
      END
