










      SUBROUTINE DIAGR(BUF3, BUF, MAXORD, IEVAL, IEVEC, IEXCP,
     $   IEVALSEL,EXCPTHRS, PRINT2)
C
C THE SUBSPACE EIGENVALUES AND EIGENVECTORS ARE DETERMINED
C IEVAL AND IEVEC DETERMINE THE STARTING POINT IN BUF FOR THE EIGENVALUES
C AND EIGENVECTORS RESPECTIVELY, IEXCP IS THE STARTING POINT FOR
C OVERLAPS WITH EXCITATION PATTERN
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL PRINT2, FIRST
C
      DIMENSION BUF(MAXORD*(2*MAXORD+3)+5)
      DIMENSION BUF3(MAXORD*MAXORD)
C
      COMMON/EXTINF/NDIMR,IOLDEST
      COMMON/EXTRAP/MAXEXP,NREDUCE,NTOL,NSIZEC
      COMMON/RMAT/ R(10000)
      COMMON/PMAT/ P(10000)
      COMMON/FLAGS/IFLAGS(100)
      COMMON/PROJECT/IPROJECT, IPATTERN, NCALC, ICALC, IWINDOW(8)
C
      CALL BLKCPY2(R,MAXORD,MAXORD,BUF3,NDIMR,NDIMR,1,1)
      IF(PRINT2)THEN
         WRITE(6,1100)
 1100    FORMAT(T3,/,'@NEXTDAV-I, Current subspace Hamiltonian: ')
         CALL OUTPUT(BUF3,1,NDIMR,1,NDIMR,NDIMR,NDIMR,1)
      ENDIF
C
      I000=1
      IEVAL = I000
      I010=I000+NDIMR
      IEXCP = I010
      I020=I010+NDIMR
      IEVEC=I020
      I030=I020+NDIMR*NDIMR
      IEVALSEL = I030
      I040=I030+MAXORD*MAXORD
      I050=I040+NDIMR
cmn      CALL RG(NDIMR,NDIMR,BUF3,BUF(I000),BUF(I010),1,BUF(I020),
cmn     &         BUF(I030),BUF(I040),IERR)
c
      CALL MN_GEEV(NDIMR,NDIMR,BUF3,BUF(I000),BUF(I010),BUF(I020),
     &             BUF(I030),MAXORD*MAXORD,IERR)
      CALL AFTERRG(NDIMR,BUF(I000),BUF(I010),BUF(I020),BUF(I030),
     &             BUF(I040),0)
C
C  EXCLUDE EIGENVALUES WITH 'LARGE' IMAGINARY COMPONENTS FROM
C  THE SET OF SEARCHED FOR EIGENVALUES
C
      FIRST = .TRUE.
      DO 5 I = 1, NDIMR
         IF (ABS(BUF(I010+I-1)) .GT. 1.0D-4) THEN
            IF (FIRST) THEN
               WRITE(6,*) 'IMAGINARY EIGENVALUES FOUND '
            ENDIF
            FIRST = .FALSE.
            WRITE(6,'(I5,3X,2F12.7)') I, BUF(I000+I-1), BUF(I010+I-1)
            BUF(I000+I-1) = 1.0D30
         ENDIF
    5  CONTINUE
C
       IF (IPROJECT .GE. 1) THEN
C
C DETERMINE NORMS OF VECTORS PROJECTED ON EXCITATION PATTERN
C
      CALL BLKCPY2(P,MAXORD,MAXORD,BUF3,NDIMR,NDIMR,1,1)
      Write(6,"(a)") " The p-matrix"
      call output(buf3,1,ndimr,1,ndimr,ndimr,ndimr,1)
      CALL XGEMM('N','N',NDIMR,NDIMR,NDIMR,1.0D0,BUF3,NDIMR,
     $   BUF(IEVEC),NDIMR,0.0D0,BUF(I030),NDIMR)
      DO 10 I=1,NDIMR
         CALL XGEMM('T','N',1,1,NDIMR,1.0D0,BUF(IEVEC+(I-1)*NDIMR),
     $      NDIMR,BUF(I030+(I-1)*NDIMR),NDIMR,ZERO,BUF(IEXCP+I-1),1)
 10   CONTINUE
C
      DO 20 I = 1, NDIMR
         IF (BUF(IEXCP+I-1) .GT. EXCPTHRS) THEN
            BUF(IEVALSEL+I-1) = BUF(IEVAL+I-1)
         ELSE
            BUF(IEVALSEL+I-1) = 1.0D12 * ABS(1.0d0 - BUF(IEXCP+I-1))
         ENDIF
   20  CONTINUE
C
       ELSE
         DO I = 1, NDIMR
           BUF(IEVALSEL+I-1) = BUF(IEVAL+I-1)
         ENDDO
       ENDIF
C      
C       IF (IFLAGS(1) .ge. 10 .or. (ndimr .eq. Nsizec)) THEN
       WRITE(6,1101)
1101   FORMAT(/,T3,' Subspace eigenvalues: ')
       WRITE(6,'(5f12.6)')(BUF(I),I=I000,I000+NDIMR-1)
       WRITE(6,*)' CORRECTED SUBSPACE EIGENVALUES'
       WRITE(6,'(5e12.6)')(BUF(I),I=IEVALSEL,IEVALSEL+NDIMR-1)
       WRITE(6,*)' NORM OF EXCITATION PATTERN PROJECTED VECTORS'
       WRITE(6,'(5f12.6)')(BUF(I),I=IEXCP,IEXCP+NDIMR-1)
       WRITE(6, 1300)
C       ENDIF
C
 1300 FORMAT(/)
      RETURN
      END
