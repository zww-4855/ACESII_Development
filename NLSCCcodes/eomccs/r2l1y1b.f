      SUBROUTINE R2L1Y1B(XI1,L1,ICORE,MAXCOR,IUHF)
C
C XI1(ai) = -1/2 L(if)*R(mn,ef)*W(mn,ea)
C
C SPIN CASES:
C
C XI1(AI) = -1/2 L(IF)*R(MN,EF)*W(MN,EA) - L(IF)*R(mN,eF)*W(mN,eA)
C
C        = L(IF)*G(FA)
C  
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE
      DIMENSION ICORE(MAXCOR),XI1(*),L1(*)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE/1.0D0/
C
      IOFFL=1
      IOFFXI=1  
      DO 10 ISPIN=1,1+IUHF
       I0G=1
       I000=I0G+IINTFP*IRPDPD(IRREPX,18+ISPIN)
C
C FIRST PICK UP G(af) FROM LIST 492
C
       CALL GETLST(ICORE(I0G),1,1,1,ISPIN,492)
C
C FORM PRODUCT XI1(ai) = G(af)*L(fi)
C
       DO 11 IRREPI=1,NIRREP
        IRREPA=IRREPI
        IRREPF=DIRPRD(IRREPI,IRREPX)
        NUMA=VRT(IRREPA,ISPIN)
        NUMF=VRT(IRREPF,ISPIN)
        NUMI=POP(IRREPI,ISPIN)
        IOFFG=I0G+(ISYMOFF(IRREPF,IRREPX,18+ISPIN)-1)*IINTFP
        CALL XGEMM('N','N',NUMA,NUMI,NUMF,ONE,ICORE(IOFFG),NUMA,
     &             L1(IOFFL),NUMF,ONE,XI1(IOFFXI),NUMA)
        IOFFL=IOFFL+IINTFP*NUMF*NUMI
        IOFFXI=IOFFXI+IINTFP*NUMA*NUMI
11     CONTINUE
C
10    CONTINUE
C
      RETURN
      END
