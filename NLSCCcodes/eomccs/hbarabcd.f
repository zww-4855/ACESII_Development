      SUBROUTINE HBARABCD(ICORE,MAXCOR,IUHF, ADD)
C
C THIS ROUTINE CALCULATES THE TAU*W CONTRIBUTION TO THE
C ABCD HBAR MATRIX ELEMENTS
C
C     TAU(AB,MN) * <MN||EF> [ISPIN=1]
C     TAU(ab,mn) * <mn||ef> [ISPIN=2]
C     TAU(Ab,Mn) * <Mn|Ef>  [ISPIN=3]
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE, FAC
      LOGICAL ADD
      DIMENSION ICORE(MAXCOR),IOFFT1(8,2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      DATA ONE /1.0D0/
C
      IF (ADD) THEN
         FAC = 1.D0
      ELSE
         FAC = -1.D0
      ENDIF
C
      CALL GETT1(ICORE,MAXCOR,MXCOR,IUHF,IOFFT1)
C
      DO 10 ISPIN=3,3-2*IUHF,-1
C
       LISTT=43+ISPIN
       LISTW=13+ISPIN
       LISTTAR=230+ISPIN
       DO 20 IRREPDO=1,NIRREP
        DISSYT=IRPDPD(IRREPDO,ISYTYP(1,LISTT))
        NUMDST=IRPDPD(IRREPDO,ISYTYP(2,LISTT))
        DISSYW=IRPDPD(IRREPDO,ISYTYP(1,LISTW))
        NUMDSW=IRPDPD(IRREPDO,ISYTYP(2,LISTW))
        IF(DISSYW.EQ.0)GOTO 20
        I000=1
        I010=I000+IINTFP*DISSYT*NUMDST
        I020=I010+IINTFP*DISSYW*NUMDSW
        CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREPDO,LISTW)
        CALL TRANSP(ICORE(I000), ICORE(I010), NUMDSW, DISSYW)
C
        CALL GETLST(ICORE(I000),1,NUMDST,1,IRREPDO,LISTT)
        IF(ISPIN.LT.3)THEN
         I1=ISPIN
         I2=ISPIN
        ELSE
         I1=1
         I2=2
        ENDIF
        CALL FTAU(ICORE(I000),ICORE(IOFFT1(1,I1)),ICORE(IOFFT1(1,I2)),
     &     DISSYT,NUMDST,
     &     POP(1,I1),POP(1,I2),VRT(1,I1),VRT(1,I2),
     &     IRREPDO,ISPIN,ONE)
C
C DETERMINE HOW MANY BATCHES OF ABCD TARGET CAN BE PROCESSED AT ONE
C TIME AND DO THE WORK.
C
        CORLFT=MXCOR-I020+1
        NINCOR=CORLFT/(DISSYW*IINTFP)
        IF(NINCOR.EQ.0)THEN
         WRITE(6,100)
100      FORMAT(T3,'@HBARABCD-F, Not enough memory.')
         CALL INSMEM('HBARABCD',CORLFT,DISSYW)
        ENDIF
        IOFFT=I000
        IOFFW=I010
        NLEFT =DISSYW
        IF (NINCOR.LT.NLEFT) WRITE(6,*) 'OUT OF CORE ALGORITHM'
        NFIRST=1
1       NREAD =MIN(NINCOR,NLEFT)
        CALL GETLST(ICORE(I020),NFIRST,NREAD,1,IRREPDO,LISTTAR)
        CALL XGEMM ('N','N',DISSYT,NREAD,NUMDSW,FAC,ICORE(IOFFT),
     &              DISSYT,ICORE(IOFFW),NUMDSW,ONE,ICORE(I020),DISSYT)
        CALL PUTLST(ICORE(I020),NFIRST,NREAD,1,IRREPDO,LISTTAR)
        NLEFT=NLEFT-NREAD
        IF(NLEFT.NE.0)THEN
         NFIRST=NFIRST+NREAD
         IOFFW =IOFFW+NREAD*IINTFP*NUMDSW
         GOTO 1
        ENDIF
20     CONTINUE
10    CONTINUE
C
      RETURN
      END
