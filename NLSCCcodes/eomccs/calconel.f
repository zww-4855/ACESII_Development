      SUBROUTINE CALCONEL(DOO,DVV,DVO,DOV,BUF,IUHF,EONEL)
C
C CALCULATES THE ONE-ELECTRON CONTRIBUTION TO THE EXCITATION
C ENERGY D(pq)*f(pq), ASSUMING THAT A CANONICAL SCF REFERENCE
C STATE
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*8 LABEL(2)
      DIMENSION DOO(*),DVV(*),DVO(*),DOV(*),BUF(*)
      INTEGER POP,VRT,DIRPRD
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON/FLAGS/IFLAGS(100)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/ADD/SUM
C
      DATA LABEL /'SCFEVALA','SCFEVALB'/
C
      NBAS=NOCCO(1)+NVRTO(1)
C
      EONEL=0.0D0
      FACT=1.0D0+DFLOAT(1-IUHF)
      IOFFOO=1
      IOFFVV=1
      DO 10 ISPIN=1,1+IUHF
       IOFFBUFO=1
       IOFFBUFV=NOCCO(ISPIN)+1
       CALL GETREC(20,'JOBARC',LABEL(ISPIN),IINTFP*NBAS,BUF)
       DO 20 IRREP=1,NIRREP
        NOCC=POP(IRREP,ISPIN)
        NVRT=VRT(IRREP,ISPIN)
        EOO=FACT*SDOT(NOCC,BUF(IOFFBUFO),1,DOO(IOFFOO),NOCC+1)
        EVV=FACT*SDOT(NVRT,BUF(IOFFBUFV),1,DVV(IOFFVV),NVRT+1)
        EONEL=EONEL+EOO+EVV
        IOFFBUFO=IOFFBUFO+NOCC
        IOFFBUFV=IOFFBUFV+NVRT
        IOFFOO=IOFFOO+NOCC*NOCC
        IOFFVV=IOFFVV+NVRT*NVRT
20     CONTINUE
C
C TAKE CARE OFF OFF-DIAGONAL FOCK MATRIX PIECES
C
       IF(IFLAGS(38).NE.0)THEN
        IOFF1=1+(ISPIN-1)*NFMI(1)
        IOFF2=1+(ISPIN-1)*NFEA(1)
        IOFF3=1+(ISPIN-1)*NT(1)
        CALL GETLST(BUF,1,1,1,4+ISPIN,91)
        EONEL=EONEL+FACT*SDOT(NFMI(ISPIN),BUF,1,DOO(IOFF1),1)
        CALL GETLST(BUF,1,1,1,4+ISPIN,92)
        EONEL=EONEL+FACT*SDOT(NFEA(ISPIN),BUF,1,DVV(IOFF2),1)
        CALL GETLST(BUF,1,1,1,4+ISPIN,93)
        EONEL=EONEL+FACT*SDOT(NT(ISPIN),BUF,1,DVO(IOFF3),1)
        EONEL=EONEL+FACT*SDOT(NT(ISPIN),BUF,1,DOV(IOFF3),1)
       ENDIF
C
10    CONTINUE
C
      CALL GETREC(20,'JOBARC','SCFENEG ',IINTFP,ESCF)
      SUM=ESCF+EONEL
C
      write(6,*)' Sum of reference state and one-density contributions 
     &          to E = ',sum
      RETURN
      END
