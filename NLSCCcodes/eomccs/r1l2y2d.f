      SUBROUTINE R1L2Y2D(R1,ICORE,MAXCOR,IUHF)
C
C Y1(ab,ij) = L(ab,mn)*R(ne)*W(ij,me)
C
C  RHF : SPIN-ADAPTED CODE
C  UHF : ALL SPIN CASES ARE EXPLICITELY CONSIDERED
C  
CEND
C
C CODED SEPTEMBER/93 JFS AND JG
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ZILCH
C
      DIMENSION ICORE(MAXCOR),R1(*)
      DIMENSION IOFFRR(2)
C
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ZILCH/1.0D0,0.0D0/
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE.
C
C  Y1(Ab,Ij) = Q(Ab,Ij) + Q(Ba,Ji)
C
C WHERE
C
C  Q(Ab,Ij) = W(Ij,Ne) * R(em) * L(Ab,Nm)
C
       LISTL=446
       LISTZ=63
       LISTW=10
       DO 10 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPX)
        DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
        NUMDSW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
        DISSYL=IRPDPD(IRREPR,ISYTYP(1,LISTL))
        NUMDSL=IRPDPD(IRREPL,ISYTYP(2,LISTL))
        DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
        NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
        DISSYX=DISSYW
        NUMDSX=NUMDSL
        MAXL  =MAX(DISSYL,NUMDSL,DISSYX,NUMDSX,DISSYZ,NUMDSZ)
        I000=1
        I010=I000+IINTFP*MAX(DISSYW*NUMDSW,DISSYL*NUMDSL,
     &                       DISSYZ*NUMDSZ,3*MAXL)
        I020=I010+IINTFP*DISSYX*NUMDSX
        I030=I020+IINTFP*DISSYZ*NUMDSZ
        ITMP1=I000
        ITMP2=ITMP1+IINTFP*MAXL
        ITMP3=ITMP2+IINTFP*MAXL
        CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREPR,LISTW)
C
C FORM X(Ij,Nm) = W(Ij,Ne) * R(em)
C
        DO 20 IRREPM=1,NIRREP
         IRREPE=DIRPRD(IRREPM,IRREPX)
         IRREPN=DIRPRD(IRREPE,IRREPR)
         NUMM=POP(IRREPM,1)
         NUME=VRT(IRREPE,1)
         NUMN=POP(IRREPN,1)
         NROW=DISSYW*NUMN
         NCOL=NUMM
         NSUM=NUME
         IOFFW=I000+IINTFP*(ISYMOFF(IRREPE,IRREPR,18)-1)*DISSYW
         IOFFX=I010+IINTFP*(ISYMOFF(IRREPM,IRREPL,21)-1)*DISSYX
         IOFFR=1   +IINTFP*(ISYMOFF(IRREPM,IRREPX,9)-1)
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),
     &              NROW,R1(IOFFR),NSUM,ZILCH,ICORE(IOFFX),
     &              NROW)
20      CONTINUE    
C
C NOW READ IN L(Ab,Nm)
C
        CALL GETLST(ICORE(I000),1,NUMDSL,1,IRREPL,LISTL)
C
C   Q(Ba,Ij) = L(Ab,Nm) * X(Ij,Nm)
C
        CALL XGEMM('N','T',DISSYZ,NUMDSZ,NUMDSL,ONE,ICORE(I000),
     &             DISSYL,ICORE(I010),DISSYX,ZILCH,ICORE(I020),
     &             DISSYZ)

        CALL SYMRHF(IRREPR,VRT(1,1),POP(1,1),DISSYZ,ICORE(I020),
     &              ICORE(ITMP1),ICORE(ITMP2),ICORE(ITMP3))
        CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I020),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
10     CONTINUE
C
       ELSE  
C
C GET OFFSETS FOR R1:
C
        IOFFRR(1)=1
        IOFFRR(2)=1+IINTFP*IRPDPD(IRREPX,9)
C
C DO FIRST SPIN CASES AAAA AND BBBB
C
        DO 1000 ISPIN=1,2
C
         LISTL=443+ISPIN
         LISTZ=60+ISPIN
         LISTW=6+ISPIN
C
         DO 1010 IRREPR=1,NIRREP
C
          IRREPL=DIRPRD(IRREPR,IRREPX)
C
          DISSYL=IRPDPD(IRREPR,ISYTYP(1,LISTL))
          NUMDSL=IRPDPD(IRREPL,ISYTYP(2,LISTL))
          DISSYX=IRPDPD(IRREPR,ISYTYP(1,LISTW))
          NUMDSX=IRPDPD(IRREPL,20+ISPIN)
          DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
          NUMDSW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
          DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
          NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
C
          I000=1
          I010=I000+IINTFP*MAX(NUMDSX*DISSYX,NUMDSZ*DISSYZ)
          IEND=I010+IINTFP*MAX(NUMDSW*DISSYW,NUMDSL*DISSYW)
          IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2C',IEND,MAXCOR)
C
          CALL GETLST(ICORE(I010),1,NUMDSW,1,IRREPR,LISTW)
C
C FORM X(IJ,NM) = W(IJ,NE) * R(EM)
C
          DO 1020 IRREPM=1,NIRREP
           IRREPE=DIRPRD(IRREPM,IRREPX)
           IRREPN=DIRPRD(IRREPE,IRREPR)
           NUMM=POP(IRREPM,ISPIN)
           NUME=VRT(IRREPE,ISPIN)
           NUMN=POP(IRREPN,ISPIN)
           NROW=DISSYW*NUMN
           NCOL=NUMM
           NSUM=NUME
           IOFFW=I010+IINTFP*(ISYMOFF(IRREPE,IRREPR,15+ISPIN)-1)*DISSYW
           IOFFQ=I000+IINTFP*(ISYMOFF(IRREPM,IRREPL,20+ISPIN)-1)*DISSYX
           IOFFR=IOFFRR(ISPIN)+IINTFP*(ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1)
           CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),
     &                NROW,R1(IOFFR),NSUM,ZILCH,ICORE(IOFFQ),
     &                NROW)
1020      CONTINUE
C
          CALL ASSYM(IRREPL,POP(1,ISPIN),DISSYW,DISSYW,
     &               ICORE(I010),ICORE(I000))
          CALL SCOPY(DISSYW*NUMDSL,ICORE(I010),1,ICORE(I000),1)
C
          I020=I010+IINTFP*NUMDSL*DISSYL
          IEND=I020+IINTFP*NUMDSZ*DISSYZ
          IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2D',IEND,MAXCOR)
C
          CALL GETLST(ICORE(I010),1,NUMDSL,1,IRREPL,LISTL)
          CALL XGEMM('N','T',DISSYZ,NUMDSZ,NUMDSL,ONE,ICORE(I010),
     &               DISSYL,ICORE(I000),DISSYX,ZILCH,ICORE(I020),
     &               DISSYZ)
C
          CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
          CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I020),1,ICORE(I000),1)
          CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
1010     CONTINUE
C
1000    CONTINUE
C
C DO ABAB SPIN CASE 
C
        LISTL=446
        LISTZ=63
        LISTW1=10
        LISTW2=9
C 
        DO 2010 IRREPR=1,NIRREP
C 
         IRREPL=DIRPRD(IRREPR,IRREPX)
C 
         DISSYL=IRPDPD(IRREPR,ISYTYP(1,LISTL))
         NUMDSL=IRPDPD(IRREPL,ISYTYP(2,LISTL))
         DISSYX=IRPDPD(IRREPR,ISYTYP(1,LISTW1))
         NUMDSX=IRPDPD(IRREPL,ISYTYP(2,LISTL))
         DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW1))
         NUMDSW1=IRPDPD(IRREPR,ISYTYP(2,LISTW1))
         NUMDSW2=IRPDPD(IRREPR,ISYTYP(2,LISTW2))
         DISSYZ=IRPDPD(IRREPR,ISYTYP(1,LISTZ))
         NUMDSZ=IRPDPD(IRREPR,ISYTYP(2,LISTZ))
C
         I000=1
         I010=I000+IINTFP*MAX(NUMDSX*DISSYX,NUMDSZ*DISSYZ)
         IEND=I010+IINTFP*NUMDSW1*DISSYW
         IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2C',IEND,MAXCOR)
C
         CALL GETLST(ICORE(I010),1,NUMDSW1,1,IRREPR,LISTW1)
C
C FORM X(Ij,Nm) = W(Ij,Ne) * R(em)
C
         DO 2020 IRREPM=1,NIRREP
          IRREPE=DIRPRD(IRREPM,IRREPX)
          IRREPN=DIRPRD(IRREPE,IRREPR)
          NUMM=POP(IRREPM,2)
          NUME=VRT(IRREPE,2)
          NUMN=POP(IRREPN,1)
          NROW=DISSYW*NUMN
          NCOL=NUMM
          NSUM=NUME
          IOFFW=I010+IINTFP*(ISYMOFF(IRREPE,IRREPR,18)-1)*DISSYW
          IOFFQ=I000+IINTFP*(ISYMOFF(IRREPM,IRREPL,14)-1)*DISSYX
          IOFFR=IOFFRR(2)+IINTFP*(ISYMOFF(IRREPM,IRREPX,10)-1)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),
     &               NROW,R1(IOFFR),NSUM,ZILCH,ICORE(IOFFQ),
     &               NROW)
2020     CONTINUE
C
         I020=I010+IINTFP*NUMDSL*DISSYL
         IEND=I020+IINTFP*NUMDSZ*DISSYZ
         IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2D',IEND,MAXCOR)
C
         CALL GETLST(ICORE(I010),1,NUMDSL,1,IRREPL,LISTL)
C
         CALL XGEMM('N','T',DISSYZ,NUMDSZ,NUMDSL,
     &              ONE,ICORE(I010),DISSYL,ICORE(I000),
     &              DISSYX,ZILCH,ICORE(I020),DISSYZ)
C
         CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
         CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I020),1,ICORE(I000),1)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
         I000=1
         I010=I000+IINTFP*MAX(DISSYX*NUMDSX,NUMDSZ*DISSYZ)
         ITMP=I010+IINTFP*NUMDSW2*DISSYW
         IEND=ITMP+3*IINTFP*MAX(NUMDSW2,DISSYW,NUMDSL,DISSYL)
         IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2D',IEND,MAXCOR)
C
         CALL GETLST(ICORE(I010),1,NUMDSW2,1,IRREPR,LISTW2)
C
         CALL SYMTR1(IRREPR,VRT(1,1),POP(1,2),DISSYW,ICORE(I010),
     &               ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYW),
     &               ICORE(ITMP+2*IINTFP*DISSYW))
C
C FORM X(Ij,Mn) = W(Ij,En) * R(EM)
C
         DO 2030 IRREPM=1,NIRREP
          IRREPE=DIRPRD(IRREPM,IRREPX)
          IRREPN=DIRPRD(IRREPE,IRREPR)
          NUMM=POP(IRREPM,1)
          NUME=VRT(IRREPE,1)
          NUMN=POP(IRREPN,2)
          NROW=DISSYW*NUMN
          NCOL=NUMM
          NSUM=NUME
          IOFFW=I010+IINTFP*(ISYMOFF(IRREPE,IRREPR,25)-1)*DISSYW
          IOFFQ=I000+IINTFP*(ISYMOFF(IRREPM,IRREPL,24)-1)*DISSYX
          IOFFR=IOFFRR(1)+IINTFP*(ISYMOFF(IRREPM,IRREPX,9)-1)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFW),
     &               NROW,R1(IOFFR),NSUM,ZILCH,ICORE(IOFFQ),
     &               NROW)
2030     CONTINUE
C
         CALL SYMTR1(IRREPL,POP(1,2),POP(1,1),DISSYX,ICORE(I000),
     &               ICORE(ITMP),ICORE(ITMP+IINTFP*DISSYX),
     &               ICORE(ITMP+2*IINTFP*DISSYX))
C
         I020=I010+IINTFP*NUMDSL*DISSYL
         IEND=I020+IINTFP*NUMDSZ*DISSYZ
         IF(IEND.GE.MAXCOR) CALL INSMEM('R1L2Y2D',IEND,MAXCOR)
C
         CALL GETLST(ICORE(I010),1,NUMDSL,1,IRREPL,LISTL)
         CALL XGEMM('N','T',DISSYZ,NUMDSZ,NUMDSL,ONE,ICORE(I010),
     &              DISSYL,ICORE(I000),DISSYX,ZILCH,ICORE(I020),
     &              DISSYZ)
C
         CALL GETLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
         CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I020),1,ICORE(I000),1)
         CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPR,LISTZ)
C
2010    CONTINUE
C
       ENDIF
C
C ALL DONE, RETURN
C
       RETURN
       END 
