      SUBROUTINE TPDABCI5(ICORE,MAXCOR,IUHF)
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION TWO,ONEM
      DIMENSION ICORE(MAXCOR)
      DIMENSION I0R(2),I0G(2)
C
C CALCULATION OF THE FIFTH ABCI CONTRIBUTION TO
C THE EOM-CCSD TWO-PARTICLE DENSITY MATRIX
C
C  G(AB,CI) = 1/4 P(AB) T(MN,EA)*L(EC,MN)*R(I,B)
C
C           = P(AB) * X(AC)*R(I,B) 
C
C  WHERE THE X ARE CALCULATED BY GFORMG.
C
C ALSO, ONLY THE SUM OF G(AB,CI) AND G(AI,BC) IS STORED ON DISK
C
CEND 
C
C CODED JG SEPTEMBER/93
C
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA TWO,ONEM /2.0D0,-1.0D0/
C
C CALL GFORMG TO GET THE X INTERMEDIATES
C
      CALL GFORMG(IRREPX,1,444,44,400,ICORE,MAXCOR,2,ONEM,ONEM,IUHF)
C
C READ IN R1 AND X1 AMPLITUDES AND TRANSPOSE THE FORMER TO I,A 
C ORDERING
C
      I0R(1)=1
      I0R(2)=I0R(1)+IRPDPD(IRREPX,9)*IINTFP*IUHF
      I0G(1)=I0R(2)+IRPDPD(IRREPX,10)*IINTFP
      I0G(2)=I0G(1)+IRPDPD(IRREPX,19)*IINTFP*IUHF
      ISTART=I0G(2)+IRPDPD(IRREPX,20)*IINTFP
      I000=ISTART
      MXCOR=MAXCOR-ISTART+1
C
      CALL GETLST(ICORE(ISTART),1,1,1,3,490)
      CALL SYMTRA(IRREPX,VRT(1,1),POP(1,1),1,ICORE(ISTART),
     &            ICORE(I0R(1)))
      CALL GETLST(ICORE(I0G(1)),1,1,1,3,492)
      IF(IUHF.EQ.1) THEN
       CALL GETLST(ICORE(ISTART),1,1,1,4,490)
       CALL SYMTRA(IRREPX,VRT(1,2),POP(1,2),1,ICORE(ISTART),
     &             ICORE(I0R(2)))
       CALL GETLST(ICORE(I0G(2)),1,1,1,4,492)
      ENDIF
C
C FORM G*R TERMS
C
       IF(IUHF.EQ.1) THEN
C
C AAAA AND BBBB SPIN CASES
C
        DO 2000 ISPIN=1,2
         LISTG=126+ISPIN
         DO 2100 IRREP=1,NIRREP
          NUMDSG=IRPDPD(IRREP,ISYTYP(2,LISTG))
          DISSYG=IRPDPD(IRREP,ISYTYP(1,LISTG))
          NINCOR=MXCOR/(IINTFP*MAX(1,DISSYG))
          NLEFT =NUMDSG
          IFIRST=1
1         NREAD=MIN(NINCOR,NLEFT)
          CALL GETLST(ICORE(I000),IFIRST,NREAD,1,IRREP,LISTG)
          CALL GTTAU2(ICORE(I000),ICORE(I0G(ISPIN)),ICORE(I0R(ISPIN)),
     &                DISSYG,NUMDSG,VRT(1,ISPIN),POP(1,ISPIN),
     &                VRT(1,ISPIN),IRREPX,IRREP,ISPIN,IFIRST,NREAD)
          CALL PUTLST(ICORE(I000),IFIRST,NREAD,1,IRREP,LISTG)
          IFIRST=IFIRST+NREAD
          NLEFT=NLEFT-NREAD
          IF(NLEFT.NE.0)GOTO 1
2100     CONTINUE
2000    CONTINUE
       ENDIF
C
       DO 3000 ISPIN=1,1+IUHF
        LISTG=131-ISPIN
        DO 3100 IRREP=1,NIRREP
         NUMDSG=IRPDPD(IRREP,ISYTYP(2,LISTG))
         DISSYG=IRPDPD(IRREP,ISYTYP(1,LISTG))
         NINCOR=MXCOR/(IINTFP*MAX(1,DISSYG))
         NLEFT =NUMDSG
         IFIRST=1
2        NREAD=MIN(NINCOR,NLEFT)
         CALL GETLST(ICORE(I000),IFIRST,NREAD,1,IRREP,LISTG)
         CALL GTTAU2(ICORE(I000),ICORE(I0G(ISPIN)),
     &               ICORE(I0R(3-ISPIN)),DISSYG,NUMDSG,
     &               VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,3-ISPIN),
     &               IRREPX,IRREP,2+ISPIN,IFIRST,NREAD)
         CALL PUTLST(ICORE(I000),IFIRST,NREAD,1,IRREP,LISTG)
         IFIRST=IFIRST+NREAD
         NLEFT=NLEFT-NREAD
         IF(NLEFT.NE.0)GOTO 2
3100    CONTINUE
3000   CONTINUE
C
C ALL DONE, RETURN
C
      TWO=0.D0
      if(iuhf.eq.0) then
       call checkgam1(icore,30,130,two,iuhf,2,vrt)
      endif
      IF(IUHF.EQ.1) THEN
       CALL CHECKGAM(ICORE,30,130,TWO)
       CALL CHECKGAM(ICORE,27,127,TWO)
       CALL CHECKGAM(ICORE,28,128,TWO)
       CALL CHECKGAM(ICORE,29,129,TWO)
      ENDIF
C
      RETURN
      END
