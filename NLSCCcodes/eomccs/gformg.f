      SUBROUTINE GFORMG(IRREPC,IRREPT,LISTC0,LISTT0,LISTG,ICORE,
     &                  MAXCOR,IOF0,Gae_scale,Gmi_scale,IUHF)
C
C THIS ROUTINE CARRIES OUT THE FOLLOWING TWO CONTRACTIONS :
C
C       G(EB) = - SUM C(MN,BF) * T(MN,EF) + C(Mn,Bf) * T(Mn,Ef)   [UHF]
C       G(eb) = - SUM C(mn,bf) * T(mn,ef) + C(Nm,Fb) * T(Nm,Fe)   [UHF]
C       
C       G(JM) = SUM C(EF,NJ) * T(EF,NM) + C(Fe,Jn) * T(Fe,Mn)   [UHF]
C       G(jm) = SUM C(ef,nj) * T(ef,nm) + C(Ef,Nj) * T(Ef,Nm)   [UHF]
C
C       G(eb) = - SUM C(Mn,Bf) * [2 T(Mn,Ef) - T(Nm,Ef)]          [RHF]
C
C       G(jm) = SUM C(Ef,Nj) * [2 T(Ef,Nm) - T(Fe,Nm)]          [RHF]
C
C AND ALLOWS THE VECTORS C AND T TO HAVE ARBITRARY OVERALL SYMMETRIES.
C THE RESULTANT G VECTORS [WHICH HAVE SYMMETRY DIRPRD(IRREPT,IRREPC)]
C ARE WRITTEN TO LISTS LISTG+91 AND LISTG+92.
C
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,Gae_scale,Gmi_scale
      DIMENSION ICORE(MAXCOR)
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM /1.0D0,-1.0D0/
C
      IRREPG=DIRPRD(IRREPT,IRREPC)
C
      IF(IUHF.EQ.0)THEN
C
C ALLOCATE MEMORY FOR SINGLES (C) ARRAY AND TARGET ARRAYS
C
       LENGAB=IRPDPD(IRREPG,19)
       LENGIJ=IRPDPD(IRREPG,21)
       I0GAB=1
       I0GIJ=I0GAB+IINTFP*LENGAB
       I000 =I0GIJ+IINTFP*LENGIJ
C
       LISTT=LISTT0+2
       LISTC=LISTC0+2
C
       CALL ZERO(ICORE(I0GAB),LENGAB)
       CALL ZERO(ICORE(I0GIJ),LENGIJ)
C
       DO 5 IRREPTR=1,NIRREP
        IRREPTL=DIRPRD(IRREPTR,IRREPT)
        DISSYT=IRPDPD(IRREPTL,ISYTYP(1,LISTT))
        NUMDST=IRPDPD(IRREPTR,ISYTYP(2,LISTT))
        MAXT=MAX(DISSYT,NUMDST)
        I010=I000+IINTFP*DISSYT*NUMDST
        CALL GETLST(ICORE(I000),1,NUMDST,1,IRREPTR,LISTT)
C
C SPIN-ADAPT INTEGRALS
C
        ITMP1=I010
        ITMP2=ITMP1+IINTFP*MAXT
        CALL SPINAD3(IRREPTL,VRT(1,1),DISSYT,NUMDST,ICORE(I000),
     &               ICORE(ITMP1),ICORE(ITMP2)) 
C
C FIRST EVALUATE G(mj) :
C
        IRREPCL=IRREPTL
        IRREPCR=DIRPRD(IRREPCL,IRREPC)
        DISSYC=IRPDPD(IRREPCL,ISYTYP(1,LISTC))
        NUMDSC=IRPDPD(IRREPCR,ISYTYP(2,LISTC))
        I020=I010+IINTFP*DISSYC*NUMDSC
C
        CALL GETLST(ICORE(I010),1,NUMDSC,1,IRREPCR,LISTC)
C
C PERFORM CONTRACTION AS :
C
C                +
C        T(EfN,m) * C(EfN,j)
C
        IOFFG0=I0GIJ
        IOFFT0=I000
        IOFFC0=I010
        DO 10 IRREPJ=1,NIRREP
         IRREPM=DIRPRD(IRREPJ,IRREPG)
         IRREPN=DIRPRD(IRREPM,IRREPTR)
         NUMM=POP(IRREPM,2)
         NUMJ=POP(IRREPJ,2)
         NUMN=POP(IRREPN,1)
         NROW=NUMM
         NCOL=NUMJ
         NSUM=DISSYT*NUMN
         IOFFG=IOFFG0+IINTFP*(ISYMOFF(IRREPJ,IRREPG,21)-1)
         IOFFT=IOFFT0+IINTFP*DISSYT*(ISYMOFF(IRREPM,IRREPTR,21)-1)
         IOFFC=IOFFC0+IINTFP*DISSYC*(ISYMOFF(IRREPJ,IRREPCR,21)-1)
         CALL XGEMM('T','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFT),NSUM,
     &              ICORE(IOFFC),NSUM,ONE,ICORE(IOFFG),NROW)
10      CONTINUE
C
C NOW EVALUATE G(eb):
C
        CALL TRANSP(ICORE(I000),ICORE(I010),NUMDST,DISSYT)
        CALL SCOPY (NUMDST*DISSYT,ICORE(I010),1,ICORE(I000),1)
        IRREPCR=IRREPTR
        IRREPCL=DIRPRD(IRREPCR,IRREPC)
        DISSYC=IRPDPD(IRREPCL,ISYTYP(1,LISTC))
        NUMDSC=IRPDPD(IRREPCR,ISYTYP(2,LISTC))
        I020=I010+IINTFP*DISSYC*NUMDSC
C
        CALL GETLST(ICORE(I010),1,NUMDSC,1,IRREPCR,LISTC)
        CALL TRANSP(ICORE(I010),ICORE(I020),NUMDSC,DISSYC)
        CALL SCOPY (NUMDSC*DISSYC,ICORE(I020),1,ICORE(I010),1)
C
C PERFORM CONTRACTION AS :
C
C                +
C        T(Mn,Fe) * C(Mn,Fb)
C
        IOFFG0=I0GAB
        IOFFT0=I000
        IOFFC0=I010
        DO 20 IRREPB=1,NIRREP
         IRREPE=DIRPRD(IRREPB,IRREPG)
         IRREPF=DIRPRD(IRREPE,IRREPTL)
         NUME=VRT(IRREPE,2)
         NUMB=VRT(IRREPB,2)
         NUMF=VRT(IRREPF,1)
         NROW=NUME
         NCOL=NUMB
         NSUM=NUMDST*NUMF
         IOFFT=IOFFT0+IINTFP*NUMDST*(ISYMOFF(IRREPE,IRREPTL,19)-1)
         IOFFC=IOFFC0+IINTFP*NUMDSC*(ISYMOFF(IRREPB,IRREPCL,19)-1)
         IOFFG=IOFFG0+IINTFP*(ISYMOFF(IRREPB,IRREPG,19)-1)
         CALL XGEMM('T','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NSUM,
     &              ICORE(IOFFC),NSUM,ONE,ICORE(IOFFG),NROW)
20      CONTINUE
C
5      CONTINUE
C
       CALL SSCAL (LENGIJ,Gmi_scale,ICORE(I0GIJ),1)
       CALL SSCAL (LENGAB,Gae_scale,ICORE(I0GAB),1)
       CALL PUTLST(ICORE(I0GIJ),1,1,1,1+IOF0,LISTG+91)
       CALL PUTLST(ICORE(I0GAB),1,1,1,1+IOF0,LISTG+92)
C
      ELSE
C
       DO 100 ISPIN=1,2
C
C ALLOCATE MEMORY FOR SINGLES (C) ARRAY AND TARGET ARRAYS
C
        LENGAB=IRPDPD(IRREPG,18+ISPIN)
        LENGIJ=IRPDPD(IRREPG,20+ISPIN)
        I0GAB=1
        I0GIJ=I0GAB+IINTFP*LENGAB
        I000 =I0GIJ+IINTFP*LENGIJ
C
        ISYTABAA=18+ISPIN
        ISYTIJAB=14
        ISYTIJBA=24
        ISYTABAB=13
        ISYTABBA=23
C
        LISTTAA=LISTT0-1+ISPIN
        LISTTAB=LISTT0-1+3
        LISTCAA=LISTC0-1+ISPIN
        LISTCAB=LISTC0-1+3
C
        CALL ZERO(ICORE(I0GIJ),LENGIJ)
        CALL ZERO(ICORE(I0GAB),LENGAB)
C
        DO 105 IRREPTR=1,NIRREP
         IRREPTL=DIRPRD(IRREPTR,IRREPT)
         IRREPCL=IRREPTL
         IRREPCR=DIRPRD(IRREPCL,IRREPC)
         DISSYT=IRPDPD(IRREPTL,ISYTYP(1,LISTTAB))
         NUMDST=IRPDPD(IRREPTR,ISYTYP(2,LISTTAB))
         DISSYC=IRPDPD(IRREPCL,ISYTYP(1,LISTCAB))
         NUMDSC=IRPDPD(IRREPCR,ISYTYP(2,LISTCAB))
         MAXT=MAX(DISSYT,NUMDST)
         I010=I000+IINTFP*DISSYT*NUMDST
         I020=I010+IINTFP*DISSYC*NUMDSC
         I030=I020+IINTFP*MAXT
         I040=I030+IINTFP*MAXT
         I050=I040+IINTFP*MAXT
C
C FIRST EVALUATE G(mj) ABAB CONTRIBUTION:
C
         CALL GETLST(ICORE(I000),1,NUMDST,1,IRREPTR,LISTTAB)
         CALL GETLST(ICORE(I010),1,NUMDSC,1,IRREPCR,LISTCAB)
C
         IF(ISPIN.EQ.1)THEN
          CALL SYMTR1(IRREPTR,POP(1,1),POP(1,2),DISSYT,ICORE(I000),
     &                ICORE(I020),ICORE(I030),ICORE(I040))
          CALL SYMTR1(IRREPCR,POP(1,1),POP(1,2),DISSYC,ICORE(I010),
     &                ICORE(I020),ICORE(I030),ICORE(I040))
         ENDIF
C
C PERFORM CONTRACTION AS :
C
C                +
C        T(Fe,nM) * C(Fe,nJ) [ISPIN=1]
C                +
C        T(EfN,m) * C(EfN,j) [ISPIN=2]
C
C
         IOFFG0=I0GIJ
         IOFFT0=I000
         IOFFC0=I010
         DO 110 IRREPJ=1,NIRREP
          IRREPM=DIRPRD(IRREPJ,IRREPG)
          IRREPN=DIRPRD(IRREPM,IRREPTR)
          NUMM=POP(IRREPM,ISPIN)
          NUMJ=POP(IRREPJ,ISPIN)
          NUMN=POP(IRREPN,3-ISPIN)
          NROW=NUMM
          NCOL=NUMJ
          NSUM=DISSYT*NUMN
          IOFFG=IOFFG0+IINTFP*(ISYMOFF(IRREPJ,IRREPG,20+ISPIN)-1)
          IF(ISPIN.EQ.1)THEN
           IOFFT=IOFFT0+IINTFP*DISSYT*
     &                  (ISYMOFF(IRREPM,IRREPTR,ISYTIJBA)-1)
           IOFFC=IOFFC0+IINTFP*DISSYC*
     &                  (ISYMOFF(IRREPJ,IRREPCR,ISYTIJBA)-1)
          ELSE
           IOFFT=IOFFT0+IINTFP*DISSYT*
     &                  (ISYMOFF(IRREPM,IRREPTR,ISYTIJAB)-1)
           IOFFC=IOFFC0+IINTFP*DISSYC*
     &                  (ISYMOFF(IRREPJ,IRREPCR,ISYTIJAB)-1)
          ENDIF
          CALL XGEMM('T','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFT),NSUM,
     &               ICORE(IOFFC),NSUM,ONE,ICORE(IOFFG),NROW)
110      CONTINUE
C
C NOW DO AAAA CONTRIBUTION
C
         DISSYT=IRPDPD(IRREPTL,ISYTYP(1,LISTTAA))
         NUMDST=IRPDPD(IRREPTR,ISYTYP(2,LISTTAA))
         NUMDSTX=IRPDPD(IRREPTR,20+ISPIN)
         DISSYC=IRPDPD(IRREPCL,ISYTYP(1,LISTCAA))
         NUMDSC=IRPDPD(IRREPCR,ISYTYP(2,LISTCAA))
         NUMDSCX=IRPDPD(IRREPCR,20+ISPIN)
         MAXT=MAX(DISSYT,NUMDST)
         I010=I000+IINTFP*DISSYT*NUMDSTX
         I020=I010+IINTFP*DISSYC*NUMDSCX
         I030=I020+IINTFP*MAXT
         I040=I030+IINTFP*MAXT
         I050=I040+IINTFP*MAXT
         CALL GETLST(ICORE(I000),1,NUMDST,1,IRREPTR,LISTTAA)
         CALL GETLST(ICORE(I010),1,NUMDSC,1,IRREPCR,LISTCAA)
         CALL SYMEXP(IRREPTR,POP(1,ISPIN),DISSYT,ICORE(I000))
         CALL SYMEXP(IRREPCR,POP(1,ISPIN),DISSYC,ICORE(I010))
C
C PERFORM CONTRACTION AS :
C
C                
C        T(E<F,NM) * C(E<F,NJ)
C
C
         IOFFG0=I0GIJ
         IOFFT0=I000
         IOFFC0=I010
         DO 120 IRREPJ=1,NIRREP
          IRREPM=DIRPRD(IRREPJ,IRREPG)
          IRREPN=DIRPRD(IRREPM,IRREPTR)
          NUMM=POP(IRREPM,ISPIN)
          NUMJ=POP(IRREPJ,ISPIN)
          NUMN=POP(IRREPN,ISPIN)
          NROW=NUMM
          NCOL=NUMJ
          NSUM=DISSYT*NUMN
          ISYT=20+ISPIN
          IOFFG=IOFFG0+IINTFP*(ISYMOFF(IRREPJ,IRREPG,ISYT)-1)
          IOFFT=IOFFT0+IINTFP*DISSYT*(ISYMOFF(IRREPM,IRREPTR,ISYT)-1)
          IOFFC=IOFFC0+IINTFP*DISSYC*(ISYMOFF(IRREPJ,IRREPCR,ISYT)-1)
          CALL XGEMM('T','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFT),NSUM,
     &               ICORE(IOFFC),NSUM,ONE,ICORE(IOFFG),NROW)
120      CONTINUE
C
C
C NOW EVALUATE G(EB) AND G(eb)
C
C ABAB CONTRIBUTION FIRST
C
C
         IRREPCR=IRREPTR
         IRREPCL=DIRPRD(IRREPCR,IRREPC)
         DISSYT=IRPDPD(IRREPTL,ISYTYP(1,LISTTAB))
         NUMDST=IRPDPD(IRREPTR,ISYTYP(2,LISTTAB))
         DISSYC=IRPDPD(IRREPCL,ISYTYP(1,LISTCAB))
         NUMDSC=IRPDPD(IRREPCR,ISYTYP(2,LISTCAB))
         MAXT=MAX(DISSYT,NUMDST)
         I010=I000+IINTFP*DISSYT*NUMDST
         I020=I010+IINTFP*DISSYC*NUMDSC
         I030=I020+IINTFP*MAX(MAXT,DISSYC*NUMDSC,DISSYT*NUMDST)
         I040=I030+IINTFP*MAXT
         I050=I040+IINTFP*MAXT
         CALL GETLST(ICORE(I020),1,NUMDST,1,IRREPTR,LISTTAB)
         CALL TRANSP(ICORE(I020),ICORE(I000),NUMDST,DISSYT)
C
         CALL GETLST(ICORE(I020),1,NUMDSC,1,IRREPCR,LISTCAB)
         CALL TRANSP(ICORE(I020),ICORE(I010),NUMDSC,DISSYC)
C
         IF(ISPIN.EQ.1)THEN
          CALL SYMTR1(IRREPTL,VRT(1,1),VRT(1,2),NUMDST,ICORE(I000),
     &                ICORE(I020),ICORE(I030),ICORE(I040))
          CALL SYMTR1(IRREPCL,VRT(1,1),VRT(1,2),NUMDSC,ICORE(I010),
     &                ICORE(I020),ICORE(I030),ICORE(I040))
         ENDIF
C
C PERFORM CONTRACTION AS :
C
C       G(BE) =  C(Mn,fB) * T(Mn,fE) [ISPIN=1]
C       G(be) =  C(Nm,Fb) * T(Nm,Fe) [ISPIN=2]
C
         IOFFG0=I0GAB
         IOFFT0=I000
         IOFFC0=I010
         DO 130 IRREPB=1,NIRREP
          IRREPE=DIRPRD(IRREPB,IRREPG)
          IRREPF=DIRPRD(IRREPE,IRREPTL)
          NUME=VRT(IRREPE,ISPIN)
          NUMB=VRT(IRREPB,ISPIN)
          NUMF=VRT(IRREPF,3-ISPIN)
          NROW=NUME
          NCOL=NUMB
          NSUM=NUMDST*NUMF
          IOFFG=IOFFG0+IINTFP*(ISYMOFF(IRREPB,IRREPG,18+ISPIN)-1)
          IF(ISPIN.EQ.1)THEN
           IOFFT=IOFFT0+IINTFP*NUMDST*
     &                  (ISYMOFF(IRREPE,IRREPTL,ISYTABBA)-1)
           IOFFC=IOFFC0+IINTFP*NUMDSC*
     &                  (ISYMOFF(IRREPB,IRREPCL,ISYTABBA)-1)
          ELSE
           IOFFT=IOFFT0+IINTFP*NUMDST*
     &                  (ISYMOFF(IRREPE,IRREPTL,ISYTABAB)-1)
           IOFFC=IOFFC0+IINTFP*NUMDSC*
     &                  (ISYMOFF(IRREPB,IRREPCL,ISYTABAB)-1)
          ENDIF
          CALL XGEMM('T','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NSUM,
     &               ICORE(IOFFC),NSUM,ONE,ICORE(IOFFG),NROW)
130      CONTINUE
C
C
C NOW DO AAAA CONTRIBUTION
C
         DISSYT=IRPDPD(IRREPTL,ISYTYP(1,LISTTAA))
         NUMDST=IRPDPD(IRREPTR,ISYTYP(2,LISTTAA))
         DISSYTX=IRPDPD(IRREPTL,18+ISPIN)
         DISSYC=IRPDPD(IRREPCL,ISYTYP(1,LISTCAA))
         NUMDSC=IRPDPD(IRREPCR,ISYTYP(2,LISTCAA))
         DISSYCX=IRPDPD(IRREPCL,18+ISPIN)
         MAXT=MAX(DISSYTX,DISSYCX,NUMDST,NUMDSC)
         I010=I000+IINTFP*DISSYTX*NUMDST
         I020=I010+IINTFP*DISSYCX*NUMDSC
         I030=I020+IINTFP*MAX(MAXT,NUMDST*DISSYT,NUMDSC*DISSYC)
         I040=I030+IINTFP*MAXT
         I050=I040+IINTFP*MAXT
         CALL GETLST(ICORE(I020),1,NUMDST,1,IRREPTR,LISTTAA)
         CALL TRANSP(ICORE(I020),ICORE(I000),NUMDST,DISSYT)
         CALL GETLST(ICORE(I020),1,NUMDSC,1,IRREPCR,LISTCAA)
         CALL TRANSP(ICORE(I020),ICORE(I010),NUMDSC,DISSYC)
         CALL SYMEXP(IRREPTL,VRT(1,ISPIN),NUMDST,ICORE(I000))
         CALL SYMEXP(IRREPCL,VRT(1,ISPIN),NUMDSC,ICORE(I010))
C
C PERFORM CONTRACTION AS :
C
C                
C        T(M<N,FE) * C(M<N,FB)
C
C
         IOFFG0=I0GAB
         IOFFT0=I000
         IOFFC0=I010
         DO 140 IRREPB=1,NIRREP
          IRREPE=DIRPRD(IRREPB,IRREPG)
          IRREPF=DIRPRD(IRREPE,IRREPTL)
          NUME=VRT(IRREPE,ISPIN)
          NUMB=VRT(IRREPB,ISPIN)
          NUMF=VRT(IRREPF,ISPIN)
          NROW=NUME
          NCOL=NUMB
          NSUM=NUMDST*NUMF
          IOFFG=IOFFG0+IINTFP*(ISYMOFF(IRREPB,IRREPG,18+ISPIN)-1)
          IOFFT=IOFFT0+IINTFP*NUMDST*
     &                 (ISYMOFF(IRREPE,IRREPTL,ISYTABAA)-1)
          IOFFC=IOFFC0+IINTFP*NUMDSC*
     &                 (ISYMOFF(IRREPB,IRREPCL,ISYTABAA)-1)
          CALL XGEMM('T','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NSUM,
     &               ICORE(IOFFC),NSUM,ONE,ICORE(IOFFG),NROW)
140      CONTINUE
C
C
105     CONTINUE
        CALL SSCAL (LENGIJ,Gmi_scale,ICORE(I0GIJ),1)
        CALL SSCAL (LENGAB,Gae_scale,ICORE(I0GAB),1)
        CALL PUTLST(ICORE(I0GIJ),1,1,1,ISPIN+IOF0,LISTG+91)
        CALL PUTLST(ICORE(I0GAB),1,1,1,ISPIN+IOF0,LISTG+92)
C
100    CONTINUE
C 
      ENDIF
C
      RETURN
      END
